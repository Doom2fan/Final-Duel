//===========================================================================
//
// Hell Knight
//
//===========================================================================
Actor FDHellKnight : HellKnight 30102
{
	//$category Final Duel-Original Monsters
	//$title Hell Knight
	Game Doom
	SpawnID 113
	Health 500
	GibHealth -250
	-BOSSDEATH
	SeeSound "knight/sight"
	ActiveSound "knight/active"
	PainSound "knight/pain"
	DeathSound "knight/death"
	HitObituary "$OB_KNIGHTHIT"
	Obituary "$OB_KNIGHT"
	bloodcolor "0 C8 0"
	DamageFactor RifledSlug, 1.5
	Species "Hell Noble"
	States
	{
	Spawn:
		BOS2 A 10 A_Look 
		Loop
	See:
		BOS2 AABBCCDD 3 A_Chase
		Loop
	Melee:
	Missile:
		BOS2 EF 8 A_FaceTarget
		BOS2 G 8 A_BruisAttack
		Goto See
	Pain:
		BOS2 H  2
		BOS2 H  2 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",500)
		BOS2 I 8
		BOS2 J 8 A_Scream
		BOS2 K 8
		BOS2 L 8 A_NoBlocking
		BOS2 MN 8
		BOS2 O -1
		Stop
	Death.Saw: // Sprites by Vader and Blox
		TNT1 AA 0 A_SpawnItemEx("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 32.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		BS22 A 6 
		BS22 B 6 A_Scream 
		BS22 C 6 A_Fall 
		BS22 DEF 6 
		BS22 G -1 
		Stop
	XDeath: // Sprites by Eriance
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",400)
		TNT1 AA 0 A_SpawnItemEx("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 32.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		BOS2 P 0 A_SpawnItemEx ("NashGore_Gib9", 0.0, 0.0, 0.0, 1.0, 1.0, 1.0, 0, SXF_USEBLOODCOLOR) //A_SpawnDebris("NashGore_Gib9",0)
		BOS2 P 0 A_SpawnItemEx ("NashGore_Gib10", 0.0, 0.0, 0.0, 1.0, 1.0, 1.0, 0, SXF_USEBLOODCOLOR) //A_SpawnDebris("NashGore_Gib10",0)
		BOS2 P 0 A_SpawnItemEx ("NashGore_Gib11", 0.0, 0.0, 0.0, 1.0, 1.0, 1.0, 0, SXF_USEBLOODCOLOR) //A_SpawnDebris("NashGore_Gib11",0)
		BOS2 P 0 A_SpawnItemEx ("NashGore_Gib11", 0.0, 0.0, 0.0, 1.0, 1.0, 1.0, 0, SXF_USEBLOODCOLOR) //A_SpawnDebris("NashGore_Gib11",0)
		BOS2 P 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		BOS2 P 5
		BOS2 Q 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		BOS2 Q 5 A_XScream
		BOS2 R 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		BOS2 R 5 A_NoBlocking
		BOS2 S 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		BOS2 STUVW 5
		BOS2 X -1  
		Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",400)
		TNT1 A 0 Thing_SetTranslation(0,7)
		BOS2 IJ 8
		BOS2 K 8 A_Scream
		TNT1 A 0 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BOS2 L 8 A_NoBlocking
		BOS2 MN 8 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BOS2 O 0
		BOS2 OOOOOOOOOOOOOOOOOO 5 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BOS2 OOOOOOOOOOOOOOOOOO 5 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BOS2 O -1
		stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",400)
		TNT1 A 0 Thing_SetTranslation(0,7)
		BOS2 IJ 8
		BOS2 K 8 A_Scream
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BOS2 L 8 A_NoBlocking
		BOS2 MN 8 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BOS2 OOOOOOOOOOOOOOOOOO 5 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BOS2 O -1
		stop
	Raise:
		TNT1 A 0 Thing_SetTranslation(0,10)
		BOS2 O 8
		BOS2 NMLKJI  8
		Goto See
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedMediumGreen",0,0,0,0)
		stop
	}
}