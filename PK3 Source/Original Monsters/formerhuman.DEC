// Sounds: NightmareMutant
Actor ZombiemanAmmo : Counter { Inventory.MaxAmount 30 }
Actor FDZombieMan : ZombieMan 30094
{
	//$category Final Duel-Original Monsters
	//$title Corrupted Human
	Game Doom
	Health 40
	GibHealth -20
	Radius 20
	Height 56
	HeadshotHeight 44
	Speed 8
	PainChance 200
	PainChance "Kick", 255
	Monster
	+FLOORCLIP
	SeeSound "grunt/sight"
	AttackSound "Rifle/Fire"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "$OB_ZOMBIE"
	DamageFactor "SRType1", 1.0
	DamageFactor "Ice", 1.0
	DropItem "Clip2"
	DropItem "Energy" DropItem "Energy" DropItem "Energy" DropItem "Energy" DropItem "Energy" DropItem "Energy5" 128
	Species "Corrupted Human"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_GiveInventory("ZombiemanAmmo", random(0,20))
	Idle:
		TNT1 A 0 A_JumpIfInventory("ZombiemanAmmo", 30, "Reload")
		POSS A 10 A_Look
		Loop
	See:
		TNT1 A 0 A_JumpIfInventory("ZombiemanAmmo", 30, "Reload")
		POSS AABBCCDD 4 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_Jump(32,"Missile2")
		TNT1 A 0 A_JumpIfInventory("ZombiemanAmmo", 30, "Idle")
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_GiveInventory("ZombiemanAmmo", 1)
		TNT1 A 0 A_PlaySound("Rifle/Fire")
		POSS F 2 Bright A_CustomMissile("MonsterTracer", 29, 8, frandom(-2.0,2.0), 0, frandom(-2.5,2.5))
		POSS E 2
		Goto See
	Missile2:
		TNT1 A 0 A_JumpIfInventory("ZombiemanAmmo", 30, "Idle")
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_GiveInventory("ZombiemanAmmo", 1)
		TNT1 A 0 A_PlaySound("Rifle/Fire")
		POSS F 1 Bright A_CustomMissile("MonsterTracer", 29, 8, frandom(-2.5,2.5), 0, frandom(-3.0,3.0))
		POSS E 2
		TNT1 A 0 A_JumpIfInventory("ZombiemanAmmo", 30, "Idle")
		TNT1 A 0 A_GiveInventory("ZombiemanAmmo", 1)
		TNT1 A 0 A_PlaySound("Rifle/Fire")
		POSS F 1 Bright A_CustomMissile("MonsterTracer", 29, 8, frandom(-2.5,2.5), 0, frandom(-3.0,3.0))
		POSS E 2
		TNT1 A 0 A_JumpIfInventory("ZombiemanAmmo", 30, "Idle")
		TNT1 A 0 A_GiveInventory("ZombiemanAmmo", 1)
		TNT1 A 0 A_PlaySound("Rifle/Fire")
		POSS F 1 Bright A_CustomMissile("MonsterTracer", 29, 8, frandom(-2.5,2.5), 0, frandom(-3.0,3.0))
		POSS E 2
		Goto See
	Reload: // Sprites from Brutal Doom v19 SE
		TNT1 A 0 A_TakeInventory("ZombiemanAmmo", 30)
        POSS E 3
		POSS E 1 A_CustomMissile("EmptyClipSpawn", 38, 0, random(-11, 11), 0)
        ID12 AAB 10
		TNT1 A 0 A_PlaySound("Rifle/In")
        ID12 CD 10
        POSS A 10
        Goto See
	Pain:
		POSS G 3
		POSS G 3 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",30)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ZombiemanBody")
		Stop
	HeadshotDeath: // Sprites by The_Funktasm
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",30)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ZombiemanBodyHeadshot")
		Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",30)
		BURN ABC 5 Bright Light(BURNDEATH)
		BURN D 5 Bright Light(BURNDEATH) A_PlaySound("grunt/pain")
		BURN EFGHI 5 Bright Light(BURNDEATH)
		TNT1 A 0 A_PlaySound("grunt/pain")
		BURN JKLMN 5 Bright Light(BURNDEATH)
		BURN O 5 Bright Light(BURNDEATH) A_NoBlocking
		TNT1 A 0 A_PlaySound("grunt/pain")
		BURN PQRSTU 5 Bright Light(BURNDEATH)
		BURN V -1
		Stop
	Death.SRType1:
	XDeath:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",30)
		TNT1 A 0 A_SpawnItemEx("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 28.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		POSS M 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		POSS M 5
		POSS N 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		POSS N 5 A_XScream
		POSS O 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		POSS O 5 A_NoBlocking
		POSS P 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		POSS PQRST 5
		POSS U -1
		stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",30)
		TNT1 A 0 Thing_SetTranslation(0,7)
		POSS H 5
		POSS I 5 A_Scream
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSS J 5 A_NoBlocking
		POSS K 5 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSS LLLLLLLLLLLLLLLLLL 6 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSS L -1
		stop
	Death.Saw:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",30)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ZombiemanBodySawed")
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		stop
	Raise:
		TNT1 A 0 Thing_SetTranslation(0,10)
		POSS K 5
		POSS JIH 5
		Goto See
	Pain.Kick:
	    POSS A 1 A_Pain
        POSS A 1 A_FaceTarget (0, 0, 1)
		TNT1 A 0 ThrustThing(angle*256/360+128, 25, 0, 0)
		TNT1 A 0 ThrustThingZ (0, 25, 0, 0)
        Goto Spawn
	}
}

Actor ZombiemanBody : FD_DeadBodyBase {
	States {
	Spawn:
		POSS H 5
		POSS I 5
		POSS J 5
		POSS K 5
		POSS L -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("Zombieman")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor ZombiemanBodyHeadshot : FD_DeadBodyBase {
	States { // Sprites by TheFunktasm
	Spawn:
		POSH A 5
		POSH B 5
		POSH C 5
		POSH D 5
		POSH E -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("Zombieman")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor ZombiemanBodySawed : FD_DeadBodyBase {
	+DontCorpse
	States { // Sprites by ItsNatureToDie
	Spawn:
		POSC A 5
		POSC B 2
		POSC C 3 A_SpawnItemEx ("ZombiemanBodySawedTop", 0.0, 0.0, 28.0, -2.5, 0.0, 2.5)
		POSC DEFG 5
		POSC H -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("Zombieman")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor ZombiemanBodySawedTop : FD_DeadBodyBase {
	States { // Sprites by ItsNatureToDie
	Spawn:
		POSC I 1
		Loop
	Crash:
		POSC JKLM 5
		POSC N -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	Raise:
		Stop
	}
}