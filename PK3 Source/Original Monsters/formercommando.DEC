//===========================================================================
//
// Chaingunner
//
//===========================================================================
Actor FDChaingunGuy : ChaingunGuy 30096 {
	//$category Final Duel-Original Monsters
	//$title Corrupted Commando
	GibHealth -35
	PainChance "Kick", 255
	Dropitem "FDMinigun"
	DropItem "Energy10" DropItem "Energy10" 192
	Species "Corrupted Human"
	States {
	Spawn:
	Idle:
		CPOS A 10 A_Look
		Loop
	See:
		CPOS AABBCCDD 3 A_Chase
		Loop
	Missile:
		CPOS E 10 A_FaceTarget
		TNT1 A 0 A_PlaySound ("chainguy/attack")
		CPOS F 4 Bright A_CustomMissile ("MonsterTracer", 36, 11, frandom (-4.0, 4.0), 0, frandom (-4.5, 4.5))
		TNT1 A 0 A_PlaySound ("chainguy/attack")
		CPOS E 4 Bright A_CustomMissile ("MonsterTracer", 36, 11, frandom (-4.0, 4.0), 0, frandom (-4.5, 4.5))
		CPOS F 1 A_CPosRefire
		Goto Missile+2
	Pain:
		CPOS G 3
		CPOS G 3 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 70)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ChaingunGuyBody")
		Stop
	Death.SRType1:
	XDeath:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 70)
		TNT1 A 0 A_SpawnItemEx ("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 28.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		CPOS O 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem ("NashGore_GibGenerator", 0, 0, 0, 0)
		CPOS O 5
		CPOS P 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem ("NashGore_GibGenerator", 0, 0, 0, 0)
		CPOS P 5 A_XScream
		CPOS Q 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem ("NashGore_GibGenerator", 0, 0, 0, 0)
		CPOS Q 5 A_NoBlocking
		CPOS R 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem ("NashGore_GibGenerator", 0, 0, 0, 0)
		CPOS RS 5
		CPOS T -1
		Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 70)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ChaingunGuyBodyFire")
		Stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 70)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ChaingunGuyBodyPlasma")
		Stop
	Pain.Kick:
		CPOS A 1 A_FaceTarget (0, 0, 1)
		TNT1 A 0 ThrustThing (angle * 256 / 360 + 128, 25, 0, 0)
		TNT1 A 0 ThrustThingZ (0, 25, 0, 0)
		Goto Spawn
	Crush:
		TNT1 A 1 A_SpawnItem ("NashGore_CrushedSmall", 0, 0, 0, 0)
		Stop
	Raise:
		Stop
	}
}

Actor ChaingunGuyBody : FD_DeadBodyBase {
	States {
	Spawn:
		CPOS HIJKLM 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		CPOS NNNNNNNNNNNNNNNNNNNNN 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		CPOS N -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("ChaingunGuy")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor ChaingunGuyBodyFire : FD_DeadBodyBase {
	States {
	Spawn:
		BURN ABC 5 Bright Light (BURNDEATH)
		BURN D 5 Bright Light (BURNDEATH) A_PlaySound ("chainguy/pain")
		BURN EFGHI 5 Bright Light (BURNDEATH)
		TNT1 A 0 A_PlaySound ("chainguy/pain")
		BURN JKLMNO 5 Bright Light (BURNDEATH)
		TNT1 A 0 A_PlaySound ("chainguy/pain")
		BURN PQRSTU 5 Bright Light (BURNDEATH)
		BURN V -1
		Stop
	}
}

Actor ChaingunGuyBodyPlasma : FD_DeadBodyBase {
	BloodColor Black
	States {
	Spawn:
		TNT1 A 0 Thing_SetTranslation (0, 7)
		CPOS HIJKLM 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		CPOS NNNNNNNNNNNNNNNNNNNNN 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		CPOS N -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("ChaingunGuy")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SXF_TRANSFERTRANSLATION)
		Stop
	}
}