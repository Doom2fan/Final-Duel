Actor FDDoomImp : DoomImp 30097 {
	//$category Final Duel-Original Monsters
	//$title Imp
	Game Doom
	Health 60
	GibHealth -30
	Radius 20
	Height 56
	HeadshotHeight 44
	Mass 100
	Speed 8
	PainChance 200
	PainChance "Kick", 255
	PainChance "SRType1", 255
	Monster
	+FloorClip
	SeeSound "imp/sight"
	PainSound "imp/pain"
	DeathSound "imp/death"
	ActiveSound "imp/active"
	HitObituary "$OB_IMPHIT"
	Obituary "$OB_IMP"
	Species "Imp"
	DamageFactor "SRType1", 1.0
	DamageFactor "Ice", 2.0
	DamageFactor "Fire", 0.5
	DropItem "Energy10" DropItem "Energy10" 192 DropItem "Energy5" 192
	States {
	Spawn:
	Idle:
		TROO A 10 A_Look
		Loop
	See:
		TROO AABBCCDD 3 A_Chase
		Loop
	Melee:
	Missile:
		TROO EF 8 A_FaceTarget
		TROO G 6 A_TroopAttack 
		Goto See
	Pain:
		TROO H 2
		TROO H 2 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 60)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("DoomImpBody")
		Stop
	HeadshotDeath: // Sprites by The_Funktasm
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 60)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("DoomImpBodyHeadshot")
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience", 60)
		TNT1 A 0 A_SpawnItemEx ("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 28.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		TROO N 5 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TROO O 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TROO O 5 A_XScream
		TROO P 5 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TROO Q 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TROO Q 5 A_NoBlocking
		TROO RST 5
		TROO U -1
		Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 60)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("DoomImpBodyFire")
		Stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 60)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("DoomImpBodyPlasma")
		Stop
	Death.Saw:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 60)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("DoomImpBodySawed")
		Stop
	Pain.Kick:
	    TROO A 1 A_Pain
        TROO A 1 A_FaceTarget (0, 0, 1)
		TNT1 A 0 ThrustThing (angle * 256 / 360 + 128, 25, 0, 0)
		TNT1 A 0 ThrustThingZ (0, 25, 0, 0)
        Goto Spawn
	Crush:
		TNT1 A 1 A_SpawnItem ("NashGore_CrushedSmall", 0, 0, 0, 0)
		Stop
	Raise:
		Stop
	}
}


Actor DoomImpBody : FD_DeadBodyBase {
	States {
	Spawn:
		TNT1 A 0 NoDelay A_Jump (256, "Death1", "Death2")
		Wait
	Death1:
		TROO IKL 5
		TROO M -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Death2: // Sprites by osjclatchford and Blox
		TRO2 GHIJ 8
		TRO2 KLL 5
		TRO2 M -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("DoomImp")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor DoomImpBodyHeadshot : FD_DeadBodyBase {
	States {
	Spawn:
		TROH ABCD 5
		TROH E -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("DoomImp")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor DoomImpBodySawed : FD_DeadBodyBase {
	States {
	Spawn:
		TROS A 5
		TROS B 2
		TROS C 3
		TROS DE 5
		TROS F -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("DoomImp")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor DoomImpBodyFire : FD_DeadBodyBase {
	States {
	Spawn:
		BURN ABC 5 Bright Light (BURNDEATH)
		BURN D 5 Bright Light (BURNDEATH) A_PlaySound ("imp/pain")
		BURN EFGHI 5 Bright Light (BURNDEATH)
		TNT1 A 0 A_PlaySound ("imp/pain")
		BURN JKLMN 5 Bright Light (BURNDEATH)
		BURN O 5 Bright Light (BURNDEATH)
		TNT1 A 0 A_PlaySound ("imp/pain")
		BURN PQRSTU 5 Bright Light (BURNDEATH)
		BURN V -1
		Stop
	}
}

Actor DoomImpBodyPlasma : FD_DeadBodyBase {
	BloodColor Black
	States {
	Spawn:
		TNT1 A 0 NoDelay Thing_SetTranslation (0, 7)
		TNT1 A 0 A_Jump (256, "Death1", "Death2")
		Wait
	Death1:
		TROO JKL 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		TROO MMMMMMMMMMMMMMMMMM 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		TROO M -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Death2: // Sprites by osjclatchford and Blox
		TRO2 GHIJ 8 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		TRO2 KLL 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		TRO2 MMMMMMMMMMMMMMMMMM 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		TRO2 M -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("Zombieman")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SXF_TRANSFERTRANSLATION)
		Stop
	}
}

// From Beautiful Doom
//===========================================================================
//
// Imp fireball
//
//===========================================================================
Actor FDDoomImpBall : DoomImpBall replaces DoomImpBall {
	DamageType Fire
	+NoExtremeDeath
	Scale .75
	Decal Scorch
	States {
	Spawn:
		IMPB ABC 1 Bright A_CustomMissile ("DoomImpBallTrail", random (-4, 0), random (-4, 4), random (177, 183), 2, random (5, 30))
		Loop
	Death:
		IMPB DEFGH 6 Bright
		stop
	}
}

Actor DoomImpBallTrail : ParticleFire_General {
	States {
	Spawn:
		FIPR ACEGIKMOQS 1 Bright
		Stop
	}
}