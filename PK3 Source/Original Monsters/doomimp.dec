Actor FDDoomImp : DoomImp 30097
{
	//$category Final Duel-Original Monsters
	//$title Imp
	Game Doom
	Health 60
	GibHealth -30
	Radius 20
	Height 56
	HeadshotHeight 44
	Mass 100
	Speed 8
	PainChance 200
	PainChance "Kick", 255
	PainChance "SRType1", 255
	Monster
	+FLOORCLIP
	SeeSound "imp/sight"
	PainSound "imp/pain"
	DeathSound "imp/death"
	ActiveSound "imp/active"
	HitObituary "$OB_IMPHIT"
	Obituary "$OB_IMP"
	Species "Imp"
	DamageFactor "SRType1", 1.0
	DamageFactor "Ice", 2.0
	DamageFactor "Fire", 0.5
	DropItem "Energy10" DropItem "Energy10" 192 DropItem "Energy5" 192
	States
	{
	Spawn:
	Idle:
		TROO A 10 A_Look
		Loop
	See:
		TROO AABBCCDD 3 A_Chase
		Loop
	Melee:
	Missile:
		TROO EF 8 A_FaceTarget
		TROO G 6 A_TroopAttack 
		Goto See
	Pain:
		TROO H 2
		TROO H 2 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",60)
		TNT1 A 0 A_Jump(256, "Death1", "Death2")
		Wait
	Death1:
		TROO I 8
		TROO J 8 A_Scream
		TROO K 6
		TROO L 6 A_NoBlocking
		TROO M -1
		Stop
	Death2: // Sprites by osjclatchford and Blox
		TRO2 G 8
		TRO2 H 8 A_Scream
		TRO2 IJ 8
		TRO2 K 5 A_Fall
		TRO2 L 10
		TRO2 M -1
		Stop
	HeadshotDeath: // Sprites by The_Funktasm
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",60)
		TROH A 5 A_Scream
		TROH B 5
		TROH C 5 A_NoBlocking
		TROH D 5
		TROH E -1
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",60)
		TNT1 A 0 A_SpawnItemEx("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 28.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		TROO N 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TROO N 5
		TROO O 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TROO O 5 A_XScream
		TROO P 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TROO P 5
		TROO Q 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TROO Q 5 A_NoBlocking
		TROO RST 5
		TROO U -1
		stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",60)
		BURN ABC 5 Bright Light(BURNDEATH)
		BURN D 5 Bright Light(BURNDEATH) A_PlaySound("imp/pain")
		BURN EFGHI 5 Bright Light(BURNDEATH)
		TNT1 A 0 A_PlaySound("imp/pain")
		BURN JKLMN 5 Bright Light(BURNDEATH)
		BURN O 5 Bright Light(BURNDEATH) A_NoBlocking
		TNT1 A 0 A_PlaySound("imp/pain")
		BURN PQRSTU 5 Bright Light(BURNDEATH)
		BURN V -1
		Stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",60)
		TNT1 A 0 Thing_SetTranslation(0,7)
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TROO J 8 A_Scream
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TROO K 6
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TROO L 6 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TROO MMMMMMMMMMMMMMMMMM 5 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TROO M -1
		stop
	Pain.Kick:
	    TROO A 1 A_Pain
        TROO A 1 A_FaceTarget
		TNT1 A 0 ThrustThing(angle*256/360+128, 25, 0, 0)
		TNT1 A 0 ThrustThingZ (0, 25, 0, 0)
        Goto Spawn
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		stop
	Raise:
		TNT1 A 0 Thing_SetTranslation(0,10)
		TROO MLKJI 8
		Goto See
	}
}
// From Beautiful Doom
//===========================================================================
//
// Imp fireball
//
//===========================================================================
Actor FDDoomImpBall : DoomImpBall replaces DoomImpBall
{
	DamageType Fire
	+NoExtremeDeath
	Scale .75
	Decal Scorch
	States
	{
	Spawn:
		IMPB ABC 1 Bright A_custommissile("DoomImpBallTrail",random(-4,0),random(-4,4),random(177,183),2,random(5,30))
		Loop
	Death:
		IMPB DEFGH 6 Bright
		stop
	}
}

Actor DoomImpBallTrail : ParticleFire_General
{
	States
	{
	Spawn:
		FIPR ACEGIKMOQS 1 Bright
		Stop
	}
}