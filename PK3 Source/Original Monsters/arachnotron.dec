//===========================================================================
//
// Arachnotron
//
//===========================================================================
Actor FDArachnotron : Arachnotron replaces Arachnotron 30104
{
	//$category Final Duel-Original Monsters
	//$title Arachnotron
	Game Doom
	Health 600
	Radius 64
	Height 64
	Mass 600
	Speed 12
	PainChance 128
	Monster
	+FLOORCLIP
	+BOSSDEATH
	SeeSound "baby/sight"
	PainSound "baby/pain"
	DeathSound "baby/death"
	ActiveSound "baby/active"
	Obituary "$OB_BABY"
	DamageFactor "Ice", 0.5
	DamageFactor "SRType1", 1.0
	DropItem "Energy500" DropItem "Energy10" DropItem "Energy10" DropItem "Energy5" // 525
	States
	{
	Spawn:
		TNT1 A 0 Thing_SetTranslation(0,10)
		BSPI A 10 A_Look
		Loop
	See:
		BSPI A 20
		BSPI A 3 A_BabyMetal
		BSPI ABBCC 3 A_Chase
		BSPI D 3 A_BabyMetal
		BSPI DEEFF 3 A_Chase
		Goto See+1
	Missile:
		BSPI A 20 Bright A_FaceTarget
		TNT1 A 0 A_PlaySound ("PlasmaRifle/fire", CHAN_WEAPON)
		BSPI G 4 Bright A_CustomMissile ("FDArachnotronPlasma", 20)
		BSPI H 4 Bright
		BSPI H 1 Bright A_SpidRefire
		Goto Missile+1
	Pain:
		BSPI I 3
		BSPI I 3 A_Pain
		Goto See+1
	Death:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",200)
		BSPI J 20 A_Scream
		BSPI K 7 A_NoBlocking
		BSPI LMNO 7
		BSPI P -1 A_BossDeath
		Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",200)
		TNT1 A 0 Thing_SetTranslation(0,7)
		BSPI J 20 A_Scream
		TNT1 A 0 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BSPI K 7 A_NoBlocking
		BSPI LMNO 7 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_BossDeath
		BSPI PPPPPPPPPPPPPPPPPP 5 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BSPI PPPPPPPPPPPPPPPPPP 5 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BSPI P -1
		stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",200)
		TNT1 A 0 Thing_SetTranslation(0,7)
		BSPI J 20 A_Scream
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BSPI K 7 A_NoBlocking
		BSPI LMNO 7 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_BossDeath
		BSPI PPPPPPPPPPPPPPPPPP 5 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BSPI P -1
		stop
    Raise:
		TNT1 A 0 Thing_SetTranslation(0,10)
		BSPI P 5
		BSPI ONMLKJ 5
		Goto See+1
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedMedium",0,0,0,0)
		Stop
	}
}

Actor FDArachnotronPlasma : FDPlasmaBall replaces ArachnotronPlasma { Damage 3 }