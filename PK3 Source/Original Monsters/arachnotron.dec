//===========================================================================
//
// Arachnotron
//
//===========================================================================
Actor FDArachnotron : Arachnotron replaces Arachnotron 30104
{
	//$category Final Duel-Original Monsters
	//$title Arachnotron
	Game Doom
	Health 600
	Radius 64
	Height 64
	Mass 600
	Speed 12
	PainChance 128
	Monster
	+FLOORCLIP
	+BOSSDEATH
	SeeSound "baby/sight"
	PainSound "baby/pain"
	DeathSound "baby/death"
	ActiveSound "baby/active"
	Obituary "$OB_BABY"
	DamageFactor "Ice", 0.5
	DamageFactor "SRType1", 1.0
	DropItem "Energy"
	States
	{
	Spawn:
		TNT1 A 0 Thing_SetTranslation(0,10)
		BSPI A 10 A_Look
		Loop
	See:
		BSPI A 20
		BSPI A 3 A_BabyMetal
		BSPI ABBCC 3 A_Chase
		BSPI D 3 A_BabyMetal
		BSPI DEEFF 3 A_Chase
		Goto See+1
	Missile:
		BSPI A 20 Bright A_FaceTarget
		BSPI G 4 Bright A_BspiAttack
		BSPI H 4 Bright
		BSPI H 1 Bright A_SpidRefire
		Goto Missile+1
	Pain:
		BSPI I 3
		BSPI I 3 A_Pain
		Goto See+1
	Death:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",200)
		BSPI J 20 A_Scream
		BSPI K 7 A_NoBlocking
		BSPI LMNO 7
		BSPI P -1 A_BossDeath
		Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",200)
		TNT1 A 0 Thing_SetTranslation(0,7)
		BSPI J 20 A_Scream
		TNT1 A 0 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BSPI K 7 A_NoBlocking
		BSPI LMNO 7 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_BossDeath
		BSPI PPPPPPPPPPPPPPPPPP 5 A_SpawnItemEx("ParticleFire_Red",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BSPI PPPPPPPPPPPPPPPPPP 5 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BSPI P -1
		stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget("XPSystemExperience",200)
		TNT1 A 0 Thing_SetTranslation(0,7)
		BSPI J 20 A_Scream
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BSPI K 7 A_NoBlocking
		BSPI LMNO 7 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		TNT1 A 0 A_BossDeath
		BSPI PPPPPPPPPPPPPPPPPP 5 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		BSPI P -1
		stop
    Raise:
		TNT1 A 0 Thing_SetTranslation(0,10)
		BSPI P 5
		BSPI ONMLKJ 5
		Goto See+1
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedMedium",0,0,0,0)
		stop
	}
}

//===========================================================================
//
// Arachnotron plasma
//
//===========================================================================



Actor GreenFlarePlasma : Flare_General
{
Alpha .9
scale .1
States
	{
	Spawn:
		LENG A 2 Bright
		stop
	}
}

Actor GreenFlarePlasmaTrail : GreenFlarePlasma 
{
Alpha .8
scale .065
States
	{
	Spawn:
		LENG A 1 Bright A_FadeOut(0.2)
		Loop
	}
}

Actor GreenPlasmaBallExplosion : DebrisGeneral
{
translation "0:255=0:255"
scale .2
RenderStyle Add
Alpha .6
States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAA 0 A_CustomMissile("GreenPlasmaPiece",0,0,random(-360,360),2,random(-50,50))
		TNT1 AAAAAA 0 A_CustomMissile("GreenPlasmaShred",0,0,random(-360,360),2,random(-90,90))
		TNT1 A 0 Bright A_SpawnItem("RocketLSmoke")
		PEXP ABCDEFGHIJKLM 2 Bright
		stop
	}
}

Actor GreenPlasmaShred : DebrisGeneral
{
translation "192:207=112:127","240:247=10:12"
+DontSplash
RenderStyle ADD
speed 8
scale .1
Alpha .8
States
{
	Spawn:
		PLSE ABCDE 1 Bright A_SpawnItem("GreenPlasmaShredTrail")
		stop
	}
}

Actor GreenPlasmaShredTrail : GreenPlasmaShred
{
translation "192:207=112:127","240:247=10:12"
speed 0
Alpha .5
States
{
	Spawn:
		PLSE ABCDE 2 Bright
		stop
	}
}

Actor GreenPlasmaPiece : DebrisGeneral
{
+DontSplash
translation "192:207=112:127","240:247=10:12"
-NoGravity
RenderStyle Add
Alpha 1.0
scale 0.08
speed 6
gravity 0.8
States
	{
	Spawn:
		PLBS A 1 Bright A_FadeOut(0.03)
		Loop
	}
}
Actor FDArachnotronPlasma : FastProjectile replaces ArachnotronPlasma
{
translation "192:207=112:127","240:247=10:12"
spawnid 129
Radius 13
Height 8
Speed 25
Damage 5
Projectile
SeeSound "baby/attack"
DeathSound "baby/shotx"
Obituary "%o let an arachnotron get %h."
MissileType "GreenFlarePlasmaTrail"
MissileHeight 8
damagetype PlasmaDamage
+ForceXYBillboard
+THRUGHOST
+NoExtremeDeath
decal ArachnotronPlasmaSplat
RenderStyle ADD
scale .7
Alpha .8
States
	{
	Spawn:
		PLBS AB 1 Bright A_SpawnItem("GreenFlarePlasma")
		Loop
	Death:
		TNT1 A 0
		TNT1 A 0 A_Scream
		TNT1 A 1 A_SpawnItem("GreenPlasmaBallExplosion",0,0,0)
		stop
	}
}