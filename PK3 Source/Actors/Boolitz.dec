// Credits:
// Decorate: WildWeasel, Alex_Mercer, Jekyll Grim Payne/zer0
// Sprites: WildWeasel
// Weapons: A_FireCustomMissile (tracerName, frandom (-, horizontalSpread), 1, horizontalOffset, verticalOffset, 0, frandom (-, verticalSpread))
// Monsters: A_CustomMissile (tracerName, verticalOffset, horizontalOffset, frandom (-, horizontalSpread), 0, frandom (-, verticalSpread))

Actor Tracer : FastProjectile {
	Alpha 1.0
	RenderStyle Add
	Radius 2
	Height 2
	Speed 75
	Projectile
	+BloodSplatter +NoExtremeDeath +NoDamageThrust +AllowHeadshot
	Scale 0.025
	Damage (random (1, 3) * 4)
	MissileType TracerTrail
	MissileHeight 8
	Decal Bullet
	States {
	Spawn:
		SPRK A 1 Bright
		Loop
	Death:
	Crash:
		TNT1 A 0 A_SpawnItemEx ("BulletPuff2")
		Stop
	XDeath:
		TNT1 A 3 A_PlaySound ("Bullet/HitFlesh", CHAN_BODY, 0.5)
		Stop
	}
}

Actor TracerTrail {
	Alpha 1.0
	RenderStyle Add
	Speed 0
	+NoBlockmap +NoGravity +NoTeleport +CannotPush
	+NoDamageThrust
	Scale 0.025
	States {
	Spawn:
		SPRK A 1 Bright A_FadeOut (0.3)
		Wait
	}
}

Actor BulletPuff2 Replaces BulletPuff {
	+NoGravity +ForceXYBillboard +AllowHeadshot +NoTimeFreeze
	+NoExtremeDeath
	RenderStyle Add
	Scale 0.05
	Alpha 1.0
	Decal Bullet
	States {
	Spawn: // Sprites by Neoworm
		TNT1 AAAA 0 A_CustomMissile ("WallPart", 0, 0, random (0, 360), 2, random (-20, 80))
		TNT1 A 0 A_SpawnItemEx ("SparkSpawner")
		TNT1 A 0 A_SpawnItem ("ShotSmoke")
		BPUF ABCDEFGHI 1 Light (BPUFF1) Bright
		Stop
	}
}

Actor WallPart : DebrisGeneral {
	+DoomBounce +NoTimeFreeze +DontSplash -NoGravity
	Speed 8
	BounceFactor 0.4
	RenderStyle Translucent
	Alpha 1.0
	Scale 0.1
	Translation "0:255=5:5"
	States {
	Spawn:
		BPRT ABCDEFGH 2 NoDelay A_FadeOut (0.001)
		Loop
	Death:
		TNT1 A 0
		Stop
	}
}

Actor SparkSpawner {
	+NoTimeFreeze +NoInteraction +NoGravity
	States {
	Spawn:
		TNT1 AAAAAAAA 0 NoDelay A_CustomMissile ("RicochetSpark", 0, 0, random (0, 360), 2, random (10, 60))
		TNT1 A 0 A_Jump (128, "STAHP")
		TNT1 AAAAAAAA 0 A_CustomMissile ("RicochetSpark", 0, 0, random (0, 360), 2, random (10, 60))
	STAHP:
		TNT1 A 0
		Stop
	}
}

Actor RicochetSpark : DebrisGeneral {
	+DontSplash +NoTimeFreeze -DoomBounce -NoGravity
	RenderStyle Add
	Alpha 1.0
	Speed 10
	Radius 3
	Height 3
	Scale .03
	Gravity 1.0
	States {
	Spawn:
		FLR3 A 0 NoDelay A_Jump (256, "SpawnA", "SpawnB", "SpawnC", "SpawnD", "SpawnF", "SpawnG", "SpawnH")
	SpawnA:
		FLR3 A 0
		Goto Loople
	SpawnB:
		FLR3 B 0
		Goto Loople
	SpawnC:
		FLR3 C 0
		Goto Loople
	SpawnD:
		FLR3 D 0
		Goto Loople
	SpawnE:
		FLR3 E 0
		Goto Loople
	SpawnF:
		FLR3 F 0
		Goto Loople
	SpawnG:
		FLR3 G 0
		Goto Loople
	SpawnH:
		FLR3 H 0
	Loople:
		"####" "#" 1 Light (BPUFF2) Bright A_FadeOut (0.05)
		Loop
	}
}

Actor ShotSmoke {
	+NotDMatch +NoInteraction +ForceXYBillboard +NoTimeFreeze
	VSpeed 2
	Renderstyle Add
	Alpha 0.3
	Scale 0.1
	States {
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 1 NoDelay A_SpawnItem ("ShotSmokeTrail")
		Stop
	}
}

Actor ShotSmokeTrail : ShotSmoke {
	VSpeed 0
	Alpha 0.15
	States {
	Spawn:
		SMOK CDEFGHIJKLMNOPQ 1
		Stop
	}
}

// Custom tracers

// Weapon tracers
Actor PistolTracer              : Tracer { Speed 125 Damage (random (1, 3) * 6) }
Actor RifleTracer               : Tracer { Speed 95 Damage (random (1, 3) * 6) }
Actor ShotgunTracer             : Tracer { Speed 150 Damage (random (1, 3) * 6) }
Actor DoubleShotgunTracer       : Tracer { Speed 125 Damage (random (1, 3) * 5) }
Actor TacticalShotgunTracer     : Tracer { Speed 150 Damage (random (1, 5) * 6) DamageType TacticalShotgunTracer }
Actor QuadShotgunTracer         : Tracer { Speed 150 Damage (random (1, 5) * 3) }
Actor AK47Tracer                : Tracer { Speed 130 }
Actor AMGTracer                 : Tracer { Speed 130 Damage (random (1, 4) * 4) }
Actor MinigunTracer             : Tracer { Speed 180 Damage (random (1, 2) * 3) }
Actor MinigunTracerUpgraded     : Tracer { Speed 180 Damage (random (1, 3) * 4) }
Actor RevolverTracer            : Tracer { Speed 160 Damage (random (2, 6) * 20) }
Actor UpgradedRevolverTracer    : Tracer { Speed 160 Damage (random (2, 5) * 32) }
Actor MagnumTracer              : Tracer { Speed 250 Damage (random (1, 4) * 40) DamageType MagnumTracer }
Actor ExplosiveMagnumTracer     : Tracer {
	Speed 230
	Damage (random (1, 5) * 40)
	DamageType MagnumTracer
	States {
	Death:
	Crash:
		TNT1 A 0 A_Explode (64, 64)
		TNT1 A 3 A_SpawnItemEx ("BulletPuff2")
		Stop
	XDeath:
		TNT1 A 0 A_Explode (64, 64)
		TNT1 A 3 A_PlaySound ("Bullet/HitFlesh", CHAN_BODY, 0.5)
		Stop
	}
}

// Monster tracers
Actor MonsterTracer             : Tracer { Damage (random (1, 3) * 3) }
Actor ShotgunnerTracer          : Tracer { Damage (random (1, 3) * 2) }
Actor SMGZombieTracer           : Tracer { Speed 83 Damage (random (1, 2)) }