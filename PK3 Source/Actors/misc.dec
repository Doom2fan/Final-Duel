// By Jekyll Grim Payne "Aka" zer0, edited by Alex_Mercer
Actor BulletPuff2 Replaces BulletPuff {
	+NoGravity
	+ForceXYBillboard
	-DontSplash
	-AllowParticles
	+NoExtremeDeath
	RenderStyle Add
	Scale 0.05
	Alpha 1.0
	Decal Bullet
	States {
	Spawn: // Sprites by Neoworm
		TNT1 AAAA 0 A_CustomMissile ("WallPart", 0, 0, random (0, 360), 2, random(-20, 80))
		TNT1 A 0 A_SpawnItem ("ShotSmoke")
		BPUF ABCDEFGHI 1 Bright
		Stop
	}
}

Actor WallPart : DebrisGeneral {
	+DoomBounce
	-NoGravity
	+DontSplash
	Speed 8
	BounceFactor 0.4
	RenderStyle Translucent
	Alpha 1.0
	Scale 0.1
	Translation "0:255=5:5"
	States {
	Spawn:
		BPRT ABCDEFGH 2 NoDelay A_FadeOut (0.001)
		Loop
	Death:
		TNT1 A 0
		Stop
	}
}

Actor SparkSpawner {
	+NoInteraction
	+NoGravity
	States {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump (128,9)
		TNT1 AAAAAAAA 0 A_CustomMissile ("RicochetSpark", 0, 0, random(0, 360), 2, random(10, 60))
		TNT1 A 0
		stop
	}
}

Actor RicochetSpark : DebrisGeneral {
	-DOOMBOUNCE
	-NoGravity
	+DontSplash
	RenderStyle ADD
	Alpha 1.0
	Speed 10
	Radius 3
	Height 3
	Scale .03
	Gravity 1.0
	States {
	Spawn:
		SPRK A 1 NoDelay Bright A_FadeOut (0.05)
		Loop
	}
}

Actor ShotSmoke {
	+NotDMatch
	+NoInteraction
	+ForceXYBillboard
	VSpeed 2
	Renderstyle Add
	Alpha 0.3
	Scale 0.1
	States {
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 1 NoDelay A_SpawnItem ("ShotSmokeTrail")
		Stop
	}
}

Actor ShotSmokeTrail : ShotSmoke {
	VSpeed 0
	Alpha 0.15
	States {
	Spawn:
		SMOK CDEFGHIJKLMNOPQ 1
		Stop
	}
}

Actor BlackSmoke : ShotSmoke {
	RenderStyle Translucent
	Alpha 0.15
	States {
	Spawn:
		TNT1 A 0
		SMOK ABCDEFGHIJKLMNOPQ 1 A_SpawnItem ("BlackSmokeTrail") 
		SMOK Q 1 A_FadeOut (0.02)
		Wait
	}
}

Actor BlackSmokeTrail : ShotSmokeTrail {
	RenderStyle Translucent
	Alpha 0.08
}

Actor SmokingPiece : DebrisGeneral { // Edited by Alex_Mercer
	-NoGravity
	Speed 12
	Gravity 0.7
	States {
	Spawn:
		TNT1 A 0 A_JumpIf (waterlevel >= 1, "Water")
		TNT1 A 1
		TNT1 A 1 A_SpawnItem ("SmokingPieceSmoke")
		Wait
	Water:
		TNT1 A 0
		Stop
	}
}

Actor SmokingPieceSmoke : ShotSmokeTrail {
	Scale 0.06
	Alpha 0.5
}

Actor ParticleFire_Red : ParticleFire_General {
	States {
	Spawn:
		FIPR ABCDEFGHIJKLMNOPQRST 1 Bright
		Stop
	}
}

Actor ParticleFire_Yellow : ParticleFire_General {
	States {
	Spawn:
		FIPY ABCDEFGHIJKLMNOPQRST 1 Bright
		Stop
	}
}

Actor MeleePuff { // Edited by Alex_Mercer
	+NoBlockmap
	+NoGravity
	+PuffOnActors
	-AllowParticles
	+NoExtremeDeath
	DamageType Kick
	AttackSound "Weapons/kick/hitwall"
	ActiveSound "Weapons/kick/swing"
}

Actor MeleePuffStrong : MeleePuff { // Edited by Alex_Mercer
	Decal FistScorch
	AttackSound "Weapons/kick/strong"
	ActiveSound "Weapons/kick/swing"
	-NoExtremeDeath
	+ExtremeDeath
	States {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem ("ShotSmoke")
		Stop
	}
}

Actor SmokeSpawner : DebrisGeneral {
    Speed 20
    +NoClip
    States {
    Spawn:
		TNT1 A 1
        TNT1 A 0 A_SpawnItem ("ShotSmoke", 0, 0)
        Stop
    }
}
Actor RocketLSmoke : ShotSmoke {
	Scale 0.15
}

Actor Step : Boolean { }
Actor Footstep {
	+NoBlockmap
	-NoGravity
	-DontSplash
	Radius 1
	Height 1
	Speed 40
	Mass 1000
	Gravity 40
	States {
	Spawn:
		TNT1 A 2
		Stop
	}
}

Actor BlueKeyCard : CustomInventory replaces BlueCard {
	Inventory.PickupMessage "Picked up a blue keycard."
	Inventory.PickupSound "pickups/keycard"
	States {
	Spawn:
		BKEY A 10
		BKEY B 10 Bright
		Loop
	Pickup:
		TNT1 A 1 A_GiveInventory ("BlueCard", 1)
		Stop
	}
}
Actor RedKeyCard : CustomInventory replaces RedCard {
	Inventory.PickupMessage "Picked up a red keycard."
	Inventory.PickupSound "pickups/keycard"
	States {
	Spawn:
		RKEY A 10
		RKEY B 10 Bright
		Loop
	Pickup:
		TNT1 A 1 A_GiveInventory ("RedCard", 1)
		Stop
	}
}
Actor YellowKeyCard : CustomInventory replaces YellowCard {
	Inventory.PickupMessage "Picked up a yellow keycard."
	Inventory.PickupSound "pickups/keycard"
	States {
	Spawn:
		YKEY A 10
		YKEY B 10 Bright
		Loop
	Pickup:
		TNT1 A 1 A_GiveInventory ("YellowCard", 1)
		Stop
	}
}
// By Alex_Mercer "Aka" Chronos Ouroboros
// Bullet puff -------------------------------------------------------------
Actor PressingZoom : Boolean { }
Actor SawPuff : BulletPuff2 {
	Decal ChainsawMark
	DamageType "Saw" 
	AttackSound "Chainsaw/Wall"
	States {
	Spawn:
		TNT1 A 0
		BPUF ABCDEFGHI 1 Bright Light (BPUFF1)
		TNT1 A 0
		Stop
	}
}

Actor FDTeleportFog Replaces TeleportFog {
	+NoBlockmap
	+NoTeleport
	+NoGravity
	RenderStyle Add
	Translation "112:127=176:191", "224:231=168:175", "160:167=184:191", "128:143=176:191", "144:151=176:183", "152:159=184:191", "64:79=176:191"
	States {
	Spawn:
		TFOG ABABCDEFGHIJ 6 Bright
		Stop
	}
}

Actor HeatSmoke { // Modified version of code from Beautiful Doom
	+NotDMatch
	+NoInteraction
	+ForceXYBillboard
	Vspeed 2
	RenderStyle Add
	Scale 0.1
	Alpha 0.5
	States {
	Spawn:
		TNT1 A 0
		SMOK ABCDEFGHIJKLMNOPQ 1 A_SpawnItem ("HeatSmokeTrail")
		Stop
	}
}

Actor HeatSmokeTrail : HeatSmoke {
	Vspeed 0
	Alpha 0.15
	States {
	Spawn:
		SMOK CDEFGHIJKLMNOPQ 1
		Stop
	}
}

Actor HeatSmokeSpawner : DebrisGeneral {
	Speed 20
	+NoClip
	States {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem ("HeatSmoke", 0, 0)
        Stop
	}
}
Actor BoomPuff {
	+NoGravity
	+ForceXYBillboard
	-DontSplash
	-AllowParticles
	+PuffOnActors
	+PuffGetsOwner
	Scale 0.1
	Decal Bullet
	States {
	XDeath:
		TNT1 A 0 A_Explode (20, 20)
		Stop
	Spawn:
		TNT1 AAAA 0 A_custommissile ("WallPart", 0, 0, random (0, 360), 2, random (-20, 80))
		TNT1 A 0 A_SpawnItem ("ShotSmoke")
		REXP G 1 Bright Light (BPUFF1)
		TNT1 A 0 A_Explode (20, 20, 0)
		REXP HIJKLMNOPQRSTUVW 1 Bright Light (BPUFF1)
		Stop
	}
}