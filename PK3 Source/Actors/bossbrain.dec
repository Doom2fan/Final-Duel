Actor FDBossBrain : BossBrain Replaces BossBrain {
	Health 250
	Mass 10000000
	PainChance 255
	+Solid +Shootable +NoIceDeath +OldRadiusDmg
	+Invulnerable
	DamageFactor BFG, 0.3
	DamageFactor Shrapnel, 0.0
	PainChance Shrapnel, 0
	PainSound "brain/pain"
	DeathSound "brain/death"
	States {
	BrainExplode:
		MISL BC 10 Bright
		MISL D 10 A_BrainExplode
		Stop
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecuteAlways ("FD_BossBrainWaitForMonologue", 0)
		BBRN A 1
		Wait
	BossStart:
		TNT1 A 0 A_ChangeFlag ("Invulnerable", 0)
	Idle:
		BBRN A -1
		Stop
	Pain:
		TNT1 A 0 Radius_Quake (6, 10, 0, 50, 0)
		BBRN A 36 A_BrainPain
		Goto Spawn
	Death:
		TNT1 A 0 Radius_Quake (8, 300, 0, 50, 0)
		BBRN A 100 A_BrainScream
		BBRN A 60
		BBRN A -1 A_BrainDie
		Stop
	}
}

Actor FDBossEye : BossEye Replaces BossEye {
	States {
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecuteAlways ("FD_BossBrainMonologue", 0)
		SSWV A 1
		Wait
	BossStart:
		TNT1 A 0 A_RearrangePointers (AAPTR_PLAYER1)
		TNT1 A 0 A_Look
		TNT1 A 0 A_RearrangePointers (AAPTR_PLAYER2)
		TNT1 A 0 A_Look
		TNT1 A 0 A_RearrangePointers (AAPTR_PLAYER3)
		TNT1 A 0 A_Look
		TNT1 A 0 A_RearrangePointers (AAPTR_PLAYER4)
		TNT1 A 0 A_Look
		TNT1 A 0 A_RearrangePointers (AAPTR_PLAYER5)
		TNT1 A 0 A_Look
		TNT1 A 0 A_RearrangePointers (AAPTR_PLAYER6)
		TNT1 A 0 A_Look
		TNT1 A 0 A_RearrangePointers (AAPTR_PLAYER7)
		TNT1 A 0 A_Look
		TNT1 A 0 A_RearrangePointers (AAPTR_PLAYER8)
		Goto See
	Idle:
		SSWV A 10 A_Look
		Loop
	See:
		SSWV A 181 A_BrainAwake
		SSWV A 150 A_BrainSpit
		Wait
	}
}