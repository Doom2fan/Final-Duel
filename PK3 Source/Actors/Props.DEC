// Lamps
Actor LampFlare : LensFlare {
	Alpha 0.5
	XScale 0.6
	YScale 0.3
}

Actor YellowLampFlare : YellowLensFlare {
	Alpha 0.5
	XScale 0.6
	YScale 0.2
}

Actor FDTechLamp : SwitchableDecoration Replaces TechLamp {
	Radius 16
	Height 80
	Health 10
	+Solid +Shootable +UseSpecial +NoBlood
	Activation THINGSPEC_Switch|THINGSPEC_Deactivate
	States {
	Active:
	Spawn:
		TNT1 A 0 A_CheckSight ("Spawn2")
		TLMP AABBCCDD 2 Bright Light (BIGLAMP) A_SpawnItemEx ("LampFlare", 0.0, 0.0, 70.0)
		Loop
	Spawn2:
		TLMP A 1 Bright Light (BIGLAMP)
		Goto Spawn
	Inactive: // by Simo Malinen, edited by Alex_Mercer
		TLMP E -1
		Stop
	Death: // by Alex_Mercer
		TNT1 A 0 A_ChangeFlag ("USESPECIAL", 0)
		TNT1 A 0 A_NoBlocking
		TLMP F -1
		Stop
	}
}

Actor FDTechLamp2 : SwitchableDecoration Replaces TechLamp2 {
	Radius 16
	Height 60
	Health 10
	+Solid +Shootable +UseSpecial +NoBlood
	Activation THINGSPEC_Switch|THINGSPEC_Deactivate
	States
	{
	Active:
	Spawn:
		TNT1 A 0 A_CheckSight ("Spawn2")
		TLP2 AABBCCDD 2 Bright Light (SMALLLAMP) A_SpawnItemEx ("LampFlare", 0.0, 0.0, 52.5)
		Loop
	Spawn2:
		TLP2 A 1 Bright Light (SMALLLAMP)
		Goto Spawn
	Inactive: // by Alex_Mercer
		TLP2 E -1
		Stop
	Death: // by Alex_Mercer
		TNT1 A 0 A_ChangeFlag ("USESPECIAL", 0)
		TNT1 A 0 A_NoBlocking
		TLP2 F -1
		Stop
	}
}

Actor FDColumn : SwitchableDecoration Replaces Column {
	Radius 16
	Height 48
	Health 10
	+Solid +Shootable +UseSpecial +NoBlood
	Activation THINGSPEC_Switch|THINGSPEC_Deactivate
	States {
	Active:
	Spawn:
		TNT1 A 0 A_CheckSight ("Spawn2")
		COLU A 2 Bright Light (LAMP) A_SpawnItemEx ("YellowLampFlare", 0.0, 0.0, 44.0)
		Loop
	Spawn2:
		COLU A 1 Bright Light (LAMP)
		Goto Spawn
	Inactive: // by Alex_Mercer
		COLU B -1
		Stop
	Death: // by Alex_Mercer
		TNT1 A 0 A_ChangeFlag ("USESPECIAL", 0)
		TNT1 A 0 A_NoBlocking
		COLU C -1
		Stop
	}
}

// Torches
Actor RedTorchFire {
	Radius 0
	Height 0
	RenderStyle Add
	Scale 0.1875
	+NoGravity +NoBlockmap +NoTeleport +ForceXYBillboard
	+NoInteraction
	States {
	Spawn:
		TNT1 A 0 NoDelay A_Jump (256, "Spawn1", "Spawn2")
		Goto Spawn
	Spawn1:
		FIR1 ABCDEFG 1 Bright
		FIR1 HIJKL 1 Bright A_FadeOut (0.15)
		Stop
	Spawn2:
		FIR1 MNOPQRS 1 Bright
		FIR1 TUVWX 1 Bright A_FadeOut (0.15)
		Stop
	}
}

Actor TallTorch_Red : RedTorch replaces RedTorch {
	States {
	Spawn:
		TNT1 A 0 NoDelay A_PlaySoundEx ("Decoration/TorchBurn", CHAN_BODY, 1)
	Looper:
		TNT1 A 0 A_CheckSight ("Spawn2")
		TATO A 1 Bright A_SpawnItemEx ("RedTorchFire", Random (-2, 2), Random (-2, 2), Random (56, 58), 0.001 * Random (10, 200), 0.001 * Random (10, 200), 0.001 * Random (100, 2000), 0, 128, 0)
		Loop
	Spawn2:
		TATO A 1
		Goto Looper
	}
}

Actor GreenTorchFire : RedTorchFire {
	States {
	Spawn:
		TNT1 A 0 NoDelay A_Jump (256, "Spawn1", "Spawn2")
		Goto Spawn
	Spawn1:
		FIR2 ABCDEFG 1 Bright
		FIR2 HIJKL 1 Bright A_FadeOut (0.15)
		Stop
	Spawn2:
		FIR2 MNOPQRS 1 Bright
		FIR2 TUVWX 1 Bright A_FadeOut (0.15)
		Stop
	}
}

Actor TallTorch_Green : GreenTorch replaces GreenTorch {
	States {
	Spawn:
		TNT1 A 0 NoDelay A_PlaySoundEx ("Decoration/TorchBurn", CHAN_BODY, 1)
	Looper:
		TNT1 A 0 A_CheckSight ("Spawn2")
		TATO A 1 Bright A_SpawnItemEx ("GreenTorchFire", Random (-2, 2), Random (-2, 2), Random (56, 58), 0.001 * Random (10, 200), 0.001 * Random (10, 200), 0.001 * Random (100, 2000), 0, 128, 0)
		Loop
	Spawn2:
		TATO A 1
		Goto Looper
	}
}

Actor BlueTorchFire : RedTorchFire {
	States {
	Spawn:
		TNT1 A 0 NoDelay A_Jump (256, "Spawn1", "Spawn2")
		Goto Spawn
	Spawn1:
		FIR3 ABCDEFG 1 Bright
		FIR3 HIJKL 1 Bright A_FadeOut (0.15)
		Stop
	Spawn2:
		FIR3 MNOPQRS 1 Bright
		FIR3 TUVWX 1 Bright A_FadeOut (0.15)
		Stop
	}
}

Actor TallTorch_Blue : BlueTorch replaces BlueTorch {
	States {
	Spawn:
		TNT1 A 0 NoDelay A_PlaySoundEx ("Decoration/TorchBurn", CHAN_BODY, 1)
	Looper:
		TNT1 A 0 A_CheckSight ("Spawn2")
		TATO A 1 Bright A_SpawnItemEx ("BlueTorchFire", Random (-2, 2), Random (-2, 2), Random (56, 58), 0.001 * Random (10, 200), 0.001 * Random (10, 200), 0.001 * Random (100, 2000), 0, 128, 0)
		Loop
	Spawn2:
		TATO A 1
		Goto Looper
	}
}