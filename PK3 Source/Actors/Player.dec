// By Alex_Mercer
// Base Player Class
Actor BasePlayerClass : PlayerPawn {
	// Damage
	GibHealth -50
	PainChance 255
	DamageFactor PsiBurnout, 1.0
	// Speed Related
	Player.JumpZ 8.0
	// Misc
	Radius 16
	Height 56
    Player.ViewHeight 48
	Player.AttackZOffset 20
	Player.InvulnerabilityMode Reflective
	Player.ColorRange 112, 127
    +Solid +NoSkin
	States {
	Spawn:
		PLAY AAAA 1
		Loop
	See:
		TNT1 A 0 A_JumpIfInventory ("Step", 1, "See2")
		PLAY AAAAAA 1
		PLAY BBBBBB 1
		TNT1 A 0 A_GiveInventory ("Step", 1)
		Goto Spawn
	See2:
		PLAY CCCCCC 1
		PLAY DDDDDD 1
		TNT1 A 0 A_TakeInventory("Step", 1)
		Goto Spawn
	Missile:
		PLAY E 12 
		Goto Spawn
	Melee:
		PLAY F 6 Bright
		Goto Missile
	Pain:
		PLAY G 4 
		PLAY G 4 A_Pain
		Goto Spawn
	Death.Suicide:
	Death:
		PLAY H 10 A_PlayerScream
		PLAY I 10 
		PLAY J 10 A_NoBlocking
		PLAY K 10
		TNT1 A 0 A_PlaySound ("Misc/Body", CHAN_BODY)
		PLAY LM 10
		PLAY N -1
		Stop
	XDeath:
		TNT1 A 0 A_SpawnItemEx ("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 28.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		TNT1 A 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		PLAY O 5
		TNT1 A 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		PLAY P 5 A_XScream
		TNT1 A 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		PLAY Q 5 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		PLAY RSTUV 5
		PLAY W -1
		Stop
	Death.Fire:
		BURN ABC 5 Bright
		BURN D 5 Bright A_PlaySound ("*burndeath")
		BURN EFGHIJKLMN 5 Bright
		BURN O 5 Bright A_NoBlocking
		BURN PQRSTU 5 Bright
		BURN V -1
		Stop
	Death.Plasma:
		TNT1 A 0 Thing_SetTranslation(0,7)
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke",random (-16, 16),random (-16, 16),random (8, 16), 0, 0, 1)
		PLAY H 10 A_PlayerScream
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke",random (-16, 16),random (-16, 16),random (8, 16), 0, 0, 1)
		PLAY I 10 
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke",random (-16, 16),random (-16, 16),random (8, 16), 0, 0, 1)
		PLAY J 10 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke",random (-16, 16),random (-16, 16),random (8, 16), 0, 0, 1)
		PLAY K 10
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke",random (-16, 16),random (-16, 16),random (8, 16), 0, 0, 1)
		PLAY L 10 A_PlaySound("Misc/Body", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke",random (-16, 16),random (-16, 16),random (8, 16), 0, 0, 1)
		PLAY M 10
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke",random (-16, 16),random (-16, 16),random (8, 16), 0, 0, 1)
		PLAY NNNNNNNNNNNNNNNNNN 5 A_SpawnItemEx("BlackSmoke",random (-16,16),random (-16,16),random (8,16),0,0,1)
		PLAY N -1
		Stop
	}
}

// Null Weapon
Actor NullWeapon : Weapon {
	Tag "Unarmed"
	+Inventory.Undroppable +Weapon.Wimpy_Weapon +Weapon.CheatNotWeapon +NoInteraction
	+Weapon.NoAlert
	Height 0
	Radius 0
	States {
	Spawn:
		TNT1 A 0
		Stop
	Select:
		TNT1 A 1 A_Raise
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_Lower
		Wait
	Ready:
		TNT1 A 1 A_WeaponReady (WRF_NOFIRE|WRF_NOBOB|WRF_NOSWITCH)
		Loop
	Fire:
		TNT1 A 1
		Goto Ready
	}
}

Actor FDSprinting : Boolean { }
Actor SprintWeapon : NullWeapon {
	States {
	Ready:
		TNT1 AA 1 A_WeaponReady (WRF_NOFIRE|WRF_NOBOB|WRF_NOSWITCH)
		TNT1 A 1 A_GiveInventory ("FDSprinting", 1)
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("FDSprinting", 1, 1)
		Goto Ready3
		TNT1 A 1 A_WeaponReady (WRF_NOFIRE|WRF_NOBOB|WRF_NOSWITCH)
		Loop
	Ready3:
		TNT1 A 1 A_WeaponReady (WRF_NOFIRE|WRF_NOBOB)
		Loop
	Deselect:
		TNT1 BB 1 A_TakeInventory ("FDSprinting", 99999)
		TNT1 A 1 A_Lower
		TNT1 A 0 A_Lower
		Wait
	}
}

// Stamina Regeneration
Actor StaminaRegen : Boolean { }
Actor FDStamina : Counter { Inventory.MaxAmount 100 }

// Taunting
Actor Taunting   : Boolean { }
Actor FDTauntKey : CustomInventory {
	Inventory.Amount 1
	Inventory.MaxAmount 1
	-InvBar
	States {
	Use:
        TNT1 A 0 A_GiveInventory ("Taunting", 1)
        Fail
	}
}

// Auto Reload Enabled
Actor EnableAutoReload : Boolean { }

// Is Player
Actor IsPlayer : Boolean { }

// Reloading
Actor Reloading   : Boolean { }
Actor FDReloadKey : CustomInventory {
	Inventory.Amount 1
	Inventory.MaxAmount 1
	-InvBar
	States {
	Use:
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, 2)
        TNT1 A 0 A_GiveInventory ("Reloading", 1)
        Fail
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		Fail
	}
}

// Kicking
Actor Kicking   : Boolean { }
Actor FDKickKey : CustomInventory {
	Inventory.Amount 1
	Inventory.MaxAmount 1
	-InvBar
	States {
	Use:
        TNT1 A 0 A_GiveInventory ("Kicking", 1)
        Fail
	}
}

// Special action caller
Actor GoSpecial : Boolean { }

// Drop Weapon
Actor DropDatWeapon   : Boolean { }
Actor FDDropWeaponKey : CustomInventory {
	Inventory.Amount 1
	Inventory.MaxAmount 1
	-InvBar
	States {
	Use:
        TNT1 A 0 A_GiveInventory ("DropDatWeapon", 1)
        Fail
	}
}

// Chasecam Actor
Actor Chasecam {
	Height 0
	Radius 0
	+NoGravity
	States {
	Spawn:
		TNT1 A -1
		Stop
	}
}

/* ---------------------------------------------------- */
// Omega/Chronos class items
Actor ChronosItem : Boolean { }

/* ---------------------------------------------------- */

/* ---------------------------------------------------- */
// Marine/Nero class Items
Actor NeroItem : Boolean { }
/* ---------------------------------------------------- */

// By WildWeasel

// Underwater effects
Actor IsUnderwater : Boolean { }

// Base dropped Weapon
Actor DroppedWeaponBase {
	Projectile
	-NoGravity +Painless +BloodlessImpact
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "Weapons/gundrop"
}

// Synth-Fire|By Xaser
Actor SynthFireActive : Boolean { }
Actor SynthFireLeft   : Boolean { }
Actor SynthFireRight  : Boolean { }