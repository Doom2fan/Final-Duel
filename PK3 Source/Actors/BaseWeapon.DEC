// Taunt sprites by Blox
// The kick sprites are from Brutal Doom v19 SE
Actor FDWeapon : Weapon {
	Inventory.PickupSound "misc/gunpickup"
	Weapon.Kickback 100
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.5
	+Ammo_Optional +Weapon.NoAutoFire +Inventory.Undroppable
	States {
	Kicking:
		TNT1 A 0 A_GunFlash ("KillSynthFire", GFF_NOEXTCHANGE)
		TNT1 A 0 A_ZoomFactor (1.0)
		TNT1 A 0 A_TakeInventory ("Kicking", 1)
		TNT1 A 0 A_TakeInventory ("RGZoomed", 1)
		TNT1 A 0 A_TakeInventory ("SniperRifleZoomed", 1)
		TNT1 A 0 A_TakeInventory ("ShotgunZoomed", 1)
		// TNT1 A 0 A_TakeInventory ("RifleZoomed", 1)
		TNT1 A 0 A_JumpIf (momZ > 0, "AirKick")
		TNT1 A 0 A_JumpIf (momZ < 0, "AirKick")
	    TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "BerserkKick")
		KICK BCD 1
		KICK H 4 A_CustomPunch (random (30, 40), 1, 0, "MeleePuff")
		KICK IGFEDCBA 1
		TNT1 A 0 A_Jump (256, "Ready")
	AirKick:
	    TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "AirKick.Berserk")
		KICK JKLMN 1
        RIFF A 0 A_CustomPunch (random (50, 60), 1, 0, "MeleePuff")
        KICK OPQRST 2
		TNT1 A 0 A_Jump (256, "Ready")
	BerserkKick:
		KICK BCD 1
		KICK H 4 A_CustomPunch (3 * random (30, 40), 1, 0, "MeleePuffStrong")
		KICK IGFEDCBA 1
		TNT1 A 0 A_Jump (256, "Ready")
	AirKick.Berserk:
		KICK JKLMN 1
        RIFF A 0 A_CustomPunch (3 * random (50, 60), 1, 0, "MeleePuffStrong")
        KICK OPQRST 1
		TNT1 A 0 A_Jump (256, "Ready")
	Taunt:
		TNT1 A 0 A_GunFlash ("KillSynthFire", GFF_NOEXTCHANGE)
		TNT1 A 0 A_TakeInventory ("Taunting", 1)
		TNT1 A 0 A_FireBullets (0.0, 0.0, -1, 0, "TauntAlert", FBF_NOFLASH|FBF_NORANDOMPUFFZ)
		TNT1 A 0 A_PlaySound ("*offend", 5)
		TAUN AABBCCDDDDDDDCCBBAA 1
		TNT1 A 0 A_Jump (256, "Ready")
	DeselectDEAD:
		TNT1 A 0 A_StopSound (CHAN_Weapon)
		TNT1 A 0 A_StopSound (5)
		"####" "#" 1 A_Lower
		Loop
	KillSynthFire:
		TNT1 A 1
		Stop
	}
}

Actor TauntAlert {
	Radius 1
	Height 1
	DamageType Taunt
	+DontSplash +ForcePain +BloodlessImpact +PuffOnActors
	+AlwaysPuff
	States {
	Spawn:
	XDeath:
		TNT1 A 1 A_AlertMonsters
		Stop
	}
}