// Modify the internal Blood actor to spawn flying blood
actor NashGore_Blood : Blood replaces Blood
{
	-ALLOWPARTICLES
	Game Doom
	States
	{
	Spawn:
		BLUD C 8
		BLUD B 8 A_SpawnDebris("NashGore_FlyingBlood",1)
		BLUD A 8
		Stop
	}
}
Actor NashGore_BloodSplatter : NashGore_Blood Replaces BloodSplatter
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_ChangeVelocity (0.0, 0.0, 0.0, CVF_REPLACE)
		TNT1 A 0 A_SpawnItemEx("NashGore_Blood", 0.0, 0.0, 0.0,
		0.0, 0.0, 0.0, 0.0, SXF_TRANSFERTRANSLATION)
		Stop
	}
}

// Blood sprite that flies outwards and leaves trails behind
actor NashGore_FlyingBlood
{
	Game Doom
	Scale 0.75
	Health 1
	Radius 8
	Height 1
	Mass 1
	+CORPSE
	+NOTELEPORT
	+NOBLOCKMAP
	+ThruSpecies
	States
	{
	Spawn:
		FBLD A 1 NoDelay A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD A 1 
		FBLD A 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD A 1
		FBLD B 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD B 1
		FBLD B 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD B 1
		FBLD C 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD C 1
		FBLD C 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD C 1
		FBLD D 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD D 1
		FBLD D 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD D 1
		TNT1 A 0 A_JumpIf(waterlevel >= 1,"Water")
		Loop
	Water:
	Crash:
		TNT1 A 1 A_SpawnItem("NashGore_BloodSpot",0,0,0,1)
		Stop
	}
}

actor NashGore_BloodSpot
{
	Game Doom
	Radius 12
	Height 2
	Mass 1
	+NOTELEPORT
	-NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("bloodsplat")
		TNT1 A 0 A_Jump(64,4)
		TNT1 A 0 A_Jump(128,4)
		TNT1 A 0 A_Jump(192,4)
		TNT1 A 0 A_Jump(255,4)
		// make the blood spots stay on the ground. 1500 is the default duration.
		BSPT A 1000
		Goto Fade
		BSPT B 1000
		Goto Fade
		BSPT C 1000
		Goto Fade
		BSPT D 1000
		Goto Fade
		Fade:
		"----" A 3 A_FadeOut(0.03)
		Loop
	}
}

// Blood trails spawned by FlyingBlood
Actor NashGore_FlyingBloodTrail
{
	Game Doom
	Scale 0.70
	Mass 1
	+LOWGRAVITY
	+NOTELEPORT
	+NOBLOCKMAP
	States
	{
	Spawn:
		BTRL A 4
		BTRL B 4
		BTRL C 4
		BTRL D 4
		Stop
	}
}

// Gibbing blood
Actor NashGore_GibbingCounter : Counter { }
Actor NashGore_FlyingBloodGibbingSpawner
{ 
	States 
	{ 
	Spawn:
		TNT1 A 0 NoDelay A_GiveInventory("NashGore_GibbingCounter", random(8, 20))
	Looper:
		TNT1 A 0 A_JumpIfInventory("NashGore_GibbingCounter", 1, 1)
		Goto Stahp
		TNT1 A 0 A_SpawnDebris("NashGore_FlyingBloodGibbing",1)
		TNT1 A 0 A_TakeInventory("NashGore_GibbingCounter", 1)
		Loop
	Stahp:
		TNT1 A 0
		Stop
	}
}

// Blood sprite that flies outwards and leaves trails behind
actor NashGore_FlyingBloodGibbing : NashGore_FlyingBlood
{
	States
	{
	Spawn:
		FBLD A 1 NoDelay A_SpawnItem("NashGore_FlyingBloodTrailGibbing",0,0,0,1)
		FBLD A 1 
		FBLD A 1 A_SpawnItem("NashGore_FlyingBloodTrailGibbing",0,0,0,1)
		FBLD A 1
		FBLD B 1 A_SpawnItem("NashGore_FlyingBloodTrailGibbing",0,0,0,1)
		FBLD B 1
		FBLD B 1 A_SpawnItem("NashGore_FlyingBloodTrailGibbing",0,0,0,1)
		FBLD B 1
		FBLD C 1 A_SpawnItem("NashGore_FlyingBloodTrailGibbing",0,0,0,1)
		FBLD C 1
		FBLD C 1 A_SpawnItem("NashGore_FlyingBloodTrailGibbing",0,0,0,1)
		FBLD C 1
		FBLD D 1 A_SpawnItem("NashGore_FlyingBloodTrailGibbing",0,0,0,1)
		FBLD D 1
		FBLD D 1 A_SpawnItem("NashGore_FlyingBloodTrailGibbing",0,0,0,1)
		FBLD D 1
		TNT1 A 0 A_JumpIf(waterlevel >= 1,"Water")
		Loop
	Water:
	Crash:
		TNT1 A 1 A_SpawnItem("NashGore_BloodSpotGibbing",0,0,0,1)
		Stop
	}
}

actor NashGore_BloodSpotGibbing : NashGore_BloodSpot
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("bloodsplat")
		TNT1 A 0 A_Jump(64,4)
		TNT1 A 0 A_Jump(128,4)
		TNT1 A 0 A_Jump(192,4)
		TNT1 A 0 A_Jump(255,4)
		// make the blood spots stay on the ground. 1500 is the default duration.
		BSPT A 250
		Goto Fade
		BSPT B 250
		Goto Fade
		BSPT C 250
		Goto Fade
		BSPT D 250
		Goto Fade
	Fade:
		"----" A 3 A_FadeOut(0.03)
		Loop
	}
}
actor NashGore_FlyingBloodTrailGibbing : NashGore_FlyingBloodTrail
{
	States
	{
	Spawn:
		BTRL A 2
		BTRL B 2
		BTRL C 2
		BTRL D 2
		Stop
	}
}