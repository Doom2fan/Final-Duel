Actor FDViper1 30162 {
	//$Category Final Duel-Marines
	//$Title Viper (Rifle)
	Obituary "$OB_VIPERRIFLE"
	Health 120
	Radius 20
	Height 56
	Mass 100
	Speed 8
	GibHealth -60
	Species "Human"
	Monster
	+FloorClip +MissileMore
	PainChance 120
	PainChance "Kick", 255
	SeeSound "Viper/Sit"
	PainSound "Viper/Pain"
	DeathSound "Viper/Death"
	ActiveSound "Viper/Active"
	DropItem "Clip"
	DropItem "Energy10" DropItem "Energy10" DropItem "Energy10"
	States {
	Spawn:
	Idle:
		MRN1 A 1 A_Look
		Loop
	See:
		MRN1 AABBCCDD 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_JumpIfCloser (256, "Missile.FullAuto")
		TNT1 A 0 A_JumpIfCloser (512, "Missile.Burst")
		MRN1 OOOOOOOO 1 A_FaceTarget
		TNT1 A 0 A_PlaySound ("Rifle/Fire")
		MRN1 P 2 Bright A_CustomMissile ("MonsterTracer", 29, 8, frandom (-0.9, 0.9), 0, frandom (-0.9, 0.9))
		MRN1 O 5
		Goto See
	Missile.Burst:
		MRN1 E 2 A_FaceTarget
		TNT1 A 0 A_PlaySound ("Rifle/Fire")
		MRN1 F 3 Bright A_CustomMissile ("MonsterTracer", 29, 8, frandom (-2.0, 2.0), 0, frandom (-2.5, 2.5))
		MRN1 E 2 A_FaceTarget
		TNT1 A 0 A_PlaySound ("Rifle/Fire")
		MRN1 F 3 Bright A_CustomMissile ("MonsterTracer", 29, 8, frandom (-2.0, 2.0), 0, frandom (-2.5, 2.5))
		MRN1 E 2 A_FaceTarget
		TNT1 A 0 A_PlaySound ("Rifle/Fire")
		MRN1 F 3 Bright A_CustomMissile ("MonsterTracer", 29, 8, frandom (-2.0, 2.0), 0, frandom (-2.5, 2.5))
	Missile.Finish:
		MRN1 E 5
		Goto See
	Missile.FullAuto:
		MRN1 E 2 A_FaceTarget
		TNT1 A 0 A_PlaySound ("Rifle/Fire")
		MRN1 F 3 Bright A_CustomMissile ("MonsterTracer", 29, 8, frandom (-5.0, 5.0), 0, frandom (-6.0, 6.0))
		TNT1 A 0 A_MonsterRefire (128, "Missile.Finish")
		Goto Missile
	Pain:
		MRN1 G 3
		MRN1 G 3 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		MRN1 H 5
		MRN1 I 5 A_Scream
		MRN1 J 5 A_NoBlocking
		MRN1 K 5
		TNT1 A 0 A_PlaySound ("Misc/Body", CHAN_BODY)
		MRN1 LM 5
		MRN1 N -1
		Stop
	Death.SRType1:
	XDeath:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		TNT1 A 0 A_SpawnItemEx ("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 28.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		TNT1 A 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		PLAY O 5
		TNT1 A 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		PLAY P 5 A_XScream
		TNT1 A 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		PLAY Q 5 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		PLAY RSTUV 5
		PLAY W -1
		Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		BURN ABC 5 Bright Light (BURNDEATH)
		BURN D 5 Bright Light (BURNDEATH) A_PlaySound ("Viper/Burn")
		BURN EFGHI 5 Bright Light (BURNDEATH)
		BURN JKLMN 5 Bright Light (BURNDEATH)
		BURN O 5 Bright Light (BURNDEATH) A_NoBlocking
		BURN PQRSTU 5 Bright Light (BURNDEATH)
		BURN V -1
		Stop
	Death.Plasma:
		TNT1 A 0 Thing_SetTranslation (0, 7)
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		MRN1 H 10 A_Scream
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		MRN1 I 10
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		MRN1 J 10 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		MRN1 K 10
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		MRN1 L 10 A_PlaySound ("Misc/Body", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		MRN1 M 10
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		MRN1 NNNNNNNNNNNNNNNNNN 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		MRN1 N -1
		Stop
	Raise:
		TNT1 A 0 Thing_SetTranslation (0, 10)
		MRN1 NMLKJIH 5
		Goto See
	}
}