Actor TeslaCoil 30128
{
	//$Category Final Duel-Monsters
	//$Title Tesla Coil
  obituary "$OB_TESLACOIL"
  health 250
  radius 16
  height 56
  mass 10000
  speed 0
  MONSTER
  +FLOORCLIP
  +NOBLOOD
  +LOOKALLAROUND
  +SHORTMISSILERANGE
  States
  {
  Spawn:
    TESL A 1 A_Look
    TESL A 0 A_PlaySoundEx ("Tesla/Loop", "SoundSlot5", 1)
    Loop
  See:
    TESL A 1 A_Chase
    TESL A 0 A_PlaySoundEx ("Tesla/Loop", "SoundSlot5", 1)
    Loop
  Missile:
    TESL A 9 A_FaceTarget
    TESL A 1 bright
    TESL A 0 bright A_PlaySound ("Tesla/Attack")
    TESL A 0 bright A_SpawnItem ("TeslaZapper", 0, 60)
    TESL A 1 bright
    TESL A 59
    Goto See
  Death:
	TNT1 A 0 A_GiveToTarget("XPSystemExperience",150)
	TESL A 1
    TESL A 1 bright A_SpawnItemEx ("Boom", 0, 0, 60, 0, 0, 0, 0, 160)
    TESL A 0 bright A_StopSoundEx ("SoundSlot5")
    TESL A 0 bright A_PlaySound ("weapons/rocklx")
    TESL B -1 
    Stop
  Raise:
    Stop
  }
}

Actor TeslaZapper
{
  obituary "Death:%o was zapped by a tesla coil."
  radius 2
  height 2
  //Decal "CrossbowScorch2"
  +NOGRAVITY
  +LOOKALLAROUND
  +NOCLIP
  +INVULNERABLE
  States
  {
  Spawn:
    TNT1 A 1 A_FaceTarget
  Missile:
    TNT1 AAAAA 0 bright A_CustomRailgun (2, 0, none, lightblue, 1, 0, 1000)
    TNT1 AAAAA 0 bright A_CustomRailgun (2, 0, none, white, 1, 0, 1000)
    Stop
  }
}

Actor Boom
{
  Radius 11
  Height 8
  Speed 0
  Damage 0
  Projectile
  renderstyle Add
  +NOINTERACTION
  States
  {
  Spawn:
    MISL B 8 bright
    MISL C 6 bright
    MISL D 4 bright
    Stop
  }
}
