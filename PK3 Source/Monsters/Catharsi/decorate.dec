Actor Catharsi 30114 {
	//$Category Final Duel-Monsters
	//$Title Catharsi
	Obituary "$OB_CATHARSI"
	Health 120
	Radius 20
	Height 56
	Mass 100
	Speed 8
	GibHealth -60
	Species "Imp"
	PainChance 140
	PainChance "Kick", 255
	SeeSound "catharsi/sit"
	PainSound "catharsi/pain"
	DeathSound "catharsi/death"
	ActiveSound "imp/active"
	Monster
	+FloorClip
	DropItem "HellClip"
	DropItem "Energy10" DropItem "Energy10" DropItem "Energy10" DropItem "Energy5" 128
	States {
	Pain.Kick:
		SDDM A 1 A_Pain
		SDDM A 1 A_FaceTarget (0, 0, 1)
		TNT1 A 0 ThrustThing (angle * 256 / 360 + 128, 25, 0, 0)
		TNT1 A 0 ThrustThingZ (0, 25, 0, 0)
		Goto Spawn
	Spawn:
	Idle:
		SDDM A 10 A_Look
		Loop
	See:
		SDDM AABBCCDD 3 A_Chase
		Loop
	Missile:
		SDDM E 9
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound ("SoulReaper/Fire")
		SDDM F 2 Bright A_CustomMissile ("CatharsiBall", 30, 8)
		SDDM E 4
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound ("SoulReaper/Fire")
		SDDM F 2 Bright A_CustomMissile ("CatharsiBall", 30, 8)
		SDDM E 4
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound ("SoulReaper/Fire")
		SDDM F 2 Bright A_CustomMissile ("CatharsiBall", 30, 8)
		SDDM E 9
		Goto See
	Pain:
		SDDM G 3
		SDDM G 3 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("CatharsiBody")
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		TNT1 A 0 A_SpawnItemEx ("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 28.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		SDDM O 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		SDDM O 5
		SDDM P 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		SDDM P 5 A_XScream
		SDDM Q 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		SDDM Q 5 A_NoBlocking
		SDDM R 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		SDDM RSTUV 5
		SDDM W -1
		Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("CatharsiBodyFire")
		Stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("CatharsiBodyPlasma")
		Stop
	Death.Saw:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("CatharsiBodySawed")
		Stop
	}
}

Actor CatharsiBall : SRLaser {
	Speed 35
	Damage (random (1, 4) * 5)
	States {
	Death:
		TNT1 A 0 A_Scream
		TNT1 A 1 A_SpawnItem ("SRLaserExplosion2", 0, 0, 0)
		Stop
	}
}

Actor CatharsiDetonator {
	Radius 6
	Height 8
	SeeSound "catharsi/shotsit"
	DeathSound "catharsi/shotdth"
	+FloatBob +Randomize +NoGravity
	var int user_Angle; // 340
	States {
	Spawn:
		THRM A 4 A_PlaySound ("catharsi/thermtick")
		THRM B 16
		THRM A 4 A_PlaySound ("catharsi/thermtick")
		THRM B 16
		THRM A 4 A_PlaySound ("catharsi/thermtick")
		THRM B 16
		THRM A 4 A_PlaySound ("catharsi/thermtick")
		THRM B 16
		THRM A 4 A_PlaySound ("catharsi/thermtick")
		THRM B 16
		TNT1 A 0 A_PlaySound ("SoulReaper/Fire")
	Loople:
		TNT1 A 0 A_JumpIf (user_Angle >= 340, "End")
		TNT1 A 0 A_CustomMissile ("CatharsiBall", 0, 0, user_Angle, CMF_ABSOLUTEPITCH)
		TNT1 A 0 A_SetUserVar ("user_Angle", user_Angle + 20)
		Loop
	End:
		SDFB BCDE 6 Bright A_Die
		Stop
	}
}

Actor CatharsiWeapon {
	States {
	Spawn:
		SDDC R -1
		Stop
	}
}

Actor CatharsiBody : FD_DeadBodyBase {
	States {
	Spawn:
		SDDM H 5
		SDDM I 5
		SDDM J 5
		SDDM K 0 A_SpawnItem ("CatharsiDetonator", 0, 30)
		SDDM KLM 5
		SDDM N -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("DoomImp")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("CatharsiWeapon")
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor CatharsiBodyFire : FD_DeadBodyBase {
	States {
	Spawn:
		BURN ABC 5 Bright Light (BURNDEATH)
		BURN D 5 Bright Light (BURNDEATH) A_PlaySound ("catharsi/pain")
		BURN EFGHI 5 Bright Light (BURNDEATH)
		TNT1 A 0 A_PlaySound ("catharsi/pain")
		BURN JKLMN 5 Bright Light (BURNDEATH)
		BURN O 5 Bright Light (BURNDEATH)
		TNT1 A 0 A_PlaySound ("catharsi/pain")
		BURN PQRSTU 5 Bright Light (BURNDEATH)
		BURN V -1
		Stop
	}
}

Actor CatharsiBodyPlasma : FD_DeadBodyBase {
	BloodColor Black
	States {
	Spawn:
		TNT1 A 0 NoDelay Thing_SetTranslation (0, 7)
		SDDM H 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM I 5 A_Scream
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM J 5 A_NoBlocking
		SDDM K 0 A_SpawnItem ("CatharsiDetonator", 0, 30)
		SDDM KLM 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM NNNNNNNNNNNNNNNNNN 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM N -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("DoomImp")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor CatharsiBodySawed : FD_DeadBodyBase {
	States { // Sprites by ItsNatureToDie
	Spawn:
		SDDC A 5
		SDDC B 2
		TNT1 A 0 A_SpawnItem ("CatharsiDetonator", 0, 30)
		TNT1 A 0 A_SpawnItemEx ("CatharsiWeapon")
		SDDC C 3 A_SpawnItemEx ("CatharsiBodySawedTop", 0.0, 0.0, 28.0, 0.45, 0.0, 2.5)
		SDDC DEFGH 5
		SDDC I -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("DoomImp")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor CatharsiBodySawedTop : FD_DeadBodyBase {
	States { // Sprites by ItsNatureToDie
	Spawn:
		SDDC J 1
		Loop
	Crash:
		SDDC KLMNOP 5
		SDDC Q -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}