Actor Catharsi 30114 {
	//$Category Final Duel-Monsters
	//$Title Catharsi
	Obituary "$OB_CATHARSI"
	Health 120
	Radius 20
	Height 56
	Mass 100
	Speed 8
	GibHealth -60
	Species "Imp"
	PainChance 140
	PainChance "Kick", 255
	SeeSound "catharsi/sit"
	PainSound "catharsi/pain"
	DeathSound "catharsi/death"
	ActiveSound "imp/active"
	Monster
	+FloorClip
	DropItem "HellClip"
	DropItem "Energy10" DropItem "Energy10" DropItem "Energy10" DropItem "Energy5" 128
	States {
	Pain.Kick:
		SDDM A 1 A_Pain
		SDDM A 1 A_FaceTarget (0, 0, 1)
		TNT1 A 0 ThrustThing (angle * 256 / 360 + 128, 25, 0, 0)
		TNT1 A 0 ThrustThingZ (0, 25, 0, 0)
		Goto Spawn
	Spawn:
	Idle:
		SDDM A 10 A_Look
		Loop
	See:
		SDDM AABBCCDD 3 A_Chase
		Loop
	Missile:
		SDDM E 9
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound ("SoulReaper/Fire")
		SDDM F 2 Bright A_CustomMissile ("CatharsiBall", 30, 8)
		SDDM E 4
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound ("SoulReaper/Fire")
		SDDM F 2 Bright A_CustomMissile ("CatharsiBall", 30, 8)
		SDDM E 4
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound ("SoulReaper/Fire")
		SDDM F 2 Bright A_CustomMissile ("CatharsiBall", 30, 8)
		SDDM E 9
		Goto See
	Pain:
		SDDM G 3
		SDDM G 3 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		SDDM H 5
		SDDM I 5 A_Scream
		SDDM J 5 A_NoBlocking  
		SDDM J 0 A_SpawnItem ("CatharsiDetonator", 0, 30)
		SDDM KLM 5
		SDDM N -1
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		TNT1 A 0 A_SpawnItemEx ("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 28.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		SDDM O 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		SDDM O 5
		SDDM P 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		SDDM P 5 A_XScream
		SDDM Q 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		SDDM Q 5 A_NoBlocking
		SDDM R 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		SDDM RSTUV 5
		SDDM W -1
		Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 60)
		BURN ABC 5 Bright Light (BURNDEATH)
		BURN D 5 Bright Light (BURNDEATH) A_PlaySound ("catharsi/pain")
		BURN EFGHI 5 Bright Light (BURNDEATH)
		TNT1 A 0 A_PlaySound ("catharsi/pain")
		BURN JKLMN 5 Bright Light (BURNDEATH)
		BURN O 5 Bright Light (BURNDEATH) A_NoBlocking
		TNT1 A 0 A_PlaySound ("catharsi/pain")
		BURN PQRSTU 5 Bright Light (BURNDEATH)
		BURN V -1
		Stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 130)
		TNT1 A 0 Thing_SetTranslation (0, 7)
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM H 5
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM I 5 A_Scream
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM J 5 A_NoBlocking  
		SDDM J 0 A_SpawnItem("CatharsiDetonator", 0, 30)
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM K 5
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM L 5
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM M 5
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM NNNNNNNNNNNNNNNNNN 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		SDDM N -1
		stop
	Raise:
		TNT1 A 0 Thing_SetTranslation (0, 10)
		SDDM NMLKJIH 5
		Goto See
	}
}

Actor CatharsiBall : SRLaser {
	Speed 35
	Damage (random (1, 4) * 5)
    States {
    Death:
		TNT1 A 0 A_Scream
		TNT1 A 1 A_SpawnItem ("SRLaserExplosion2", 0, 0, 0)
		Stop
    }
}

Actor CatharsiDetonator {
	Radius 6
	Height 8
	Speed 0
	SeeSound "catharsi/shotsit"
	DeathSound "catharsi/shotdth"
	Monster
	+FloatBob +Randomize +NoGravity +NoBlood
	var int user_Angle; // 340
	States {
	Spawn:
		THRM A 4 A_PlaySound ("catharsi/thermtick")
		THRM B 16 
		THRM A 4 A_PlaySound ("catharsi/thermtick")
		THRM B 16
		THRM A 4 A_PlaySound ("catharsi/thermtick")
		THRM B 16
		THRM A 4 A_PlaySound ("catharsi/thermtick")  
		THRM B 16
		THRM A 4 A_PlaySound ("catharsi/thermtick")  
		THRM B 16
		TNT1 A 0 A_PlaySound ("SoulReaper/Fire")
	Loople:
		TNT1 A 0 A_JumpIf (user_Angle >= 340, "End")
		TNT1 A 0 A_CustomMissile ("CatharsiBall", 0, 0, user_Angle, CMF_ABSOLUTEPITCH)
		TNT1 A 0 A_SetUserVar ("user_Angle", user_Angle + 20)
		Loop
	End:
		SDFB BCDE 6 Bright A_Die
		Stop
	}
}
