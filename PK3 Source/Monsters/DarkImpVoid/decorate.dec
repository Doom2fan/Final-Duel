Actor VoidDarkImp 30118 {
	//$Category Final Duel-Monsters
	//$Title Void Dark Imp
	Health 130
	GibHealth -70
    Radius 20
    Height 56
    Speed 7
    PainChance 200
	PainChance "Kick", 255
    Monster
    +FloorClip
	Species "Imp"
	SeeSound "imp/sight"
	PainSound "imp/pain"
	DeathSound "imp/death"
	ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "$OB_DARKIMP"
    HitObituary "$OB_DARKIMP_MELEE"
    MissileType DarkNormal
    MeleeDamage 3
	DropItem "Energy100" DropItem "Energy20" DropItem "Energy5" // 125
    States {
	Spawn:
	Idle:
        DRKV A 10 A_Look
        Loop
    See:
        DRKV AABBCCDD 3 A_Chase
        Loop
    Melee:
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_ComboAttack
        Goto See
    Missile:
        DRKV E 0 A_Jump (64, 4)
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_CustomMissile ("DarkNormal", 32, 0, 0)
        Goto Missile+7
        DRKV IJ 8 Bright A_FaceTarget
        DRKV K 6 Bright A_CustomMissile ("DarkStrong", 32, 0, 0)
        DRKV E 0 A_Jump (64, 4)
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_CustomMissile ("DarkNormal", 32, 0, 0)
        Goto See
        DRKV IJ 8 Bright A_FaceTarget
        DRKV K 6 Bright A_CustomMissile ("DarkStrong", 32, 0, 0)
        Goto See
    Pain:
        DRKV H 2
        DRKV H 2 A_Pain
        Goto See
	Pain.Kick:
		DRKV A 1 A_Pain
		DRKV A 1 A_FaceTarget
		TNT1 A 0 ThrustThing (angle*256/360+128, 25, 0, 0)
		TNT1 A 0 ThrustThingZ (0, 25, 0, 0)
		Goto Spawn
    Death:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 120)
		TNT1 A 0
        DRKV L 8
        DRKV M 8 A_Scream
        DRKV N 5
        DRKV O 5 A_Fall
        DRKV P -1
        Stop
    XDeath:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 120)
		TNT1 A 0 A_SpawnItemEx ("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 28.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		DRKI N 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		DRKI N 5
		DRKI O 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		DRKI O 5 A_XScream
		DRKI P 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		DRKI P 5
		DRKI Q 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		DRKI Q 5 A_NoBlocking
		DRKI RST 5
		DRKI U -1
        Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 120)
		BURN ABC 5 Bright Light (BURNDEATH)
		BURN D 5 Bright Light (BURNDEATH) A_PlaySound ("imp/pain")
		BURN EFGHI 5 Bright Light (BURNDEATH)
		TNT1 A 0 A_PlaySound ("imp/pain")
		BURN JKLMN 5 Bright Light (BURNDEATH)
		BURN O 5 Bright Light (BURNDEATH) A_NoBlocking
		TNT1 A 0 A_PlaySound ("imp/pain")
		BURN PQRSTU 5 Bright Light (BURNDEATH)
		BURN V -1
		Stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 120)
		TNT1 A 0 Thing_SetTranslation (0, 7)
		DRKV L 8 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		DRKV M 8 A_Scream
		DRKV N 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		TNT1 A 0 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		DRKV O 5 A_Fall
		DRKV PPPPPPPPPPPPPPPPPP 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		DRKV P -1
		stop
    Raise:
		TNT1 A 0 Thing_SetTranslation (0, 10)
        DRKV PO 8
        DRKV NML 6
        Goto Spawn
    }
}

Actor DarkNormal {
    Radius 8
    Height 6
    Speed 9
    Damage 3
    Projectile
    RenderStyle Add
    Alpha 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
    States {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

Actor DarkStrong {
    Radius 8
    Height 6
    Speed 9
    Damage 6
    Projectile
    RenderStyle Add
    Alpha 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
    States {
    Spawn:
        DKB2 AB 4 Bright A_SpawnItemEx ("DarkTail", 0, 0, 0, 0, 0, 0, 0, 128)
        Loop
    Death:
        DKB2 CDE 4 Bright
        Stop
    }
}

Actor DarkTail {
    Projectile
	RenderStyle Add
    +NoClip
    Alpha 0.5
    States {
    Spawn:
        DKBT ABCDEF 2 Bright
        Stop
    }
}