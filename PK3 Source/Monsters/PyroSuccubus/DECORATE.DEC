//===========================================================================
//
// Pyro Succubus
//
//===========================================================================

Actor PyroSuccubus 30126 {
	//$Category Final Duel-Monsters
	//$Title "Pyro Succubus"
	SpawnID 65
	Obituary "A pyrosuccubus was too hot for %o."
	Health 200
	Radius 16
	Height 50
	Mass 200
	Speed 12
	Scale 0.95
	MissileType FireBall1
	DamageFactor Ice, 1.5
	DamageFactor Fire, 0.2
	PainChance 95
	SeeSound "pyr/sight"
	PainSound "pyr/pain"
	DeathSound "pyr/death"
	ActiveSound "pyr/active"
	RenderStyle Translucent
	Monster
	+NoTarget +FloorClip +DontHurtSpecies
	Species "Succubus"
	DropItem "Energy100"
	States {
	Spawn:
	Idle:
		SUC1 A 10 Bright A_Look
		Loop
	Pain.Kick:
		SUC1 A 1 A_Pain
		SUC1 A 1 A_FaceTarget (0, 0, 1)
		TNT1 A 0 ThrustThing (angle * 256 / 360 + 128, 25, 0, 0)
		TNT1 A 0 ThrustThingZ (0, 25, 0, 0)
		Goto Spawn
	See:
		TNT1 A 0 A_ChangeFlag ("Float", 0)
		TNT1 A 0 A_ChangeFlag ("NoGravity", 0)
		TNT1 A 0 A_ChangeFlag ("DropOff", 0)
		SUC1 AA 3 Bright A_Chase
		SUC1 A 0 Bright A_SpawnItemEx ("WalkFire1", 0, 0, 0, 1, 0, 0, 0, 128)
		SUC1 BB 3 Bright A_Chase
		SUC1 B 0 Bright A_SpawnItemEx ("WalkFire1", 0, 0, 0, 1, 0, 0, 0, 128)
		SUC1 CC 3 Bright A_Chase
		SUC1 C 0 Bright A_SpawnItemEx ("WalkFire1", 0, 0, 0, 1, 0, 0, 0, 128)
		SUC1 DD 3 Bright A_Chase
		SUC1 D 0 Bright A_SpawnItemEx ("WalkFire1", 0, 0, 0, 1, 0, 0, 0, 128)
		SUC1 A 0 A_Jump (32, "Grav")
		Loop
	Grav:
		TNT1 A 0 A_JumpIf (Z + height + 100 >= CeilingZ, "See")
		TNT1 A 0 A_ChangeFlag ("NoGravity", 1)
		TNT1 A 0 A_ChangeFlag ("DropOff", 1)
		TNT1 A 0 A_ChangeFlag ("Float", 1)
		Goto Fly
	Fly:
		SUFY AA 4 Bright ThrustThingZ (0, 15, 0, 0)
		SUFY AABBCCDD 2 Bright
		Goto Hover
	Land:
		SUFY AA 4 Bright ThrustThingZ (0, -20, 0, 0)
		SUFY A 1 Bright A_CheckFloor ("See")
		SUFY AABBCCDD 2 Bright
		SUFY A 1 Bright A_CheckFloor ("See")
		Goto Hover
	Hover:
		SUFY AABBCCDD 2 Bright A_Chase (0, "MissileF")
		SUFY A 0 Bright A_Jump (64, "Fly")
		SUFY A 0 Bright A_Jump (32, "Land")
		Loop
	Missile:
		TNT1 A 0 A_Jump (85, "TripleShot")
		Goto QuadrupleShot
	QuadrupleShot:
		SUC1 E 8 Bright A_FaceTarget
		SUC1 F 8 Bright A_FaceTarget
		SUC1 G 8 Bright A_ComboAttack
		SUC1 E 8 Bright A_FaceTarget
		SUC1 F 8 Bright A_FaceTarget
		SUC1 G 0 Bright A_CustomMissile ("FireBall1", 27, 0, 8)
		SUC1 G 0 Bright A_ComboAttack
		SUC1 G 8 Bright A_CustomMissile ("FireBall1", 27, 0, -8)
		SUC1 E 8 Bright
		Goto See
	TripleShot:
		SUC1 E 8 Bright A_FaceTarget
		SUC1 F 8 Bright A_FaceTarget
		SUC1 G 0 Bright A_CustomMissile ("FireBall1", 27, 0, 8)
		SUC1 G 0 Bright A_ComboAttack
		SUC1 G 8 Bright A_CustomMissile ("FireBall1", 27, 0, -8)
		SUC1 E 8 Bright
		Goto See
	MissileF:
		TNT1 A 0 A_Jump (85, "TripleShotF")
		Goto QuadrupleShotF
	QuadrupleShotF:
		SUFY E 8 Bright A_FaceTarget
		SUFY F 8 Bright A_FaceTarget
		SUFY G 8 Bright A_ComboAttack
		SUFY E 8 Bright A_FaceTarget
		SUFY F 8 Bright A_FaceTarget
		SUFY G 0 Bright A_CustomMissile ("FireBall1", 27, 0, 8)
		SUFY G 0 Bright A_ComboAttack
		SUFY G 8 Bright A_CustomMissile ("FireBall1", 27, 0, -8)
		SUFY E 8 Bright
		Goto Hover
	TripleShotF:
		SUFY E 8 Bright A_FaceTarget
		SUFY F 8 Bright A_FaceTarget
		SUFY G 0 Bright A_CustomMissile ("FireBall1", 27, 0, 8)
		SUFY G 0 Bright A_ComboAttack
		SUFY G 8 Bright A_CustomMissile ("FireBall1", 27, 0, -8)
		SUFY E 8 Bright
		Goto Hover
	Pain:
		SUC1 L 2 Bright
		SUC1 L 2 Bright A_Pain
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 200)
		SUC1 M 7 Bright
		SUC1 N 7 Bright A_Scream
		SUC1 O 7 Bright
		SUC1 P 7  A_NoBlocking
		SUC1 QRST 7
		SUC1 U -1
		Stop
	Raise:
		SUC1 UTSRQ 8 Bright
		SUC1 P 0 A_PlaySoundEx ("pyr/resurrected", 3)
		SUC1 PONM 8 Bright
		Goto See
	}
}

Actor PyroSuccubusWalk : PyroSuccubus 9481 {
	//$Category Final Duel-Monsters
	//$Title "Pyro Succubus - Walking"
	SpawnId 166
	Species "Succubus"
	DropItem "Energy100"
	States {
	Spawn:
		SUC1 AB 10 Bright A_Look
		Loop
	See:
		SUC1 AA 3 Bright A_Chase
		SUC1 A 0 Bright A_SpawnItemEx ("WalkFire1", 0, 0, 0, 1, 0, 0, 0, 128)
		SUC1 BB 3 Bright A_Chase
		SUC1 B 0 Bright A_SpawnItemEx ("WalkFire1", 0, 0, 0, 1, 0, 0, 0, 128)
		SUC1 CC 3 Bright A_Chase
		SUC1 C 0 Bright A_SpawnItemEx ("WalkFire1", 0, 0, 0, 1, 0, 0, 0, 128)
		SUC1 DD 3 Bright A_Chase
		SUC1 D 0 Bright A_SpawnItemEx ("WalkFire1", 0, 0, 0, 1, 0, 0, 0, 128)
		Loop
	}
}

Actor PyroSuccubusFly : PyroSuccubus 9482 {
	//$Category Final Duel-Monsters
	//$Title "Pyro Succubus - Flying"
	SpawnId 167
	+NoGravity +Float
	Species "Succubus"
	DropItem "Energy100"
	States {
	Spawn:
		SUFY ABCD 10 Bright A_Look
		Loop
	See:
		SUFY AABBCCDDAABBCCDD 2 Bright A_Chase
		Loop
	Missile:
		TNT1 A 0 A_Jump (85, "TripleShot")
		Goto QuadrupleShot
	QuadrupleShot:
		SUFY E 8 Bright A_FaceTarget
		SUFY F 8 Bright A_FaceTarget
		SUFY G 8 Bright A_ComboAttack
		SUFY E 8 Bright A_FaceTarget
		SUFY F 8 Bright A_FaceTarget
		SUFY G 0 Bright A_CustomMissile ("FireBall1", 27, 0, 8)
		SUFY G 0 Bright A_ComboAttack
		SUFY G 8 Bright A_CustomMissile ("FireBall1", 27, 0, -8)
		SUFY E 8 Bright
		Goto See
	TripleShot:
		SUFY E 8 Bright A_FaceTarget
		SUFY F 8 Bright A_FaceTarget
		SUFY G 0 Bright A_CustomMissile ("FireBall1", 27, 0, 8)
		SUFY G 0 Bright A_ComboAttack
		SUFY G 8 Bright A_CustomMissile ("FireBall1", 27, 0, -8)
		SUFY E 8 Bright
		Goto See
	}
}

Actor FireBall1 {
	Radius 8
	Height 8
	Speed 15
	Damage 5
	DamageType fire
	Projectile
	+Randomize
	RenderStyle "ADD"
	Alpha 0.7
	SeeSound "Weapons/pyrobrn"
	DeathSound "Weapons/pyroxxpl"
	Decal BaronScorch
	States {
	Spawn:
		BRB2 AB 1 Bright A_SpawnItemEx ("PyroTail", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	Death:
		BRBA K 0 A_CustomMissile ("Fire2", 4, 2, 0)
		BRBA KLMNOPQRSTUVWX 3 Bright
		Stop
	}
}

//********************

Actor Fire2 {
	+NoClip +Missile +DropOff +Randomize
	DamageType Fire
	Scale 1.5
	Speed 1
	Damage 1
	RenderStyle Add
	Alpha 0.67
	ExplosionDamage 3
	ExplosionRadius 64
	States {
	Spawn:
		TNT1 A 8
		FRTF A 3 Bright
		FRTF A 0 A_Explode
		FRTF B 3 Bright
		FRTF B 0 A_Explode
		FRTF C 3 Bright
		FRTF C 0 A_Explode
		FRTF C 0
		FRTF C 0 A_Jump (191, 4)
		FRTF C 0 A_CustomMissile ("Fire2", 0, 24, 0)
		FRTF C 0 A_CustomMissile ("Fire2", 0, 0, 0)
		FRTF C 0 A_CustomMissile ("Fire2", 0, -24, 0)
		FRTF DEFGHIJKLMNO 3 Bright
		Stop
	}
}

//********************

Actor Fire1 {
	+NoClip +Missile +DropOff
	Speed 2
	Damage 1
	States {
	Spawn:
		TNT1 A 10 A_CustomMissile ("Fire2", 0, 0, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, 16, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, -16, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, 0, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, 16, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, -16, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, 0, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, 16, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, -16, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, 0, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, 16, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, -16, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, 0, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, 16, 0)
		TNT1 A 10 A_CustomMissile ("Fire2", 0, -16, 0)
		Stop
	}
}

Actor WalkFire1 {
	+NoClip +Missile +DropOff +Randomize
	Scale 1.5
	Speed 1
	RenderStyle Add
	Alpha 0.67
	States {
	Spawn:
		TNT1 A 8
		FRTF A 3 Bright
		FRTF A 0
		FRTF B 3 Bright
		FRTF B 0
		FRTF C 3 Bright
		FRTF C 0
		FRTF C 0
		FRTF DEFGHIJKLMNO 3 Bright
		Stop
	}
}

Actor PyroTail {
	Projectile
	Scale 1.1
	RenderStyle Add
	+NoClip
	Alpha 0.4
	States {
	Spawn:
		PYTL ABCDEFGHI 1 Bright
		Stop
	}
}