//=================================================================================================
//
// Super Shotgun Guy
//
//=================================================================================================

Actor FDSuperShotgunGuy 30134 {
	//$Category Final Duel-Monsters
	//$Title Super Shotgun Guy
	Game Doom
	Health 120
	GibHealth -60
	Radius 20
	Height 56
	Mass 100
	Speed 8
	GibHealth -25
	PainChance 170
	Monster
	+FloorClip
	Species "Corrupted Human"
	SeeSound "shotguy/sight"
	PainSound "shotguy/pain"
	DeathSound "shotguy/death"
	ActiveSound "shotguy/active"
	Obituary "$OB_SSGGUY"
	DamageFactor "SRType1", 1.0
	PainChance "Kick", 255
	DropItem "SpawnedDoomSSG"
	DropItem "Energy10" DropItem "Energy10" DropItem "Energy10" DropItem "Energy10" 192 DropItem "Energy10" 128
	States {
	Spawn:
	Idle:
		GPOS A 10 A_Look
		Loop
	Pain.Kick:
		GPOS A 1 A_Pain
		GPOS A 1 A_FaceTarget (0, 0, 1)
		TNT1 A 0 ThrustThing (angle * 256 / 360 + 128, 25, 0, 0)
		TNT1 A 0 ThrustThingZ (0, 25, 0, 0)
		Goto Spawn
	See:
		GPOS AABBCCDD 4 A_Chase
		Loop
	Missile:
		GPOS E 10 A_FaceTarget
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ShotgunnerTracer", 29, 8, frandom (-5.0, 5.0), 0, frandom (-6.0, 6.0))
		GPOS F 1 Bright A_PlaySound ("DoubleBarrelShotgun/Fire", CHAN_WEAPON)
		GPOS E 1
		TNT1 A 0 A_PlaySound ("DoubleBarrelShotgun/Open")
		GPOS A 12
		GPOS A 9
		TNT1 A 0 A_PlaySound ("DoubleBarrelShotgun/Load")
		GPOS A 12
		TNT1 A 0 A_PlaySound ("DoubleBarrelShotgun/Close")
		GPOS A 12
		Goto See
	Pain:
		GPOS G 3
		GPOS G 3 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 120)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Scream
		TNT1 A 0 A_SpawnItemEx ("SuperShotgunGuyBody")
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 120)
		TNT1 A 0 A_SpawnItemEx ("NashGore_FlyingBloodGibbingSpawner", 0.0, 0.0, 28.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR)
		GPOS O 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem ("NashGore_GibGenerator", 0, 0, 0, 0)
		GPOS O 5
		GPOS P 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem ("NashGore_GibGenerator", 0, 0, 0, 0)
		GPOS P 5 A_XScream
		GPOS Q 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem ("NashGore_GibGenerator", 0, 0, 0, 0)
		GPOS Q 5 A_NoBlocking
		GPOS R 0 A_SpawnItemEx ("NashGore_GibGenerator", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_USEBLOODCOLOR) //A_SpawnItem ("NashGore_GibGenerator", 0, 0, 0, 0)
		GPOS RS 5
		GPOS T -1
		Stop
	Death.Fire:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 120)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("SuperShotgunGuyBodyFire")
		Stop
	Death.Plasma:
		TNT1 A 0 A_GiveToTarget ("XPSystemExperience", 120)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Scream
		TNT1 A 0 A_SpawnItemEx ("SuperShotgunGuyBodyPlasma")
		Stop
	Raise:
		Stop
	}
}

Actor SuperShotgunGuyBody : FD_DeadBodyBase {
	States {
	Spawn:
		GPOS HIJKLM 5
		GPOS N -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("ShotgunGuy")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall")
		Stop
	}
}

Actor SuperShotgunGuyBodyFire : FD_DeadBodyBase {
	States {
	Spawn:
		BURN ABC 5 Bright Light (BURNDEATH)
		BURN D 5 Bright Light (BURNDEATH) A_PlaySound ("shotguy/pain")
		BURN EFGHI 5 Bright Light (BURNDEATH)
		TNT1 A 0 A_PlaySound ("shotguy/pain")
		BURN JKLMN 5 Bright Light (BURNDEATH)
		BURN O 5 Bright Light (BURNDEATH)
		TNT1 A 0 A_PlaySound ("shotguy/pain")
		BURN PQRSTU 5 Bright Light (BURNDEATH)
		BURN V -1
		Stop
	}
}

Actor SuperShotgunGuyBodyPlasma : FD_DeadBodyBase {
	BloodColor Black
	States {
	Spawn:
		TNT1 A 0 NoDelay Thing_SetTranslation (0, 7)
		GPOS HIJKLM 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		CPS2 NNNNNNNNNNNNNNNNNN 5 A_SpawnItemEx ("BlackSmoke", random (-16, 16), random (-16, 16), random (8, 16), 0, 0, 1)
		GPOS N -1 A_ChangeFlag ("Shootable", TRUE)
		Stop
	Raise:
		TNT1 A 0 A_SpawnItemEx ("ShotgunGuy")
		TNT1 A 0 A_SpawnItemEx ("TeleportFog")
	Death:
		TNT1 A 0 A_PlaySound ("misc/gibbed", CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("NashGore_CrushedSmall", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SXF_TRANSFERTRANSLATION)
		Stop
	}
}