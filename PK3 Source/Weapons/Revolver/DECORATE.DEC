Actor SpawnedMagnum : BaseSpawnedWeapon 30083 {
	Inventory.PickupMessage "$GOTMAGNUM"
	States {
	Spawn:
		REVP A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory ("Magnum", 0, "GrabAmmo")
		TNT1 A 0 A_TakeInventory ("AmmoMagnum", 999999999)
		TNT1 A 0 A_GiveInventory ("Magnum", 1)
		TNT1 A 0 A_GiveInventory ("AmmoMagnum", 999999999)
		Stop
	GrabAmmo:
		TNT1 A 0 A_JumpIfInventory ("MagnumClip", 0, "Failed")
		TNT1 A 0 A_GiveInventory ("MagnumClip", 1)
		Stop
	}
}
Actor MagnumCasingSpawner {
	Speed 20
	PROJECTILE
	+NoClip
	States {
	Spawn:
        TNT1 A 0
		TNT1 A 0 A_CustomMissile ("9mmCasing", -5, -1, 0, 2, -90)
		TNT1 A 0 A_CustomMissile ("9mmCasing", -5, -3, 0, 2, -90)
		TNT1 A 0 A_CustomMissile ("9mmCasing", -5, -5, 0, 2, -90)
		TNT1 A 0 A_CustomMissile ("9mmCasing", -5, 1, 0, 2, -90)
		TNT1 A 0 A_CustomMissile ("9mmCasing", -5, 3, 0, 2, -90)
		TNT1 A 1 A_CustomMissile ("9mmCasing", -5, 5, 0, 2, -90)
		Stop
	}
}
Actor AmmoMagnum : Ammo {
	Inventory.MaxAmount 6
	Ammo.BackpackMaxAmount 6
	+IGNORESKILL
}
Actor DroppedMagnum : DroppedWeaponBase {
	States {
	Spawn:
		REVP A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx ("Magnum")
		Stop
	}
}
Actor MagnumUpgrade : Boolean { }
Actor Magnum : FDWeapon {
	//$Category Final Duel-Weapons
	//$Title .357 Magnum
	Tag "$TAGMAGNUM"
	Inventory.PickupMessage "$GOTMAGNUM"
	Obituary "$OBMAGNUM"
	Weapon.AmmoType1 "AmmoMagnum"
	Weapon.AmmoType2 "MagnumClip"
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 0
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 0
	+Ammo_Optional +NoAlert
	States {
	Ready:
		REVS CBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		REVG A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedMagnum", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("Magnum", 1)
		Stop
	Select:
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		REVS ABC 1
		TNT1 A 0 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory ("MagnumUpgrade", 1, "Fire.Upgraded")
		REVG A 0 A_JumpIfNoAmmo ("DryFire")
		REVG A 2
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound ("Magnum/Fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("RevolverTracer", frandom (-2.0, 2.0), 1, 0, -3, 0, frandom (-2.5, 2.5))
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -0.6, 0, Sin(Pitch) * 0.6, 1)
		REVF AA 1 Bright A_SetPitch (pitch - 1.5, SPF_INTERPOLATE)
		REVF CE 1 A_SetPitch (pitch - 0.8, SPF_INTERPOLATE)
		REVF D 1 A_SetPitch (pitch + 1.1, SPF_INTERPOLATE)
		REVF C 1 A_SetPitch (pitch + 0.8, SPF_INTERPOLATE)
		REVF B 1 A_SetPitch (pitch + 0.6, SPF_INTERPOLATE)
		REVG A 7
		Goto Ready2
	Fire.Upgraded:
		REVG A 0 A_JumpIfNoAmmo ("DryFire")
		REVG A 1
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound ("Magnum/Fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("UpgradedRevolverTracer", frandom (-0.5, 0.5), 1, 0, -3, 0, frandom (-1.0, 1.0))
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -0.3, 0, Sin(Pitch) * 0.3, 1)
		REVF AA 1 Bright A_SetPitch (pitch - 0.75, SPF_INTERPOLATE)
		REVF CE 1 A_SetPitch (pitch - 0.4, SPF_INTERPOLATE)
		REVF D 1 A_SetPitch (pitch + 0.55, SPF_INTERPOLATE)
		REVF C 1 A_SetPitch (pitch + 0.4, SPF_INTERPOLATE)
		REVF B 1 A_SetPitch (pitch + 0.3, SPF_INTERPOLATE)
		REVG A 4
		Goto Ready2
	DryFire:
		REVG A 0 A_PlaySound ("Weapons/empty", 7)
		REVG A 0 A_JumpIfInventory ("EnableAutoReload", 1, "Reload")
		Goto Ready2
	Reload:
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		REVG A 0 A_JumpIfInventory ("AmmoMagnum", 6, "Ready2")
		TNT1 A 0 A_JumpIfInventory ("MagnumClip", 1, 1)
		Goto Ready2
		REVR ABCDEFGH 1
		TNT1 A 4
		TNT1 A 0 A_PlaySound ("Magnum/Open", CHAN_WEAPON)
		TNT1 A 8
		TNT1 A 0 A_PlaySound ("Magnum/Out", CHAN_WEAPON)
		TNT1 A 12 A_FireCustomMissile ("MagnumCasingSpawner", -4, 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("AmmoMagnum", 6)
		TNT1 A 0 A_TakeInventory ("MagnumClip", 1)
		TNT1 A 0 A_GiveInventory ("AmmoMagnum", 6)
		TNT1 A 0 A_PlaySound ("Magnum/In", CHAN_WEAPON)
		TNT1 A 8
		TNT1 A 4
		REVR HGFEDCBA 1
		Goto Ready2
	Spawn:
		REVP A -1
		Stop
	}
}