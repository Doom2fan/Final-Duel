#include "Weapons/PlasmaRifle/PRProjectile.DEC"
// By Alex_Mercer
Actor SpawnedPlasmaRifle : BaseSpawnedWeapon 30034 {
	Inventory.PickupMessage "$GOTPLASMA"
	States {
	Spawn:
		PLSM Z -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory ("FDPlasmaRifle", 0, "GrabAmmo")
		TNT1 A 0 A_TakeInventory ("AmmoPRifle", 999999999)
		TNT1 A 0 A_GiveInventory ("FDPlasmaRifle", 1)
		TNT1 A 0 A_GiveInventory ("AmmoPRifle", 999999999)
		Stop
	GrabAmmo:
		TNT1 A 0 A_JumpIfInventory ("Cell2", 0, "Failed")
		TNT1 A 0 A_GiveInventory ("Cell2", 2)
		Stop
	}
}

Actor AmmoPRifle : AMMO {
	Inventory.MaxAmount 80
	Ammo.BackpackMaxAmount 80
	+IgnoreSkill
}

Actor DroppedPlasmaRifle : DroppedWeaponBase {
	States {
	Spawn:
		PLSM Z 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx ("FDPlasmaRifle")
		Stop
	}
}

Actor PlasmaRifleUpgrade : Boolean { }

Actor FDPlasmaRifle : FDWeapon {
	//$Category Final Duel-Weapons
	//$Title Plasma Rifle
	//$Sprite PLSMZ0
	Weapon.SisterWeapon FDPlasmaRiflePowered
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 20
	Weapon.AmmoGive1 0
	Weapon.AmmoType "AmmoPRifle"
	Weapon.AmmoType2 "AmmoPRifle"
	Tag "$TAGPRIFLE"
	+Weapon.Ammo_Optional +Weapon.Alt_Ammo_Optional +Weapon.NoAlert +Weapon.NoAutofire
	Inventory.PickupMessage "$GOTPLASMA"
	Inventory.PickupSound "misc/gunpickup"
	Weapon.UpSound "PlasmaRifle/draw"
	States {
	Ready:
		PLSS DCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1 , "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		PLSM A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedPlasmaRifle", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("FDPlasmaRifle", 1)
		Stop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		PLSS ABCD 1
		PLSM A 0 A_Lower
		Wait
	Select:
		PLSM A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		PLSF A 0 A_PlaySound ("PlasmaRifle/fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("PlasFlareSpawn", 0, 0, 0, 0)
		PLSF A 0 A_FireCustomMissile ("FDPlasmaBall", 0, 1, 0, -8, 0, 0)
		PLSF A 2 Bright A_ChangeVelocity (Cos (Pitch) * -0.2, 0, Sin (Pitch) * 0.2, 1)
		PLSF A 0 A_AlertMonsters
		PLSF G 2
		PLSF H 1
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire1")
		PLSF B 0 A_ReFire
		PLSF E 4 A_PlaySound ("PlasmaRifle/cool", 6)
		TNT1 AAAAA 0 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random (10, 15), random (0, 3))
		PLSF FE 4
		PLSM BABA 2
		Goto Ready2
	DryFire1:
		TNT1 A 0 A_ClearRefire
		PLSF E 4 A_PlaySound ("PlasmaRifle/cool", 6)
		TNT1 AAAAA 0 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random (10, 15), random (0, 3))
		PLSF FE 4
		PLSM BABA 2
		PLSM A 14 A_PlaySound ("PlasmaRifle/Dryfire", CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory ("EnableAutoReload", 1, "Reload")
		Goto Ready2
	DryFire:
		TNT1 A 0 A_ClearRefire
		PLSM A 0 A_PlaySound ("PlasmaRifle/Dryfire", 7)
		TNT1 A 0 A_JumpIfInventory ("AmmoPRifle", 1, "Ready2")
		TNT1 A 0 A_JumpIfInventory ("EnableAutoReload", 1, "Reload")
		Goto Ready2
	Reload:
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("Cell2", 1, 1)
		Goto Ready2
		TNT1 A 0 A_JumpIfInventory ("AmmoPRifle", 80, "Ready2")
		PLSF E 4
		PLSR ABC 3
		PLSR D 3 A_PlaySound ("PlasmaRifle/cellout", CHAN_WEAPON)
		PLSR EFG 3
		TNT1 A 0 A_FireCustomMissile ("PlasmaCaseSpawn", 0, 0, 0, 0)
		PLSR HHHHGFE 3
		PLSR D 3 A_PlaySound ("PlasmaRifle/cellin", CHAN_WEAPON)
		PLSR CBA 3
		PLSF E 4
		TNT1 A 0 A_PlaySound ("Weapons/Beep", CHAN_WEAPON)
		PLSM CB 1
		TNT1 A 0 A_PlaySound ("Weapons/Beep", CHAN_WEAPON)
		PLSM A 1
		TNT1 A 0 A_GiveInventory ("AmmoPRifle", 80)
		TNT1 A 0 A_TakeInventory ("Cell2", 1)
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_PlaySound ("Weapons/BeepStartLoop", CHAN_WEAPON)
		PLSM A 1
		TNT1 A 0 A_PlaySound ("Weapons/BeepLoop", CHAN_WEAPON, 1.0, 1)
	AltHold:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "CancelAlt")
		PLSF BBCC 1 A_FireCustomMissile ("PlasFlareSpawn", 0, 0, 0, 0)
		TNT1 A 0 A_Refire ("AltHold")
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_JumpIfInventory ("PlasmaRifleUpgrade", 1, "AltHold2")
		TNT1 A 0 A_ThingDamage (10, "WeaponOverload", TDF_PIERCEARMOR)
	AltHold2:
		PLSF A 0 A_PlaySound ("PlasmaRifle/Altfire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("PlasFlareSpawn", 0, 0, 0, 0)
		TNT1 A 0 A_FireCustomMissile ("FDPlasmaBallAlt", -3, 0, 0, -8, 0, 1.3)
		TNT1 A 0 A_FireCustomMissile ("FDPlasmaBallAlt", -4.5, 0, 0, -8, 0, 0)
		TNT1 A 0 A_FireCustomMissile ("FDPlasmaBallAlt", -3, 0, 0, -8, 0, -1.3)
		TNT1 A 0 A_FireCustomMissile ("FDPlasmaBallAlt", 3, 0, 0, -8, 0, 1.3)
		TNT1 A 0 A_FireCustomMissile ("FDPlasmaBallAlt", 4.5, 0, 0, -8, 0, 0)
		TNT1 A 0 A_FireCustomMissile ("FDPlasmaBallAlt", 3, 0, 0, -8, 0, -1.3)
		TNT1 A 0 A_FireCustomMissile ("FDPlasmaBallAlt", 0, 1, 0, -8, 0, 0)
		TNT1 A 0 Radius_Quake (8, 6, 0, 1, 0)
		TNT1 A 0 A_SetPitch (pitch - 3.0, SPF_INTERPOLATE)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -5.0, 0, Sin (Pitch) * 5.0, 1)
		PLSF D 2 Bright
		PLSF A 0 A_AlertMonsters
		PLSF GGH 1
		PLSF E 4 A_PlaySound ("PlasmaRifle/cool", 6)
		PLSF FFFFFFFFFF 1 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random (10, 15), random (0, 3))
		PLSF F 5
		PLSF E 4
		PLSM BABA 2
		Goto Ready2
	CancelAlt:
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_PlaySound ("Weapons/BeepX2", CHAN_WEAPON)
		PLSM BABA 2
		Goto Ready2
	Spawn:
		PLSM Z -1
		Stop
	}
}

Actor FDPlasmaRiflePowered : FDPlasmaRifle {
	Weapon.SisterWeapon FDPlasmaRifle
	+Weapon.Powered_Up
	States {
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		PLSP A 0 A_PlaySound ("PlasmaRifle/fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("PlasMissileFlareSpawn", 0, 0, 0, 0)
		PLSF A 0 A_FireCustomMissile ("PlasmaMissile", 0, 1, 0, -8, 0, 0)
		PLSP A 2 Bright A_ChangeVelocity (Cos (Pitch) * -0.2, 0, Sin (Pitch) * 0.2, 1)
		PLSF A 0 A_AlertMonsters
		PLSF G 2
		PLSF H 1
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire1")
		PLSF B 0 A_ReFire
		PLSF E 4 A_PlaySound ("PlasmaRifle/cool", 6)
		TNT1 AAAAA 0 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random (10, 15), random (0, 3))
		PLSF FE 4
		PLSM BABA 2
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_PlaySound ("Weapons/BeepStartLoop", CHAN_WEAPON, 0.7)
		PLSM A 1
		TNT1 A 0 A_PlaySound ("Weapons/BeepLoop", CHAN_WEAPON, 0.7, 1)
	AltHold:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "CancelAlt")
		PLSP BBCC 1 A_FireCustomMissile ("PlasMissileFlareSpawn", 0, 0, 0, 0)
		TNT1 A 0 A_Refire ("AltHold")
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_JumpIfInventory ("PlasmaRifleUpgrade", 1, "AltHold2")
		TNT1 A 0 A_ThingDamage (10, "WeaponOverload", TDF_PIERCEARMOR)
	AltHold2:
		PLSP A 0 A_PlaySound ("PlasmaRifle/Altfire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("PlasMissileFlareSpawn", 0, 0, 0, 0)
		TNT1 A 0 A_FireCustomMissile ("PlasmaMissileAlt", -3, 0, 0, -8, 0, 1.3)
		TNT1 A 0 A_FireCustomMissile ("PlasmaMissileAlt", -4.5, 0, 0, -8, 0, 0)
		TNT1 A 0 A_FireCustomMissile ("PlasmaMissileAlt", -3, 0, 0, -8, 0, -1.3)
		TNT1 A 0 A_FireCustomMissile ("PlasmaMissileAlt", 3, 0, 0, -8, 0, 1.3)
		TNT1 A 0 A_FireCustomMissile ("PlasmaMissileAlt", 4.5, 0, 0, -8, 0, 0)
		TNT1 A 0 A_FireCustomMissile ("PlasmaMissileAlt", 3, 0, 0, -8, 0, -1.3)
		TNT1 A 0 A_FireCustomMissile ("PlasmaMissileAlt", 0, 1, 0, -8, 0, 0)
		TNT1 A 0 Radius_Quake (8, 6, 0, 1, 0)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -5.0, 0, Sin (Pitch) * 5.0, 1)
		PLSP D 2 Bright
		PLSP A 0 A_AlertMonsters
		PLSF GGH 1
		PLSF E 4 A_PlaySound ("PlasmaRifle/cool", 6)
		PLSF FFFFFFFFFF 1 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random (10, 15), random (0, 3))
		PLSF F 5
		PLSF E 4
		PLSM BABA 2
		Goto Ready2
	}
}