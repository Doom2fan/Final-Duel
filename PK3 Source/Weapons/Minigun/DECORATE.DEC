Actor SpawnedMinigun : BaseSpawnedWeapon 30068 {
	Inventory.PickupMessage "$GOTMINIGUN"
	States {
	Spawn:
		MING Z -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory ("FDMinigun", 0, "GrabAmmo")
		TNT1 A 0 A_TakeInventory ("AmmoMinigun", 999999999)
		TNT1 A 0 A_GiveInventory ("FDMinigun", 1)
		TNT1 A 0 A_GiveInventory ("AmmoMinigun", 999999999)
		Stop
	GrabAmmo:
		TNT1 A 0 A_JumpIfInventory ("MinigunAmmo", 0, "Failed")
		TNT1 A 0 A_GiveInventory ("MinigunAmmo", 1)
		Stop
	}
}

Actor MinigunUpgrade : Boolean { }
Actor MinigunSpin : Boolean { }
Actor AmmoMinigun : Ammo {
	Inventory.MaxAmount 120
	Ammo.BackpackMaxAmount 120
	+IGNORESKILL
}
Actor DroppedMinigun : DroppedWeaponBase {
	States {
	Spawn:
		MING Z 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("FDMinigun")
		Stop
	}
}
Actor FDMinigun : FDWeapon {
	//$Category Final Duel-Weapons
	//$Sprite MINGZ0
	//$Title "Parias" Minigun
	Tag "$TAGMINIGUN"
	Inventory.PickupSound "misc/gunpickup"
	Weapon.AmmoUse 1
	Weapon.AmmoGive1 0
	Weapon.AmmoType "AmmoMinigun"
	Weapon.AmmoType2 "MinigunAmmo"
	Inventory.PickupMessage "$GOTMINIGUN"
	Obituary "$OBMINIGUN"
	+Weapon.NoAlert
	States {
	Ready:
		TNT1 A 0 A_PlaySound("Minigun/Up", CHAN_WEAPON)
		MINS CBA 1
	Ready2:
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_JumpIfInventory("DropDatWeapon",1,"DropWeapon")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		MING A 1 A_WeaponReady
		Loop
	Ready2.Spin:
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_JumpIfInventory("DropDatWeapon",1,"StopSpinning")
		TNT1 A 0 A_JumpIfNoAmmo("StopSpinning")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		MING AC 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile("DroppedMinigun", 0, 0, 0)
		TNT1 A 0 A_TakeInventory("FDMinigun", 1)
		Stop
	Select:
		TNT1 A 0 ACS_NamedExecute("FDMinigunScript",0)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		TNT1 A 0 A_JumpIfInventory("MinigunSpin",1,"Deselect.Spin")
		MINS ABC 1
		TNT1 A 0 A_Lower
		Wait
	Deselect.Spin:
		TNT1 A 0 ACS_NamedExecute("PlayerSpeed", 0, 100)
		TNT1 A 0 A_TakeInventory("MinigunSpin")
		TNT1 A 0 A_PlaySound("Minigun/WindDown",5)
		MING AB 1
		MING CD 2
		Goto Deselect
	Fire:
		TNT1 A 0 A_JumpIfInventory("MinigunSpin",1,"Fire.Spin")
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_PlaySound("Minigun/WindUp",5,1.0,1)
		MING ABCD 2
		MING ABCD 1
		TNT1 A 0 A_PlaySound("Minigun/Spin",5,1)
		TNT1 A 0 A_Refire("Hold")
		TNT1 A 0 A_PlaySound("Minigun/WindDown",5)
		MING AB 1
		MING CD 2
		Goto Ready2
	Hold:
		TNT1 A 0 A_JumpIfInventory("MinigunUpgrade",1,"Hold.Upgrade")
		TNT1 A 0 A_JumpIfNoAmmo("Dryfire1")
		TNT1 A 0 A_PlaySound("Minigun/Fire",CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("MinigunTracer", frandom(-4.0,4.0), 1, 0, 0, 0, frandom(-4.5,4.5))
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawn",5,0,8,0)
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -0.4, 0, Sin(Pitch) * 0.4, 1)
		MING E 1 Bright A_SetPitch(pitch - 0.625, SPF_INTERPOLATE)
		MING BCD 1 A_SetPitch(pitch - 0.625, SPF_INTERPOLATE)
		TNT1 A 0 A_Refire
		TNT1 A 0 A_PlaySound("Minigun/WindDown",5)
		MING AB 1
		MING CD 2
		MING AAAA 1 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random(-3,3), 0)
		Goto Ready2
	Hold.Upgrade:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_PlaySound("Minigun/Fire",CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("MinigunTracerUpgraded", frandom(-3.0,3.0), 1, 0, 0, 0, frandom(-3.5,3.5))
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawn",5,0,8,0)
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -0.3, 0, Sin(Pitch) * 0.3, 1)
		MING E 1 Bright A_SetPitch(pitch - 0.500, SPF_INTERPOLATE)
		MING BCD 1 A_SetPitch(pitch - 0.500, SPF_INTERPOLATE)
		TNT1 A 0 A_Refire
		TNT1 A 0 A_PlaySound("Minigun/WindDown",5)
		MING AB 1
		MING CD 2
		MING AAAA 1 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random(-3,3), 0)
		Goto Ready2
	Fire.Spin:
		TNT1 A 0 A_JumpIfInventory("MinigunUpgrade",1,"Fire.Spin.Upgrade")
		TNT1 A 0 A_JumpIfNoAmmo("StopSpinning")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Minigun/Fire",CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("MinigunTracer", frandom(-8.0,8.0), 1, 0, 0, 0, frandom(-9.0,9.0))
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawn",5,0,8,0)
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -0.8, 0, Sin(Pitch) * 0.8, 1)
		MING E 1 Bright A_SetPitch(pitch - 1.6666666666666666666666666666667, SPF_INTERPOLATE)
		MING C 1 A_SetPitch(pitch - 1.6666666666666666666666666666667, SPF_INTERPOLATE)
		TNT1 A 0 A_Refire("Fire.Spin2")
		Goto Ready2.Spin
	Fire.Spin2:
		TNT1 A 0 A_JumpIfInventory("MinigunUpgrade",1,"Fire.Spin.Upgrade")
		TNT1 A 0 A_JumpIfNoAmmo("StopSpinning")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Minigun/Fire",6)
		TNT1 A 0 A_FireCustomMissile("MinigunTracer", frandom(-8.0,8.0), 1, 0, 0, 0, frandom(-9.0,9.0))
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawn",5,0,8,0)
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -0.8, 0, Sin(Pitch) * 0.8, 1)
		MING E 1 Bright A_SetPitch(pitch - 1.6666666666666666666666666666667, SPF_INTERPOLATE)
		MING C 1 A_SetPitch(pitch - 1.6666666666666666666666666666667, SPF_INTERPOLATE)
		TNT1 A 0 A_Refire("Fire.Spin")
		Goto Ready2.Spin
	Fire.Spin.Upgrade:
		TNT1 A 0 A_JumpIfNoAmmo("StopSpinning")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Minigun/Fire",CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("MinigunTracerUpgraded", frandom(-7.0,7.0), 1, 0, 0, 0, frandom(-8.0,8.0))
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawn",5,0,8,0)
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -0.8, 0, Sin(Pitch) * 0.8, 1)
		MING E 1 Bright A_SetPitch(pitch - 1.6666666666666666666666666666667, SPF_INTERPOLATE)
		MING C 1 A_SetPitch(pitch - 1.6666666666666666666666666666667, SPF_INTERPOLATE)
		TNT1 A 0 A_Refire("Fire.Spin.Upgrade2")
		Goto Ready2.Spin
	Fire.Spin.Upgrade2:
		TNT1 A 0 A_JumpIfNoAmmo("StopSpinning")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Minigun/Fire",6)
		TNT1 A 0 A_FireCustomMissile("MinigunTracerUpgraded", frandom(-7.0,7.0), 1, 0, 0, 0, frandom(-8.0,8.0))
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawn",5,0,8,0)
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -0.8, 0, Sin(Pitch) * 0.8, 1)
		MING E 1 Bright A_SetPitch(pitch - 1.6666666666666666666666666666667, SPF_INTERPOLATE)
		MING C 1 A_SetPitch(pitch - 1.6666666666666666666666666666667, SPF_INTERPOLATE)
		TNT1 A 0 A_Refire("Fire.Spin.Upgrade")
		Goto Ready2.Spin
	DryFire:
		TNT1 A 0 A_JumpIfInventory("MinigunSpin",1,"StopSpinning")
		TNT1 A 0 A_PlaySound("Weapons/empty", 7)
		TNT1 A 0 A_JumpIfInventory("EnableAutoReload",1,"Reload")
		Goto Ready2
	DryFire1:
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_JumpIfInventory("MinigunSpin",1,"StopSpinning")
		TNT1 A 0 A_PlaySound("Minigun/WindDown",5)
		MING AB 1
		MING CD 2
		MING AAAA 1 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random(-3,3), 0)
		TNT1 A 0 A_JumpIfInventory("EnableAutoReload",1,"Reload")
		Goto Ready2
	Reload:
		TNT1 A 0 A_JumpIfInventory("MinigunSpin",1,"StopSpinning")
		TNT1 A 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("AmmoMinigun",120,"Ready2")
        TNT1 A 0 A_JumpIfInventory("MinigunAmmo",1,1)
        Goto Ready2
		MING A 2
		MINR ABCDEF 2
		MINR F 2
		TNT1 A 0 A_FireCustomMissile("MinigunAmmoSpawn",0,0,0,0)
		MINR F 140 A_PlaySound("Minigun/Reload", CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("AmmoMinigun",120)
		TNT1 A 0 A_TakeInventory("MinigunAmmo",1)
		TNT1 A 0 A_GiveInventory("AmmoMinigun",120)
		MINR FEDCBA 2
		MING A 2
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AmmoMinigun",1,1)
		Goto Ready2
		TNT1 A 0 A_JumpIfInventory("MinigunSpin",1,"StopSpinning")
		TNT1 A 0 ACS_NamedExecute("PlayerSpeed", 0, 50)
		TNT1 A 0 A_GiveInventory("MinigunSpin", 1)
		TNT1 A 0 A_PlaySound("Minigun/WindUp",5,1)
		MING ABCD 2
		MING ABCD 1
		TNT1 A 0 A_PlaySound("Minigun/Spin",5,1.0,1)
		Goto Ready2.Spin
	StopSpinning:
		TNT1 A 0 ACS_NamedExecute("PlayerSpeed", 0, 100)
		TNT1 A 0 A_TakeInventory("MinigunSpin", 1)
		TNT1 A 0 A_PlaySound("Minigun/WindDown",5)
		MING AB 1
		MING CD 2
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		Goto Ready2
	Spawn:
		MING Z -1
		Stop
	}
}