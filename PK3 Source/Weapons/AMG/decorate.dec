// Spawned ActionMachineGun
Actor SpawnedActionMachineGun : BaseSpawnedWeapon 30007 {
	Inventory.PickupMessage "$GOTACTIONMACHINEGUN"
	States {
	Spawn:
		AGUN A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory ("ActionMachineGun", 0, "GrabAmmo")
		TNT1 A 0 A_TakeInventory ("AmmoActionMachinegun", 999999999)
		TNT1 A 0 A_GiveInventory ("ActionMachineGun", 1)
		TNT1 A 0 A_GiveInventory ("AmmoActionMachinegun", 999999999)
		Stop
	GrabAmmo:
		TNT1 A 0 A_JumpIfInventory ("RifleMag", 0, "Failed")
		TNT1 A 0 A_GiveInventory ("RifleMag", 1)
		Stop
	}
}

Actor DroppedAMG : DroppedWeaponBase {
	States {
	Spawn:
		AGUN A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx ("ActionMachineGun")
		Stop
	}
}

Actor ActionMachineGun : FDWeapon {
	//$Category Final Duel-Weapons
	//$Title M9A15 Carbine
	//$Sprite AGUNA0
	Tag "$TAGAMG"
	Inventory.PickupSound "misc/gunpickup"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType "AmmoActionMachinegun"
	Weapon.AmmoType2 "Clip2"
	Inventory.RestrictedTo OmegaClass
	Inventory.PickupMessage "$GOTACTIONMACHINEGUN"
	Obituary "$OBACTIONMACHINEGUN"
	+Weapon.NoAlert +Inventory.RestrictAbsolutely
	States {
	Ready:
		AMGS CBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		AMGG A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedAMG", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("ActionMachineGun", 1)
		Stop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		AMGS ABC 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_Raise
		Loop
	Fire:
		AMGG A 0 A_JumpIfInventory ("AMGMode2", 1, "SemiautomaticFire")
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		AMGF A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound ("AMG/Fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_FireCustomMissile ("AMGTracer", frandom (-3.0, 3.0), 1, 0, 0, 0, frandom (-3.0, 3.0))
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.2, 0, Sin (Pitch) * 0.2, 1)
		AMGF AAA 1 Bright A_SetPitch (pitch - 0.16666666666666666666666666666667, SPF_INTERPOLATE)
		AMGF B 1
		AMGG A 1
		AMGG A 0 A_ReFire
		Goto Ready2
	SemiautomaticFire:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		AMGF A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound ("AMG/Fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_FireCustomMissile ("AMGTracer", frandom (-2.7, 2.7), 1, 0, 0, 0, frandom (-2.7, 2.7))
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.2, 0, Sin (Pitch) * 0.2, 1)
		AMGF AAA 1 Bright A_SetPitch (pitch - 0.16666666666666666666666666666667, SPF_INTERPOLATE)
		AMGF B 1
		AMGG A 1
		Goto Ready2
	DryFire:
		AMGG A 10 A_PlaySound ("Weapons/empty", 7)
		TNT1 A 0 A_JumpIfInventory ("EnableAutoReload", 1, "Reload")
		Goto Ready2
	Reload:
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("AmmoActionMachinegun", 50, "Ready2")
		TNT1 A 0 A_JumpIfInventory ("Clip2", 1, 1)
		Goto Ready2
		AMGG ABCDEFG 2
		AMGG H 3 A_PlaySound ("AMG/ClipOut", CHAN_WEAPON)
		AMGG IJKL 2
		AMGG M 24 A_FireCustomMissile ("EmptyClipSpawn", 0, 0, 0, 0)
		AMGG LKJIH 2
		AMGG G 3 A_PlaySound ("AMG/ClipIn", CHAN_WEAPON)
		AMGG FEDCBA 2
		TNT1 A 0 A_TakeInventory ("Clip2", 1, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory ("AmmoActionMachinegun", 50)
		Goto Ready2
	Altfire:
		TNT1 A 0 A_PlaySound ("Weapons/Mode", 5, 1.5)
		TNT1 A 0 A_JumpIfInventory ("AMGMode2", 1, 3)
		TNT1 A 0 A_GiveInventory ("AMGMode2", 1)
		TNT1 A 0 A_Print ("- Semiautomatic mode -")
		Goto Ready2
		TNT1 A 0 A_TakeInventory ("AMGMode2", 1)
		TNT1 A 0 A_Print ("- Automatic mode -")
		Goto Ready2
	Spawn:
		AGUN A -1
		Stop
	}
}

Actor AMGMode2 : Boolean { }

Actor AmmoActionMachinegun : AMMO {
	Inventory.MaxAmount 51
	Ammo.BackpackMaxAmount 51
	+IgnoreSkill
}