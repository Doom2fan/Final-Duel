Actor DroppedAMG : DroppedWeaponBase
{
	States
	{
	Spawn:
		AGUN A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("ActionMachineGun")
		Stop
	}
}
Actor ActionMachineGun : FDWeapon 30007
{
	//$Category Final Duel-Weapons
	//$Title M9A15 Carbine
	//$Sprite AGUNA0
	Tag "$TAGAMG"
	Inventory.PickupSound "misc/gunpickup"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType "AmmoActionMachinegun"
	Weapon.AmmoType2 "Clip2"
	Inventory.RestrictedTo OmegaClass
	+NoAlert
	+Weapon.NoAutoFire
	+Inventory.RestrictAbsolutely
	Inventory.PickupMessage "$GOTACTIONMACHINEGUN"
	Obituary "$OBACTIONMACHINEGUN"
	+Ammo_Optional
	States
	{
	Ready:
		AMGS DCBA 1 
	Ready2:
		TNT1 A 0 A_JumpIfInventory("AMGZoomed",1,"ZReady")
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("DropDatWeapon",1,"DropWeapon")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		AMGG AA 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory("PressingZoom", 999)
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile("DroppedAMG", 0, 0, 0)
		TNT1 A 0 A_TakeInventory("ActionMachineGun", 1)
		Stop
	ZReady:
		TNT1 A 0 A_JumpIfInventory("AMGZoomed",1,1)
		Goto Ready2
		TNT1 A 0 A_JumpIfInventory("DropDatWeapon",1,"Unzoom")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_TakeInventory("Kicking",1)
		TNT1 A 0 A_TakeInventory("Taunting",1)
		SNPS AA 1 A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory("PressingZoom", 999)
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("AMGZoomed",1,"Deselect.Unzoom")
		TNT1 A 0 A_JumpIfHealthLower(0,"DeselectDEAD")
		TNT1 A 0 A_ZoomFactor(1.0)
		AMGS ABCD 1
		TNT1 A 0 A_TakeInventory("AMGZoomed",999)
		TNT1 A 0 A_Lower
		Wait
	Deselect.Unzoom:
		TNT1 A 0 A_TakeInventory("AMGZoomed",999)
		TNT1 A 0 A_PlaySound("Weapons/Zoom", CHAN_Weapon)
		TNT1 A 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
		TNT1 A 0 A_SetCrosshair(0)
		AMGG A 10 A_SetBlend("00 00 00", 1.0, 20)
		AMGG A 10
		Goto Deselect
	Select:
		TNT1 A 0 A_TakeInventory("AMGZoomed",999)
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("AMGZoomed",1,"ZFire")
		AMGG A 0 A_JumpIfInventory("AMGMode2",1,"SemiautomaticFire")
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		AMGF A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("AMG/Fire", CHAN_Weapon)
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_FireCustomMissile("AMGTracer", frandom(-3.0,3.0), 1, 2, 3, 0, frandom(-3.0,3.0))
		TNT1 A 0 A_Recoil(cos(pitch) * 0.2)
		AMGF AAA 1 Bright A_SetPitch(pitch - 0.16666666666666666666666666666667)
		AMGG A 1
		AMGG A 0 A_ReFire
		Goto Ready2
	SemiautomaticFire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		AMGF A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("AMG/Fire", CHAN_Weapon)
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_FireCustomMissile("AMGTracer", frandom(-2.7, 2.7), 1, 3, -2, 0, frandom(-2.7, 2.7))
		TNT1 A 0 A_Recoil(cos(pitch) * 0.2)
		AMGF AAA 1 Bright A_SetPitch(pitch - 0.16666666666666666666666666666667)
		AMGG A 1
		Goto Ready2
	ZFire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		SNPS A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("AMG/Fire", CHAN_Weapon)
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_FireCustomMissile("AMGTracer", frandom(-2.5, 2.5), 1, 0, -4, 0, frandom(-2.5, 2.5))
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		SNPS A 1 A_SetPitch(pitch - 0.125)
		TNT1 A 0 A_SetPitch(pitch - 0.125)
		SNPS A 1 A_GunFlash
		SNPS A 4
		Goto Ready2
	DryFire:
		AMGG A 0 A_PlaySound("Weapons/empty")
		TNT1 A 0 A_JumpIfInventory("EnableAutoReload",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("AMGZoomed",1,"ZReady")
		Goto Ready2
    Reload:
		TNT1 A 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("AmmoActionMachinegun",50,"Ready2")
        TNT1 A 0 A_JumpIfInventory("Clip2",1,1)
        Goto NoAmmo
		TNT1 A 0 A_TakeInventory("AMGZoomed",999)
		TNT1 A 0 A_ZoomFactor(1.0)
		AMGG OP 3
		AMGG K 5
		AMGG L 3
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",0,0,0,0)
		AMGG M 2 A_PlaySound("AMG/ClipOut", CHAN_Weapon)
		AMGG N 3
		AMGG C 12
		TNT1 A 0 A_TakeInventory("AmmoActionMachinegun",50)
		Goto InsertBullets
	NoAmmo:
		TNT1 A 0 A_PlaySound("Weapons/empty")
		Goto Ready2
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("AmmoActionMachinegun",50,"LoadGun")
		TNT1 A 0 A_GiveInventory("AmmoActionMachinegun",50)
		TNT1 A 0 A_TakeInventory("Clip2",1)
		Goto InsertBullets
	LoadGun:
		TNT1 A 0
		AMGG DE 3
		AMGG F 2 A_PlaySound("AMG/ClipIn", CHAN_Weapon)
		AMGG H 5
		AMGG IJ 3
		Goto Ready2
	Altfire:
		TNT1 A 0 A_JumpIfInventory("AMGMode2",1,4)
		TNT1 A 0 A_GiveInventory("AMGMode2",1)
		TNT1 A 0 A_PlaySound("Weapons/Mode",CHAN_Weapon,1.5)
		TNT1 A 0 A_Print ("- Semiautomatic mode -")
		Goto Ready2
		TNT1 A 0 A_TakeInventory("AMGMode2",1)
		TNT1 A 0 A_PlaySound("Weapons/Mode",CHAN_Weapon,1.5)
		TNT1 A 0 A_Print ("- Automatic mode -")
		Goto Ready2
	Zoom:
		TNT1 A 0 A_JumpIfInventory("PressingZoom", 1, "Ready2")
		TNT1 A 0 A_GiveInventory("PressingZoom", 1)
		TNT1 A 0 A_JumpIfInventory("AMGZoomed",2,"Unzoom")
		TNT1 A 0 A_JumpIfInventory("AMGZoomed",1,"Zoom2")
		TNT1 A 0 ACS_NamedExecute("PlayerSpeed", 0, 70)
		TNT1 A 0 A_GiveInventory("AMGZoomed",1)
		TNT1 A 0 A_ZoomFactor(1.8)
		TNT1 A 0 A_SetCrosshair(99)
		TNT1 A 0 A_PlaySound("Weapons/Zoom", CHAN_Weapon)
		SNPS A 10 A_SetBlend("00 00 00", 1.0, 20)
		SNPS AAAAAAAAAA 1
		Goto ZReady
	Zoom2:
		TNT1 A 0 A_GiveInventory("AMGZoomed",1)
		TNT1 A 0 A_PlaySound("Weapons/Zoom", CHAN_Weapon)
		TNT1 A 0 A_ZoomFactor(2.4)
		SNPS A 10
		SNPS AAAAAAAAAA 1
		Goto ZReady
	Unzoom:
		TNT1 A 0 ACS_NamedExecute("PlayerSpeed", 0, 100)
		TNT1 A 0 A_TakeInventory("AMGZoomed",999)
		TNT1 A 0 A_PlaySound("Weapons/Zoom", CHAN_Weapon)
		TNT1 A 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
		TNT1 A 0 A_SetCrosshair(0)
		AMGG A 10 A_SetBlend("00 00 00", 1.0, 20)
		AMGG A 10
		Goto Ready2
	Flash:
		NONE A 5 Bright A_Light1
		Goto LightDone
		NONE B 5 Bright A_Light1
		Goto LightDone
	Spawn:
		AGUN A -1
		Stop
	}
}
Actor AMGMode2 : Boolean { }
Actor AMGZoomed : Counter { Inventory.MaxAmount 2 }
Actor AmmoActionMachinegun : AMMO
{
	Inventory.MaxAmount 50
	Ammo.BackpackMaxAmount 50
	+IGNORESKILL
}