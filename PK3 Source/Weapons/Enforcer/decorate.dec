ACTOR EnforcerFire2 : Boolean { }
ACTOR EnforcerUpgrade : Boolean { }
ACTOR Enforcer : FDWeapon
{
	Tag "$TAGENFORCER"
	Inventory.PickupMessage "$GOTENFORCER"
	Obituary "$OBENFORCER"
	Inventory.RestrictedTo MarineClass
	Weapon.AmmoType1 "EnforcerClipR"
	Weapon.AmmoType2 "EnforcerClipL"
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 0
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	Weapon.Kickback 75
	Weapon.UpSound "weapons/enforcerup"
	// AttackSound "weapons/enforcerfire"
	+AMMO_OPTIONAL
	+NOALERT
	+NOAUTOFIRE
	+UNDROPPABLE
	States
	{
	Ready:
		ENFS EDCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		ENF1 A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(0,"DeselectDEAD")
		ENFS ABCDE 1
		ENF1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("EnforcerFire2",1,"Fire2")
		TNT1 A 0 A_JumpIfInventory("EnforcerClipR",1,1)
		Goto DryFire
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("PistolTracer", frandom(-2.0,3.0), 1, 10, 1, 0, frandom(-3.0,3.0))
		TNT1 A 0 A_PlaySound("weapons/enforcerfire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawn2",5,0,20,-4)
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		TNT1 A 0 A_SetPitch(pitch - 0.25)
		TNT1 A 0 A_TakeInventory("EnforcerClip",1)
		TNT1 A 0 A_TakeInventory("EnforcerClipR",1)
		TNT1 A 0 A_GiveInventory("EnforcerFire2",1)
		ENF1 BB 1 BRIGHT A_SetPitch(pitch - 0.125)
		ENF1 C 3 BRIGHT
		TNT1 A 0 A_JumpIfInventory("EnforcerUpgrade",1,1)
		Goto Ready2
		TNT1 A 0 A_Refire
		Goto Ready2
	Fire2:
		TNT1 A 0 A_JumpIfInventory("EnforcerClipL",1,1)
		Goto DryFire
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("PistolTracer", frandom(-2.0,3.0), 1, -10, 1, 0, frandom(-3.0,3.0))
		TNT1 A 0 A_PlaySound("weapons/enforcerfire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawn2",5,0,-11,-4)
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		TNT1 A 0 A_SetPitch(pitch - 0.25)
		TNT1 A 0 A_TakeInventory("EnforcerClip",1)
		TNT1 A 0 A_TakeInventory("EnforcerClipL",1)
		TNT1 A 0 A_TakeInventory("EnforcerFire2",1)
		ENF1 DD 1 BRIGHT A_SetPitch(pitch - 0.125)
		ENF1 E 3 BRIGHT
		TNT1 A 0 A_JumpIfInventory("EnforcerUpgrade",1,1)
		Goto Ready2
		TNT1 A 0 A_Refire("Fire")
		Goto Ready2
	Flash:
       TNT1 A 2 A_Light2
       TNT1 A 1 A_Light1
       TNT1 A 0 A_Light0
       Stop
	DryFire:
		ENF1 A 0 A_PlaySound("weapons/empty")
		ENF1 A 0 A_JumpIfInventory("EnableAutoReload",1,"Reload")
		Goto Ready2
	Reload:
		ENFR A 0 A_TakeInventory("Reloading",1)
		ENFR A 0 A_JumpIfInventory("Clip2", 2, 1)
		Goto Ready2
		ENFR A 0 A_JumpIfInventory("EnforcerClip", 30, "Ready2")
		TNT1 A 0 A_PlaySound("*reload",CHAN_VOICE)
		ENFR A 0 A_PlaySound("weapons/enforcerout")
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",0,0,15,0)
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",0,0,-15,0)
		ENFR ABCDEFG 2
		ENFR A 0 A_TakeInventory("EnforcerClip",30)
		ENFR A 0 A_TakeInventory("EnforcerClipR",15)
		ENFR A 0 A_TakeInventory("EnforcerClipL",15)
		ENFR F 0 A_PlaySound("weapons/enforcerin")
		ENFR HIJKL 2
		Goto Reloading
	Reloading:
		TNT1 A 0 A_JumpIfInventory("EnforcerClip",30,"Ready2")
		TNT1 A 0 A_TakeInventory("Clip2",2)
		TNT1 A 0 A_GiveInventory("EnforcerClip",30)
		TNT1 A 0 A_GiveInventory("EnforcerClipR",15)
		TNT1 A 0 A_GiveInventory("EnforcerClipL",15)
		Goto Reloading
   }
}
ACTOR EnforcerClip : Ammo
{
	Inventory.MaxAmount 30
	Ammo.BackpackMaxAmount 30
	+IGNORESKILL
}
ACTOR EnforcerClipR : Ammo
{
	Inventory.MaxAmount 15
	Ammo.BackpackMaxAmount 15
	+IGNORESKILL
}
ACTOR EnforcerClipL : Ammo
{
	Inventory.MaxAmount 15
	Ammo.BackpackMaxAmount 15
	+IGNORESKILL
}