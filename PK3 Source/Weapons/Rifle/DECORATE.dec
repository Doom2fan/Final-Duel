actor RifleZoomed : Boolean { }
actor RifleBurst  : Boolean { }
actor RifleUpgrade : Boolean { }
actor Rifle : FDWeapon 30033
{
	//$Category Final Duel-Weapons
	//$Sprite RIFLZ0
	//$Title M9A10 Rifle
	Tag "$TAGRIFLE"
	Inventory.PickupSound "misc/gunpickup"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoType "AmmoRifle"
	Weapon.AmmoType2 "Clip2"
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$GOTRIFLE"
	Obituary "$OBRIFLE"
	+AMMO_OPTIONAL
	+noalert
	States
	{
	Ready:
		RFLS DCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory("RifleZoomed",1,"ZReady")
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		RIFL A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	ZReady:
		TNT1 A 0 A_JumpIfInventory("RifleZoomed",1,1)
		Goto Ready2
		TNT1 A 0 A_ZoomFactor(1.6)
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		RIFS A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(0,"DeselectDEAD")
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("RifleZoomed",1)
		RFLS ABCD 1
		RIFL A 0 A_Lower
		Wait
	Select:
		RIFL A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("RifleZoomed",1,"ZFire")
		TNT1 A 0 A_JumpIfInventory("RifleBurst",1,"Fire.Burst")
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_FireCustomMissile("RifleTracer", frandom(-2.0,2.0), 1, 4, -2, 0, frandom(-2.5,2.5))
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_PlaySoundEx("Rifle/Fire","Weapon")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		RIFF AA 1 Bright A_SetPitch(pitch - 0.20)
		RIFF B 2 
		RIFF A 0 A_ReFire
		Goto Ready2
	ZFire:
		TNT1 A 0 A_JumpIfInventory("RifleBurst",1,"ZFire.Burst")
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_FireCustomMissile("RifleTracer", frandom(-1.7,1.7), 1, 0, 0, 0, frandom(-2.0,2.0))
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_PlaySoundEx("Rifle/Fire","Weapon")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		RIFS BB 1 Bright A_SetPitch(pitch - 0.20)
		RIFS C 2
		RIFS A 0 A_ReFire
		Goto Ready2
	Fire.Burst:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_FireCustomMissile("RifleTracer", frandom(-2.5,2.5), 1, 4, -2, 0, frandom(-3.0,3.0))
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_PlaySoundEx("Rifle/Fire","Weapon")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		RIFF A 1 Bright A_SetPitch(pitch - 0.20)
		RIFF B 2 A_SetPitch(pitch - 0.20)
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_FireCustomMissile("RifleTracer", frandom(-2.5,2.5), 1, 4, -2, 0, frandom(-3.0,3.0))
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_PlaySoundEx("Rifle/Fire","Weapon")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		RIFF A 1 Bright A_SetPitch(pitch - 0.20)
		RIFF B 2 A_SetPitch(pitch - 0.20)
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_FireCustomMissile("RifleTracer", frandom(-2.5,2.5), 1, 4, -2, 0, frandom(-3.0,3.0))
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_PlaySoundEx("Rifle/Fire","Weapon")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		RIFF A 1 Bright A_SetPitch(pitch - 0.20)
		RIFF B 2
		Goto Ready2
	ZFire.Burst:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_FireCustomMissile("RifleTracer", frandom(-2.2,2.2), 1, 0, 0, 0, frandom(-2.8,2.8))
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_PlaySoundEx("Rifle/Fire","Weapon")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		RIFS B 1 Bright A_SetPitch(pitch - 0.20)
		RIFS C 2 A_SetPitch(pitch - 0.20)
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_FireCustomMissile("RifleTracer", frandom(-2.2,2.2), 1, 0, 0, 0, frandom(-2.8,2.8))
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_PlaySoundEx("Rifle/Fire","Weapon")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		RIFS B 1 Bright A_SetPitch(pitch - 0.20)
		RIFS C 2 A_SetPitch(pitch - 0.20)
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_FireCustomMissile("RifleTracer", frandom(-2.2,2.2), 1, 0, 0, 0, frandom(-2.8,2.8))
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-9)
		TNT1 A 0 A_PlaySoundEx("Rifle/Fire","Weapon")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 0.1)
		RIFS B 1 Bright A_SetPitch(pitch - 0.20)
		RIFS C 2
		Goto Ready2
	Zoom:
		TNT1 A 0 A_JumpIfInventory("RifleUpgrade",1,1)
		Goto Ready2
		TNT1 A 0 A_JumpIfInventory("RifleZoomed",1,"Unzoom")
		TNT1 A 0 A_GiveInventory("RifleZoomed",1)
		TNT1 A 0 A_ZoomFactor(1.6)
		RIFS A 10
		Goto ZReady
	Unzoom:
		TNT1 A 0 A_TakeInventory("RifleZoomed",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		RIFS A 10
		Goto Ready2
	AltFire:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("RifleBurst",1,4)
		TNT1 A 0 A_GiveInventory("RifleBurst",1)
		TNT1 A 0 A_Print("- Burstfire mode -")
		TNT1 A 0 A_PlaySound("Weapons/Mode",CHAN_WEAPON,1.5)
		Goto Ready2
		TNT1 A 0 A_TakeInventory("RifleBurst",1)
		TNT1 A 0 A_Print("- Automatic mode -")
		TNT1 A 0 A_PlaySound("Weapons/Mode",CHAN_WEAPON,1.5)
		Goto Ready2
	DryFire:
		RIFL A 0 A_PlaySound("weapons/empty")
		TNT1 A 0 A_JumpIfInventory("EnableAutoReload",1,"Reload")
		Goto Ready2
    Reload:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("RifleZoomed",1)
		TNT1 A 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("AmmoRifle",30,"Ready2")
        TNT1 A 0 A_JumpIfInventory("Clip2",1,1)
        Goto NoAmmo
		TNT1 A 0 A_PlaySound("*reload",CHAN_VOICE)
		TNT1 A 0 A_TakeInventory("Clip2",1)
		TNT1 A 0 A_TakeInventory("AmmoRifle",30)
		RIFL BC 4
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",0,0,0,0)
		TNT1 A 0 A_PlaySound("Rifle/Out",CHAN_WEAPON)
		RIFL DEF 4
		TNT1 A 0 A_PlaySound("Rifle/In",CHAN_WEAPON)
		RIFL GHI 4
		TNT1 A 0 A_GiveInventory("AmmoRifle",30)
		Goto Ready2
	NoAmmo:
		TNT1 A 0 A_PlaySound("weapons/empty")
		Goto Ready2
	Flash:
		NONE A 5 Bright A_Light1
		Goto LightDone
		NONE B 5 Bright A_Light1
		Goto LightDone
	Spawn:
		RIFL Z -1
		Stop
	}
}
ACTOR AmmoRifle : AMMO
{
	Inventory.MaxAmount 30
	Ammo.BackpackMaxAmount 30
	+IGNORESKILL
}