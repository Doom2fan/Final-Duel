Actor GrenadeLauncherExplode : Boolean { }
Actor AmmoGrenadeLauncher : AMMO
{
	Inventory.MaxAmount 6
	Ammo.BackpackMaxAmount 6
	+IGNORESKILL
}
Actor DroppedGrenadeLauncher : DroppedWeaponBase
{
	States
	{
	Spawn:
		GLPK A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("FDGrenadeLauncher")
		Stop
	}
}
Actor FDGrenadeLauncher : FDWeapon 30059
{
	//$Category Final Duel-Weapons
	//$Title GT Grenade Launcher
	//$Sprite GLPKA0
	Tag "$TAGGL"
	Inventory.PickupMessage "$GOTGLAUNCHER"
	Weapon.AmmoType "AmmoGrenadeLauncher"
	Weapon.AmmoType2 "GrenadeAmmo"
	Weapon.AmmoGive1 0
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 0
	+AMMO_OPTIONAL
	+NOAUTOFIRE
	+NOALERT
	States
	{
	Ready:
		GLGR FEDCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory("DropDatWeapon",1,"DropWeapon")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		GLG1 A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile("DroppedGrenadeLauncher", 0, 0, 0)
		TNT1 A 0 A_TakeInventory("FDGrenadeLauncher", 1)
		Stop
	Select:
		GLG1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		GLGR ABCDEF 1
		GLG1 A 0 A_Lower
		Wait
	Reload:
		TNT1 A 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("AmmoGrenadeLauncher",6,"Ready2")
        TNT1 A 0 A_JumpIfInventory("GrenadeAmmo",1,1)
        Goto Ready2
		GLGR ABCDEF 1
		GLGR F 8
		TNT1 A 0 A_FireCustomMissile("GrenadeMagSpawn",0,0,0,0)
		GLGR F 17 A_PlaySound("GrenadeLauncher/DrumOut", CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("AmmoGrenadeLauncher",6)
		TNT1 A 0 A_GiveInventory("AmmoGrenadeLauncher",6)
		TNT1 A 0 A_TakeInventory("GrenadeAmmo",1)
		GLGR F 17
		TNT1 A 0 A_PlaySound("GrenadeLauncher/DrumIn", CHAN_WEAPON)
		GLGR F 17
		GLGR FEDCBA 1
		Goto Ready2
	Fire:
		TNT1 A 0 A_JumpIfInventory("GrenadeLauncherExplode",1,"Fire.Explode")
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_PlaySound("GrenadeLauncher/Fire", CHAN_WEAPON)
		TNT1 A 0 A_Recoil(cos(pitch) * 2.5)
		GLGF A 3 Bright A_FireCustomMissile("FDGrenade",0,1,0,0,0,6)
		GLGF B 3 Bright A_AlertMonsters
		GLG1 ABCDA 3
		TNT1 A 0 A_Refire
		Goto Ready2
	Fire.Explode:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_PlaySound("GrenadeLauncher/Fire", CHAN_WEAPON)
		TNT1 A 0 ThrustThing(angle*256/360+128, cos(pitch) * 5, 0, 0)
		TNT1 A 0 A_Recoil(cos(pitch) * 2.5)
		GLGF A 3 A_FireCustomMissile("FDGrenade2",0,1,0,0,0,6)
		TNT1 A 0 A_AlertMonsters
		GLGF B 3
		GLG1 ABCDA 3
		TNT1 A 0 A_Refire
		Goto Ready2
	Altfire:
		TNT1 A 0 A_JumpIfInventory("GrenadeLauncherExplode",1,5)
		TNT1 A 0 A_GiveInventory("GrenadeLauncherExplode",1)
		TNT1 A 0 A_PlaySound("Weapons/Mode",CHAN_WEAPON,1.5)
		TNT1 A 0 A_Print ("- Explode on impact -")
		GLG1 A 6
		Goto Ready2
		TNT1 A 0 A_TakeInventory("GrenadeLauncherExplode",1)
		TNT1 A 0 A_PlaySound("Weapons/Mode",CHAN_WEAPON,1.5)
		TNT1 A 0 A_Print ("- Bounce -")
		GLG1 A 6
		Goto Ready2
	DryFire:
		GLG1 A 0 A_PlaySound("Weapons/empty", 7)
		TNT1 A 0 A_JumpIfInventory("EnableAutoReload",1,"Reload")
		Goto Ready2
	Spawn:
		GLPK A -1
		Stop
	}
}
Actor FDGrenade
{
	Game Doom
	Radius 11
	Height 8
	Speed 23
	Damage 20
	Projectile
	-RocketTrail
	-NoGravity
	+Randomize
	+QuarterGravity
	deathsound "GrenadeLauncher/Explode"
	Obituary "$OBGRENADE"
	DamageType Grenade
	// Bouncing
	BounceType "None"
	BounceCount 8
	BounceFactor 0.7
	WallBounceFactor 0.75
	+BounceOnWalls
	+BounceOnFloors
	+BounceOnCeilings
	+CanBounceWater
	+MBFBouncer
	+AllowBounceOnActors
	BounceSound "GrenadeLauncher/Bounce"
	// Low momentum death
	// +BOUNCEAUTOOFF
	// -MBFBOUNCER
	States
	{
	Spawn:
		GRND A 2 Light(ROCKET)
		Loop
	Death:
		TNT1 A 0 A_ChangeFlag("DontSplash", 1)
	    TNT1 A 0 A_ChangeFlag("ALLOWBOUNCEONActorS",0)
		TNT1 A 0 A_ChangeFlag("MBFBOUNCER",0)
		TNT1 A 0 A_ChangeFlag("BOUNCEONWALLS",0)
		TNT1 A 0 A_ChangeFlag("BOUNCEONCEILINGS",0)
		TNT1 A 0 A_ChangeFlag("BOUNCEONFLOORS",0)
		TNT1 A 0 A_ChangeFlag("CANBOUNCEWATER",0)
		TNT1 A 0 A_ChangeFlag("NoGravity",1)
		TNT1 A 0 A_ChangeFlag("QUARTERGRAVITY",0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_ChangeFlag("ForceXYBillboard",1)
		TNT1 AAAAAAAAAAAA 0 A_CustomMissile("FDShrapnel",random(0,16),random(-8,8),random(-360,360),2,random(10,70))
		XPL3 G 2 Bright Light(ROCKET_X2) A_Explode
		XPL3 HIJ 2 Bright Light(ROCKET_X2)
		XPL3 KLMNO 2 Bright Light(ROCKET_X1)
		XPL3 PQRSTUVW 2 Bright
		stop
	}
}
Actor FDGrenade2 : FDGrenade
{
	BounceType "None"
	BounceCount 0
	BounceFactor 0.0
	WallBounceFactor 0.0
	+DontSplash
	-BounceOnWalls
	-BounceOnFloors
	-BounceOnCeilings
	-CanBounceWater
	-MBFBouncer
	-AllowBounceOnActors
}