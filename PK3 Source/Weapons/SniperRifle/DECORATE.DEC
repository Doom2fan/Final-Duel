Actor SpawnedSniperRifle : BaseSpawnedWeapon 30091 {
	Inventory.PickupMessage "$GOTSNIPERRIFLE"
	States {
	Spawn:
		SNPP A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory ("SniperRifle", 0, "GrabAmmo")
		TNT1 A 0 A_TakeInventory ("AmmoSniperRifle", 999999999)
		TNT1 A 0 A_GiveInventory ("SniperRifle", 1)
		TNT1 A 0 A_GiveInventory ("AmmoSniperRifle", 999999999)
		Stop
	GrabAmmo:
		TNT1 A 0 A_JumpIfInventory ("SRifleClipPool", 0, "Failed")
		TNT1 A 0 A_Jump (64, "Explosive")
		TNT1 A 0 A_GiveInventory ("SRifleClip", 1)
		Stop
	Explosive:
		TNT1 A 0 A_GiveInventory ("SRifleExpClip", 1)
		Stop
	}
}

Actor SRifleClipPool : Ammo {
	Inventory.MaxAmount 4 // 20
	Ammo.BackpackMaxAmount 6 // 30
}

Actor SRifleClipSpawned : BaseSpawnedWeapon 30092 {
	Inventory.PickupMessage "$GOTSRIFLECLIP1"
	Inventory.PickupSound "misc/ammopickup"
	States {
	Spawn:
		SNPP B -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory ("SRifleClipPool", 0, "Failed")
		TNT1 A 0 A_GiveInventory ("SRifleClip", 1)
		TNT1 A 0 A_GiveInventory ("SRifleClipPool", 1)
		Stop
	}
}

Actor SRifleClip : Counter {
	Inventory.Icon "SNPPB0"
	//Inventory.MaxAmount 5
	+IgnoreSkill
}

Actor SRifleExpClipSpawned : BaseSpawnedWeapon 30172 {
	Inventory.PickupMessage "$GOTSRIFLECLIP2"
	Inventory.PickupSound "misc/ammopickup"
	States {
	Spawn:
		SNPP C -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory ("SRifleClipPool", 0, "Failed")
		TNT1 A 0 A_GiveInventory ("SRifleExpClip", 1)
		TNT1 A 0 A_GiveInventory ("SRifleClipPool", 1)
		Stop
	}
}

Actor SRifleExpClip : Counter {
	Inventory.Icon "SNPPC0"
	Inventory.MaxAmount 5
	+IgnoreSkill
}

Actor AmmoSniperRifle : Ammo {
	Inventory.MaxAmount 5
	Ammo.BackpackMaxAmount 5
	+IgnoreSkill
}

Actor DroppedSniperRifle : DroppedWeaponBase {
	States {
	Spawn:
		SNPP A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx ("SniperRifle")
		Stop
	}
}

Actor SniperRifleZoomed : Counter { Inventory.MaxAmount 4 }
Actor SniperRifleExplosive : Boolean { }

Actor SniperRifle : FDWeapon {
	//$Category Final Duel-Weapons
	//$Title Magnum Sniper Rifle
	//$Sprite SNPPA0
	Tag "$TAGSNIPERRIFLE"
	Inventory.PickupSound "misc/gunpickup"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType "AmmoSniperRifle"
	Weapon.AmmoType2 "SRifleClipPool"
	+Weapon.NoAlert +Weapon.NoAutoFire +Weapon.Ammo_Optional
	Inventory.PickupMessage "$GOTSNIPERRIFLE"
	Obituary "$OBSNIPERRIFLE"
	States {
	Spawn:
		SNPP A -1
		Stop
	Ready:
		SNPD CBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 1, "ZReady")
		TNT1 A 0 A_ZoomFactor (1.0)
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		SNPG AA 1 A_WeaponReady (WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory ("PressingZoom", 1)
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedSniperRifle", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("SniperRifle", 1)
		Stop
	Select:
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		SNPD A 0 A_JumpIfInventory ("SniperRifleZoomed", 1, "Deselect.Unzoom")
		SNPD ABC 1
		TNT1 A 0 A_Lower
		Wait
	Deselect.Unzoom:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 100)
		TNT1 A 0 A_TakeInventory ("SniperRifleZoomed", 999)
		TNT1 A 0 A_PlaySound ("Weapons/Zoom", CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor (1.0, ZOOM_INSTANT)
		TNT1 A 0 A_SetCrosshair (0)
		SNPG A 10 A_SetBlend ("00 00 00", 1.0, 20)
		SNPG A 10
		Goto Deselect
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 4, "ZFire4")
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 3, "ZFire3")
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 2, "ZFire2")
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 1, "ZFire")
		TNT1 A 0 A_JumpIfInventory ("SniperRifleExplosive", 1, "Fire.Explosive")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile ("MagnumTracer", frandom (-1.0, 1.0), 1, 0, -2, 0, frandom (-1.3, 1.3))
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_PlaySound ("SniperRifle/Fire", CHAN_WEAPON)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.8, 0, Sin (Pitch) * 0.8, 1)
		SNPG BB 1 Bright A_SetPitch (pitch - 0.25, SPF_INTERPOLATE)
		TNT1 A 0 A_SetPitch (pitch - 0.8, SPF_INTERPOLATE)
		Goto Fire.Finish
	Fire.Explosive:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile ("ExplosiveMagnumTracer", frandom (-1.0, 1.0), 1, 0, -2, 0, frandom (-1.3, 1.3))
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, -8, -9)
		TNT1 A 0 A_PlaySound ("SniperRifle/Fire", CHAN_WEAPON)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -1.6, 0, Sin (Pitch) * 1.6, 1)
		SNPG BB 1 Bright A_SetPitch (pitch - 0.9, SPF_INTERPOLATE)
		TNT1 A 0 A_SetPitch (pitch - 0.9, SPF_INTERPOLATE)
		Goto Fire.Finish
	Fire.Finish:
		SNPG CDAAAA 1
		SNPG A 3
		SNPG E 3 A_PlaySound ("SniperRifle/Bolt1", 5)
		SNPG FGHHH 3
		SNPG H 1 A_PlaySound ("SniperRifle/Bolt2", 5)
		SNPG GFEA 3
		SNPG A 3
		Goto Ready2
	
	Reload:
		TNT1 A 0 A_JumpIfInventory ("AmmoSniperRifle", 5, "CancelReload")
		TNT1 A 0 A_JumpIfInventory ("SRifleClip", 1, 1)
		Goto CancelReload
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 1, "Reload.Unzoom")
		SNPG AAAAA 1 A_WeaponReady (WRF_NOFIRE | WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_TakeInventory ("SniperRifleExplosive", 1)
		Goto ReloadAnimation
	ReloadExplosive:
		TNT1 A 0 A_JumpIfInventory ("SRifleClip", 1, 1)
		Goto CancelReload
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 1, "Reload.Unzoom")
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_GiveInventory ("SniperRifleExplosive", 1)
		Goto ReloadAnimation
	
	ReloadAnimation:
		
		TNT1 A 0 A_JumpIfInventory ("SniperRifleExplosive", 1, "Reload2Explosive")
		Goto Reload2
		
	Reload2:
		TNT1 A 0 A_TakeInventory ("SRifleClip", 1)
		TNT1 A 0 A_TakeInventory ("SRifleClipPool", 1)
		TNT1 A 0 A_GiveInventory ("AmmoSniperRifle", 5)
		Goto Ready2
	Reload2Explosive:
		TNT1 A 0 A_TakeInventory ("SRifleExpClip", 1)
		TNT1 A 0 A_TakeInventory ("SRifleClipPool", 1)
		TNT1 A 0 A_GiveInventory ("AmmoSniperRifle", 5)
		Goto Ready2
	
	CancelReload:
		TNT1 A 0 A_JumpIfInventory ("SRifleExpClip", 1, "ReloadExplosive")
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		Goto Ready2
	
	Reload.Unzoom:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 100)
		TNT1 A 0 A_TakeInventory ("SniperRifleZoomed", 999)
		TNT1 A 0 A_PlaySound ("Weapons/Zoom", CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor (1.0, ZOOM_INSTANT)
		TNT1 A 0 A_SetCrosshair (0)
		SNPG A 10 A_SetBlend ("00 00 00", 1.0, 20)
		SNPG A 10
		Goto Reload
	DryFire:
		TNT1 A 0 A_PlaySound ("Weapons/empty", 7)
		TNT1 A 0 A_JumpIfInventory ("EnableAutoReload", 1, "DryReload")
		Goto Ready2
	DryReload:
		TNT1 A 0 A_JumpIf (CallACS ("FD_SniperRiflePriority") >= 1, "ReloadExplosive")
		Goto Reload
		// Zoom
	Zoom:
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "ReloadExplosive")
		TNT1 A 0 A_JumpIfInventory ("PressingZoom", 1, "Ready2")
		TNT1 A 0 A_GiveInventory ("PressingZoom", 1)
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 4, "Unzoom")
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 3, "Zoom4")
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 2, "Zoom3")
		TNT1 A 0 A_JumpIfInventory ("SniperRifleZoomed", 1, "Zoom2")
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 65)
		TNT1 A 0 A_GiveInventory ("SniperRifleZoomed", 1)
		TNT1 A 0 A_ZoomFactor (3.0)
		TNT1 A 0 A_SetCrosshair (99)
		TNT1 A 0 A_PlaySound ("Weapons/Zoom", 5)
		SNPS C 10 A_SetBlend ("00 00 00", 1.0, 20)
		SNPS CCCCCCCCCC 1
		Goto ZReady
	Zoom2:
		TNT1 A 0 A_GiveInventory ("SniperRifleZoomed", 1)
		TNT1 A 0 A_ZoomFactor (5.0)
		TNT1 A 0 A_SetCrosshair (99)
		TNT1 A 0 A_PlaySound ("Weapons/Zoom", 5)
		SNPS C 10
		SNPS CCCCCCCCCC 1
		Goto ZReady
	Zoom3:
		TNT1 A 0 A_GiveInventory ("SniperRifleZoomed", 1)
		TNT1 A 0 A_ZoomFactor (7.0)
		TNT1 A 0 A_SetCrosshair (99)
		TNT1 A 0 A_PlaySound ("Weapons/Zoom", 5)
		SNPS C 10
		SNPS CCCCCCCCCC 1
		Goto ZReady
	Zoom4:
		TNT1 A 0 A_GiveInventory ("SniperRifleZoomed", 1)
		TNT1 A 0 A_ZoomFactor (10.0)
		TNT1 A 0 A_SetCrosshair (99)
		TNT1 A 0 A_PlaySound ("Weapons/Zoom", 5)
		SNPS C 10
		SNPS CCCCCCCCCC 1
		Goto ZReady
	Unzoom:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 100)
		TNT1 A 0 A_TakeInventory ("SniperRifleZoomed", 999)
		TNT1 A 0 A_PlaySound ("Weapons/Zoom", 5)
		TNT1 A 0 A_ZoomFactor (1.0, ZOOM_INSTANT)
		TNT1 A 0 A_SetCrosshair (0)
		SNPG A 10 A_SetBlend ("00 00 00", 1.0, 20)
		SNPG A 10
		Goto Ready2
	ZReady:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "Unzoom")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_TakeInventory ("Kicking", 1)
		TNT1 A 0 A_TakeInventory ("Taunting", 1)
		SNPS CC 1 A_WeaponReady (WRF_ALLOWZOOM|WRF_NOBOB)
		TNT1 A 0 A_TakeInventory ("PressingZoom", 1)
		Loop
	
	ZFire:
		TNT1 A 0 A_JumpIfInventory ("SniperRifleExplosive", 1, "ZFire.Explosive")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile ("MagnumTracer", frandom (-0.5, 0.5), 1, 0, -3, 0, frandom (-0.5, 0.5))
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_PlaySound ("SniperRifle/Fire", CHAN_WEAPON)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.8, 0, Sin (Pitch) * 0.8, 1)
		SNPS CC 1 Bright A_SetPitch (pitch - 0.525, SPF_INTERPOLATE)
		Goto ZFire.Finish
	ZFire2:
		TNT1 A 0 A_JumpIfInventory ("SniperRifleExplosive", 1, "ZFire2.Explosive")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile ("MagnumTracer", frandom (-0.3, 0.3), 1, 0, -3, 0, frandom (-0.3, 0.3))
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_PlaySound ("SniperRifle/Fire", CHAN_WEAPON)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.6, 0, Sin (Pitch) * 0.6, 1)
		SNPS CC 1 Bright A_SetPitch (pitch - 0.4215, SPF_INTERPOLATE)
		Goto ZFire.Finish
	ZFire3:
		TNT1 A 0 A_JumpIfInventory ("SniperRifleExplosive", 1, "ZFire3.Explosive")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile ("MagnumTracer", frandom (-0.1, 0.1), 1, 0, -3, 0, frandom (-0.1, 0.1))
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_PlaySound ("SniperRifle/Fire", CHAN_WEAPON)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.5, 0, Sin (Pitch) * 0.5, 1)
		SNPS CC 1 Bright A_SetPitch (pitch - 0.44, SPF_INTERPOLATE)
		Goto ZFire.Finish
	ZFire4:
		TNT1 A 0 A_JumpIfInventory ("SniperRifleExplosive", 1, "ZFire4.Explosive")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile ("MagnumTracer", frandom (-0.0, 0.0), 1, 0, -3, 0, frandom (-0.0, 0.0))
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_PlaySound ("SniperRifle/Fire", CHAN_WEAPON)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.4, 0, Sin (Pitch) * 0.4, 1)
		SNPS CC 1 Bright A_SetPitch (pitch - 0.15, SPF_INTERPOLATE)
		SNPS CCCC 1 A_SetPitch (pitch + 0.04175, SPF_INTERPOLATE)
		Goto ZFire.Finish
	ZFire.Explosive:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile ("ExplosiveMagnumTracer", frandom (-0.5, 0.5), 1, 0, -3, 0, frandom (-0.5, 0.5))
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_PlaySound ("SniperRifle/Fire", CHAN_WEAPON)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.8, 0, Sin (Pitch) * 0.8, 1)
		SNPS CC 1 Bright A_SetPitch (pitch - 1.05, SPF_INTERPOLATE)
		Goto ZFire.Finish
	ZFire2.Explosive:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile ("ExplosiveMagnumTracer", frandom (-0.3, 0.3), 1, 0, -3, 0, frandom (-0.3, 0.3))
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_PlaySound ("SniperRifle/Fire", CHAN_WEAPON)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.6, 0, Sin (Pitch) * 0.6, 1)
		SNPS CC 1 Bright A_SetPitch (pitch - 0.843, SPF_INTERPOLATE)
		Goto ZFire.Finish
	ZFire3.Explosive:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile ("ExplosiveMagnumTracer", frandom (-0.1, 0.1), 1, 0, -3, 0, frandom (-0.1, 0.1))
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_PlaySound ("SniperRifle/Fire", CHAN_WEAPON)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.5, 0, Sin (Pitch) * 0.5, 1)
		SNPS CC 1 Bright A_SetPitch (pitch - 0.88, SPF_INTERPOLATE)
		Goto ZFire.Finish
	ZFire4.Explosive:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile ("ExplosiveMagnumTracer", frandom (-0.0, 0.0), 1, 0, -3, 0, frandom (-0.0, 0.0))
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_PlaySound ("SniperRifle/Fire", CHAN_WEAPON)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.4, 0, Sin (Pitch) * 0.4, 1)
		SNPS CC 1 Bright A_SetPitch (pitch - 0.3, SPF_INTERPOLATE)
		SNPS CCCC 1 A_SetPitch (pitch + 0.020875, SPF_INTERPOLATE)
		Goto ZFire.Finish
	ZFire.Finish:
		SNPS CCCCCC 1
		SNPS C 3
		SNPS C 3 A_PlaySound ("SniperRifle/Bolt1", 5)
		SNPS CCCCC 3
		SNPS C 1 A_PlaySound ("SniperRifle/Bolt2", 5)
		SNPS CCCC 3
		SNPS C 3
		Goto Ready2
	}
}