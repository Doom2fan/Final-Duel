Actor PsiEnergy : AMMO {
	Inventory.MaxAmount 100
	Ammo.BackpackMaxAmount 100
	+IgnoreSkill
}

Actor MagicMode : Counter { Inventory.MaxAmount 2 }
Actor MagicUnlock : Counter { Inventory.MaxAmount 2 }

Actor Magic : FDWeapon {
	//$Category Final Duel-Weapons
	Weapon.AmmoUse 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoType "PsiEnergy"
	Tag "$TAGMAGIC"
	Obituary "$OBMAGIC"
	+Weapon.NoAlert +Weapon.Ammo_Optional +Weapon.NoAutoFire
	States {
	Ready:
		TNT1 A 0 A_PlaySound ("Magic/Book", CHAN_WEAPON)
		TNT1 A 1 A_SetBlend ("FF FF FF", 1, 1)
		MAGI LKJIH 2
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		MAGI AAA 1 A_WeaponReady
		TNT1 A 0 A_GiveInventory ("PsiEnergy", 1)
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		MAGI A 5 ACS_NamedExecute ("FD_CannotDropThisWeapon", 0)
		Goto Ready2
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "Super::DeselectDEAD")
		MAGI HIJKL 2
		TNT1 A 0 A_PlaySound ("Magic/Book", CHAN_WEAPON)
		TNT1 A 1 A_SetBlend ("FF FF FF", 1, 1)
		MAGI A 0 A_Lower
		Wait
	Select:
		MAGI A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory ("MagicMode", 2, "Fire.Lava")
		TNT1 A 0 A_JumpIfInventory ("MagicMode", 1, "Fire.Fire")
		TNT1 A 0 A_JumpIfInventory ("PsiEnergy", 5, 1)
		Goto Burnout
		MAGI B 3
		MGCI A 3
		MGCI B 3 Bright
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound ("Magic/IceFire", CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory ("PsiEnergy", 5)
		TNT1 A 0 A_FireCustomMissile ("FreezeBlast")
		MGCI BCB 3 Bright
		MGCI A 3
		MAGI B 3
		Goto Ready2
	Fire.Fire:
		TNT1 A 0 A_JumpIfInventory ("PsiEnergy", 2, 1)
		Goto Burnout
		MGCF ABCD 3 Bright
		TNT1 A 0 A_PlaySound ("Magic/FireFire", CHAN_WEAPON, 1.0, 1)
	Hold.Fire:
		TNT1 A 0 A_JumpIfInventory ("PsiEnergy", 2, 1)
		Goto Burnout.Fire
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory ("PsiEnergy", 2)
		MGCF E 4 Bright A_FireCustomMissile ("MagicFire")
		TNT1 A 0 A_JumpIfInventory ("PsiEnergy", 2, 1)
		Goto Burnout.Fire
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory ("PsiEnergy", 2)
		MGCF F 4 Bright A_FireCustomMissile ("MagicFire")
		TNT1 A 0 A_JumpIfInventory ("PsiEnergy", 2, 1)
		Goto Burnout.Fire
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory ("PsiEnergy", 2)
		MGCF G 4 Bright A_FireCustomMissile ("MagicFire")
		TNT1 A 0 A_JumpIfInventory ("PsiEnergy", 2, 1)
		Goto Burnout.Fire
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory ("PsiEnergy", 2)
		MGCF H 4 Bright A_FireCustomMissile ("MagicFire")
		TNT1 A 0 A_JumpIfInventory ("PsiEnergy", 2, 1)
		Goto Burnout.Fire
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory ("PsiEnergy", 2)
		MGCF I 4 Bright A_FireCustomMissile ("MagicFire")
		TNT1 A 0 A_JumpIfInventory ("PsiEnergy", 2, 1)
		Goto Burnout.Fire
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory ("PsiEnergy", 2)
		MGCF J 4 Bright A_FireCustomMissile ("MagicFire")
		TNT1 A 0 A_Refire ("Hold.Fire")
		TNT1 A 0 A_StopSound (CHAN_WEAPON)
		MGCF EDCBK 2 Bright
		MGCF LM 2
		MAGI B 3
		Goto Ready2
	Fire.Lava:
		TNT1 A 0 A_JumpIfInventory ("PsiEnergy", 20, 1)
		Goto Burnout
		MAGI B 3
		MGCL AB 3
		TNT1 A 0 A_AlertMonsters
		TNT1 B 0 A_PlaySound ("Magic/LavaFire", CHAN_WEAPON)
		MGCL C 2 Bright A_FireCustomMissile ("MagicLava")
		TNT1 A 0 A_TakeInventory ("PsiEnergy", 20)
		MGCL CDEF 2 Bright
		MGCL BA 2
		MAGI B 3
		Goto Ready2
	Burnout.Fire:
		TNT1 A 0 A_StopSound (CHAN_WEAPON)
		TNT1 A 0 A_PlaySound ("Magic/Burnout", CHAN_WEAPON)
		PSIA A 0 A_SetBlend ("FF FF FF", 1, 20)
		TNT1 A 0 A_ThingDamage (10, "PsiBurnout", TDF_PIERCEARMOR|TDF_IGNOREFactor)
		MGCF EDCBK 2 Bright
		MGCF LM 2
		MAGI B 3
		MAGI A 36
		Goto Ready2
	Burnout:
		TNT1 A 0 A_PlaySound ("Magic/Burnout", CHAN_WEAPON)
		PSIA A 0 A_SetBlend ("FF FF FF", 1, 20)
		TNT1 A 0 A_ThingDamage (10, "PsiBurnout", TDF_PIERCEARMOR|TDF_IGNOREFactor)
		MAGI A 36
		Goto Ready2
	Reload:
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("MagicUnlock", 1, 1)
		Goto Ready2
		TNT1 A 0 A_JumpIfInventory ("MagicMode", 2, "ToIce")
		TNT1 A 0 A_JumpIfInventory ("MagicMode", 1, "ToLava")
		TNT1 A 0 A_GiveInventory ("MagicMode", 1)
		TNT1 A 0 A_PlaySound ("Magic/Mode", CHAN_WEAPON)
		MAGI CDEFGH 2
		Goto Ready2
	ToLava:
		TNT1 A 0 A_JumpIfInventory ("MagicUnlock", 2, 1)
		Goto ToIce
		TNT1 A 0 A_GiveInventory ("MagicMode", 1)
		TNT1 A 0 A_PlaySound ("Magic/Mode", CHAN_WEAPON)
		MAGI CDEFGH 2
		Goto Ready2
	ToIce:
		TNT1 A 0 A_TakeInventory ("MagicMode", 9999)
		TNT1 A 0 A_PlaySound ("Magic/Mode", CHAN_WEAPON)
		MAGI CDEFGH 2
		Goto Ready2
	Flash:
		Goto LightDone
	}
}

// Ice
Actor FreezeBlast {
	Radius 13
	Height 8
	Speed 20
	Damage 5
	Projectile
	+Randomize +DontSplash
	RenderStyle Add
	Alpha 0.75
	Scale 0.9
	DeathSound "Magic/IceHit"
	Obituary "$OBMAGIC"
	DamageType Ice
	Decal Scorch
	States {
	Spawn:
		FBLS A 6 Bright NoDelay A_SpawnItem ("FreezeBlastTrail")
		Loop
	Death:
		FBLS BCDE 4 Bright
		stop
	}
}

Actor FreezeBlastTrail {
	Radius 13
	Height 8
	Speed 20
	Damage 0
	Projectile
	+Randomize +DontSplash
	RenderStyle Add
	Alpha 0.75
	SeeSound "Weapons/iceshot"
	DeathSound "Weapons/icehit"
	Obituary "$OBMAGIC"
	States {
	Spawn:
		FRTR ABC 6 Bright NoDelay A_FadeOut (0.2)
		Loop
	Death:
		FRTR ABC 4 Bright
		stop
	}
}

// Lava
Actor MagicLava {
	Radius 13
	Height 8
	Speed 25
	Damage 20
	Mass 25
	Projectile
	-NoGravity
	Obituary "$OBMAGIC"
	DamageType Fire
	Decal Scorch
	Gravity 0.50
	States { // Sprites by Marrub
	Spawn:
		MGLP AABBCCBBAA 1 Bright NoDelay Light (MAGICLAVAGLOWER) A_SpawnItemEx ("BlackSmokeTrail", -5, random (-7, 7), random (-7, 7), 0, 0, 0)
		Loop
	Death:
		TNT1 A 0 A_ChangeFlag ("NoGravity", 1)
		TNT1 A 0 A_Explode
		TNT1 AAAAAAAAAAAA 0 A_CustomMissile ("MagicLavaRocks", 0, 0, random (-360, 360), 2, random (-90, 90))
		MGLE ABCDE 3 Bright Light (MAGICLAVAGLOWER)
		Stop
	}
}

Actor MagicLavaRocks : DebrisGeneral
{
	+DontSplash -NoGravity
	Speed 12
	Gravity 0.7
	Scale 0.1
	States { // sprites by Marrub
	Spawn:
		MGLT ABC 1 Bright NoDelay Light (MAGICLAVAGLOWER2) A_SpawnItem ("SmokingPieceSmoke")
		Loop
	Death:
		MGLE ABCDE 2 Bright Light (MAGICLAVAGLOWER2)
		Stop
	}
}

// Fire
Actor MagicFire {
	Radius 2
	Height 4
	Speed 16
	Damage 1
	Projectile
	RenderStyle Add
	DamageType Fire
	Alpha 0.67
	Scale 0.67
	DeathSound "Flamethrower/Hit"
	States {
	Spawn:
		TNT1 A 0 A_Jump (256, "Spawn2", "SpawnPainless")
	Spawn2:
		TNT1 A 2
		FRFX ABCD 2 Bright Light (MAGICFIRE) A_Explode (5, 8, 0)
		TNT1 A 0 A_LowGravity
		FRFX EFG 2 Bright Light (MAGICFIRE) A_Explode (5, 16, 0)
		FRFX HIJ 2 Bright Light (MAGICFIRE) A_Explode (5, 32, 0)
		FRFX KLM 2 Bright Light (MAGICFIRE) A_Explode (5, 64, 0)
		FRFX NO 2 Bright
		Stop
	SpawnPainless:
		TNT1 A 2 A_ChangeFlag ("PAINLESS", 1)
		FRFX ABCD 2 Bright Light (MAGICFIRE) A_Explode (5, 8, 0)
		TNT1 A 0 A_LowGravity
		FRFX EFG 2 Bright Light (MAGICFIRE) A_Explode (5, 16, 0)
		FRFX HIJ 2 Bright Light (MAGICFIRE) A_Explode (5, 32, 0)
		FRFX KLM 2 Bright Light (MAGICFIRE) A_Explode (5, 64, 0)
		FRFX NO 2 Bright
		Stop
	Death:
		FRFX HIJ 2 Bright Light (MAGICFIRE) A_Explode (5, 32, 0)
		FRFX KLM 2 Bright Light (MAGICFIRE) A_Explode (5, 64, 0)
		FRFX NO 2 Bright Light (MAGICFIRE)
		Stop
	}
}