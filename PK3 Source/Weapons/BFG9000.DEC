// Credits:
// DECORATE: Alex_Mercer, parts from zandronum.pk3
// Sprites: Bane, Midway, Neoworm
// Sounds: ID Software
// --------------------------------------------------------------------------
//
// BFG 9000
//
// --------------------------------------------------------------------------
ACTOR FDBFG9000 : FDWeapon 30035
{
	//$Category Final Duel-Weapons
	//$Sprite BFGGZ0
	Tag "$TAGBFG"
	Game Doom
	Height 20
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "Cell2"
	+WEAPON.NOAUTOFIRE
	+AMMO_OPTIONAL
	Inventory.PickupMessage "$GOTBFG9000"
	States
	{
	Ready:
	Ready2:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		TNT1 A 0 A_TakeInventory("Reloading",1)
		BFGG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(0,"DeselectDEAD")
		BFGG A 1 A_Lower
		Loop
	Select:
		BFGG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_PlaySound("BFG9000/BeginFire", CHAN_WEAPON)
		BFGG ABCD 3 Bright
		BFGG EFG 4 Bright
		TNT1 A 0 Bright A_PlaySound("BFG9000/Fire", CHAN_WEAPON)
		TNT1 A 0 Bright A_FireCustomMissile("FDBFGBall")
		TNT1 A 0 A_Recoil(cos(pitch) * 15.0)
		BFGG HIJK 4 Bright
		BFGG AAAAAAAAAAAAAAAAAAAA 2 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random(-15,15), random(0,3))
		Goto Ready2
	DryFire:
		BFGG A 0 A_PlaySound("BFG/Dryfire")
		Goto Ready2
	Flash:
		Goto LightDone
	Spawn:
		BFGG Z -1
		Stop
	}
}


ACTOR FDBFGBall : FastProjectile Replaces BFGBall
{
	Game Doom
	Radius 13
	Height 8
	Speed 20
	Damage (random(1,4) * 200)
	Projectile
	RenderStyle Add
	Alpha 0.75
	Obituary "$OBBFG9000"
	DamageType BFG
	+EXTREMEDEATH
	+SEEKERMISSILE
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("BFG9000/Fly", CHAN_BODY, 1.0, 1)
	Spawn2:
		BFS1 A 2 Bright
		TNT1 A 0 A_SpawnItemEx("BFGTrail")
		BFS1 AB 2 Bright
		TNT1 A 0 A_SpawnItemEx("BFGTrail")
		BFS1 B 2 Bright
		Loop
	Death:
		TNT1 A 0 A_PlaySound("BFG9000/ChargeExplosion", CHAN_BODY, 1.0, 0, 0.0)
		TNT1 A 0 A_SetTranslucent(1.0, 0)
		TNT1 A 0 A_SetScale(1.5)
		NN02 RRQQPPOONNMMLLKKIIHHGGFFEEDDCCBBAA 1 Bright A_Warp(AAPTR_TRACER, 0.0, 0.0, 15.0)
		TNT1 A 0 A_SetTranslucent(1.0, 1)
		TNT1 A 0 A_Warp(AAPTR_TRACER, 0.0, 0.0, 15.0)
		NN02 A 1 A_FadeOut(0.1999998)
		TNT1 A 0 A_Warp(AAPTR_TRACER, 0.0, 0.0, 15.0)
		NN02 A 1 A_FadeOut(0.1999998)
		TNT1 A 0 A_Warp(AAPTR_TRACER, 0.0, 0.0, 15.0)
		NN02 A 1 A_FadeOut(0.1999998)
		TNT1 A 0 A_Warp(AAPTR_TRACER, 0.0, 0.0, 15.0)
		NN02 A 1 A_FadeOut(0.1999998)
		TNT1 A 0 A_Warp(AAPTR_TRACER, 0.0, 0.0, 15.0)
		NN02 A 1 A_FadeOut(0.1999998)
		TNT1 A 15
		TNT1 AAAAA 1 A_FadeIn(0.1999998)
		TNT1 A 0 A_Warp(AAPTR_TRACER, 0.0, 0.0, 15.0)
		TNT1 A 0 A_Quake(8, 18, 0, 512, "")
		TNT1 A 0 A_SpawnItem("BFGShockwave")
		TNT1 A 0 A_Explode(96, 96)
		TNT1 A 0 A_SetScale(2.0)
		TNT1 A 0 A_PlaySound("BFG9000/Explode", 5, 1.0, 0, 0.0)
		BFE1 ABCDEF 3 Bright A_Warp(AAPTR_TRACER, 0.0, 0.0, 15.0)
		Stop
	}
}
ACTOR FDBFGExtra
{
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.75
	DamageType "Extreme"
	States
	{
	Spawn:
		BFE2 ABCD 8 Bright
		Stop
	}
}
Actor BFGTrail
{
	RenderStyle ADD
	+FORCEXYBILLBOARD
	+NOGRAVITY
	Alpha 0.75
	Scale 0.5
	States
	{
	Spawn:
		ENR1 C 8 Bright
	Fade:
		ENR1 C 1 Bright A_FadeOut(0.1)
		Wait
	}
}
Actor BFGShockwave
{
	Translation "192:207=112:127", "240:247=120:127"
	RenderStyle ADD
	Alpha 5.0
	Scale 2.0
	+FORCEXYBILLBOARD
	+NOGRAVITY
	States
	{
	Spawn:
		SSHK ABCDEFGHIJKLMNOPQR 1
		Stop
	}
}