// Credits:
// DECORATE: Alex_Mercer
// Sprites: Bane, Midway, Neoworm
// Sounds: ID Software, Alex_Mercer
Actor DroppedBFG : DroppedWeaponBase {
	States {
	Spawn:
		BFGG Z 1
		Loop
	Death:
		BFGG Z 0 A_SpawnItemEx ("BioForceGun")
		Stop
	}
}

Actor AmmoBFG : AMMO {
	Inventory.MaxAmount 80
	Ammo.BackpackMaxAmount 80
	+IgnoreSkill
}
Actor BFGMode2 : Boolean { }
Actor BioForceGun : FDWeapon 30035 {
	//$Category Final Duel-Weapons
	//$Title Bio Force Gun MK XVI
	//$Sprite BFGGZ0
	Tag "$TAGBFG"
	Game Doom
	Height 20
	Weapon.AmmoUse 40
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoType "AmmoBFG"
	Weapon.AmmoType2 "Cell2"
	Inventory.PickupMessage "$GOTBFG"
	+Alt_Ammo_Optional
	States {
	Ready:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 75)
		BFGS IHGFEDCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		BFGG AA 1 A_WeaponReady (WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory ("PressingZoom", 999)
		Loop
	Zoom:
		TNT1 A 0 A_JumpIfInventory ("PressingZoom", 1, "Ready2")
		TNT1 A 0 A_GiveInventory ("PressingZoom", 1)
		TNT1 A 0 A_PlaySound("Weapons/Mode",5,1.5)
		TNT1 A 0 A_JumpIfInventory ("BFGMode2", 1, "BFGMode1")
		TNT1 A 0 A_Print ("- Seeking mode -")
		BFGG A 10 A_GiveInventory ("BFGMode2", 1)
		Goto Ready2
	BFGMode1:
		TNT1 A 0 A_Print ("- Normal mode -")
		BFGG A 10 A_TakeInventory ("BFGMode2", 1)
		Goto Ready2
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedBFG", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("BioForceGun", 1)
		Stop
	Deselect:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 100)
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		BFGS ABCDEFGHI 1
		BFGG A 0 A_Lower
		Wait
	Select:
		BFGG A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIf (CallACS ("CheckBFGBall") == 1, "KillBFGBall")
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_JumpIfInventory ("BFGMode2", 1, "Fire2")
		TNT1 A 0 A_PlaySound ("BFG/BeginFire", CHAN_WEAPON)
		BFGG ABCD 3 Bright
		BFGG EFG 4 Bright
		TNT1 A 0 Bright A_PlaySound ("BFG/Fire", CHAN_WEAPON)
		TNT1 A 0 Bright A_FireCustomMissile ("FDBFGBall")
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -15.0, 0, Sin(Pitch) * 15.0, 1)
		BFGG HIJK 4 Bright
		Goto Ready2
	Fire2:
		TNT1 A 0 A_PlaySound ("BFG/BeginFire", CHAN_WEAPON)
		BFGG ABCD 3 Bright
		BFGG EFG 4 Bright
		TNT1 A 0 Bright A_PlaySound ("BFG/Fire", CHAN_WEAPON)
		TNT1 A 0 Bright A_FireCustomMissile ("FDBFGBall2")
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -15.0, 0, Sin(Pitch) * 15.0, 1)
		BFGG HIJK 4 Bright
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIf (CallACS ("CheckBFGBall") == 1, "KillBFGBall")
		TNT1 A 0 A_JumpIfInventory ("AmmoBFG", 80, 1)
		Goto DryFire
		TNT1 A 0 A_PlaySound ("BFG/BeginFire", CHAN_WEAPON)
		BFGG ABCD 4 Bright
		BFGG EFG 5 Bright
		TNT1 A 0 A_PlaySound ("BFG/Fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("FDBFGBallAlt")
		TNT1 A 0 A_TakeInventory ("AmmoBFG", 80)
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -25.0, 0, Sin(Pitch) * 25.0, 1)
		BFGG HIJK 4 Bright
		Goto Ready2
	KillBFGBall:
		TNT1 A 0 A_PlaySound ("BFG/SpecialAction", 7)
		BFGG A 5 ACS_NamedExecute ("KillBFGBall", 0)
		Goto Ready2
	Reload:
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("Cell2", 1, 1)
        Goto Ready2
		TNT1 A 0 A_JumpIfInventory ("AmmoBFG", 80, "Ready2")
		TNT1 A 0 A_JumpIfInventory ("AmmoBFG", 1, 1)
		BFGG AAAAAAAAAA 2 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random (-15, 15), random (0, 3))
		BFGG AAAAAAAAAA 2 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random (-15, 15), random (0, 3))
		BFGS ABCDEFGHI 1
		TNT1 A 1
		TNT1 A 0 A_FireCustomMissile ("PlasmaCaseSpawn", 0, 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("AmmoBFG", 80)
		TNT1 A 52 A_PlaySound ("BFG/CellOut", CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory ("AmmoBFG", 80)
		TNT1 A 0 A_TakeInventory ("Cell2", 1)
		TNT1 A 30 A_PlaySound ("BFG/CellIn", CHAN_WEAPON)
		BFGS IHGFEDCBA 1
		Goto Ready2
	DryFire:
		BFGG A 12 A_PlaySound ("BFG/Dryfire", 7)
		TNT1 A 0 A_JumpIfInventory ("AmmoBFG", 40, "Ready2")
		TNT1 A 0 A_JumpIfInventory ("EnableAutoReload", 1, "Reload")
		Goto Ready2
	Flash:
		Goto LightDone
	Spawn:
		BFGG Z -1
		Stop
	}
}

Actor FDBFGBall : FastProjectile Replaces BFGBall {
	Radius 13
	Height 8
	Speed 13
	Damage (random (1, 4) * 200)
	Projectile
	RenderStyle Add
	Alpha 0.75
	Obituary "$OBBFG"
	SelfObituary "$OBBFGSELF"
	DamageType BFG
	+ExtremeDeath +DontSplash +ForceRadiusDMG +SeekerMissile
	States {
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound ("BFG/Fly", CHAN_BODY, 1.0, 1) // Start flying sound loop
		TNT1 A 0 Thing_ChangeTID (0, CallACS ("SetTIDForBFGBall")) // Set our TID
		TNT1 A 0 A_SeekerMissile (0, 0, SMF_LOOK, 256)
	Spawn2:
		BFS1 AA 1 Bright A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		TNT1 A 0 A_SpawnItemEx ("FDBFGExtra", 0.0, 0.0, 0.0,
			0.0, 0.0, 0.0, 0, SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS) // Spawn secondary effect
		TNT1 A 0 A_SpawnItemEx ("BFGTrail") // Spawn trail
		BFS1 AABB 1 Bright A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		TNT1 A 0 A_SpawnItemEx ("BFGTrail") // Spawn trail
		BFS1 BB 1 Bright A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		Loop
	KilledByPlayer:
	Death:
		TNT1 A 0 Thing_ChangeTID (0, 0)
		TNT1 A 0 A_Quake (8, 18, 0, 512, "") // Shake the ground
		TNT1 A 0 A_SpawnItem ("BFGShockwave") // Spawn a shockwave
		TNT1 A 0 A_Explode (256, 192) // Explode with a radius of 256 and damage of 256
		TNT1 A 0 A_SetScale (2.0) // Set scale to 2.0
		TNT1 A 0 A_PlaySound ("BFG/Explode", 5, 1.0, 0, 0.0) // Play "BFG/Explode" in channel 5 with full volume everywhere
		TNT1 A 0 A_RemoveChildren // Remove any secondary effect actor still alive
		TNT1 AAAAAAAA 0 A_SpawnItemEx ("BFGTrail", 0.0, 0.0, 0.0, 0.0, frandom (-4.0, 4.0), frandom (-4.0, 4.0))
		BFE1 ABCDEF 3 Bright
		Stop
	}
}

Actor FDBFGBall2 : FDBFGBall {
	States {
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound ("BFG/Fly", CHAN_BODY, 1.0, 1) // Start flying sound loop
		TNT1 A 0 Thing_ChangeTID (0, CallACS ("SetTIDForBFGBall")) // Set our TID
	Spawn2:
		TNT1 A 0 A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		BFS1 A 1 Bright A_SeekerMissile (30, 65, SMF_LOOK|SMF_PRECISE, 128)
		TNT1 A 0 A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		BFS1 A 1 Bright A_SeekerMissile (30, 65, SMF_LOOK|SMF_PRECISE, 128)
		TNT1 A 0 A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		TNT1 A 0 A_SpawnItemEx ("BFGTrail") // Spawn trail
		BFS1 A 1 Bright A_SeekerMissile (30, 65, SMF_LOOK|SMF_PRECISE, 128)
		TNT1 A 0 A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		BFS1 A 1 Bright A_SeekerMissile (30, 65, SMF_LOOK|SMF_PRECISE, 128)
		TNT1 A 0 A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		BFS1 B 1 Bright A_SeekerMissile (30, 65, SMF_LOOK|SMF_PRECISE, 128)
		TNT1 A 0 A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		BFS1 B 1 Bright A_SeekerMissile (30, 65, SMF_LOOK|SMF_PRECISE, 128)
		TNT1 A 0 A_SpawnItemEx ("BFGTrail") // Spawn trail
		TNT1 A 0 A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		BFS1 B 1 Bright A_SeekerMissile (30, 65, SMF_LOOK|SMF_PRECISE, 128)
		TNT1 A 0 A_SpawnItemEx ("BFGParticleSpawner") // Spawn particles
		BFS1 B 1 Bright A_SeekerMissile (30, 65, SMF_LOOK|SMF_PRECISE, 128)
		Loop
	KilledByPlayer:
	Death:
		TNT1 A 0 Thing_ChangeTID (0, 0)
		TNT1 A 0 A_Quake (4, 18, 0, 256, "") // Shake the ground
		TNT1 A 0 A_SpawnItem ("BFGShockwave") // Spawn a shockwave
		TNT1 A 0 A_Explode (128, 128) // Explode with a radius of 128 and damage of 128
		TNT1 A 0 A_PlaySound ("BFG/Explode", 5, 0.8, 0, 0.0) // Play "BFG/Explode" in channel 5 with full volume everywhere
		TNT1 AAAAAAAA 0 A_SpawnItemEx ("BFGTrail", 0.0, 0.0, 0.0, 0.0, frandom (-4.0, 4.0), frandom (-4.0, 4.0))
		BFE1 ABCDEF 3 Bright
		Stop
	}
}

Actor FDBFGBallAlt : FDBFGBall {
	Radius 12
	Speed 20
	MissileType "BFGAltEyecandySpawner"
	+Ripper +BloodlessImpact
	Damage 0
	
	var int user_ExplosionDMG;
	States {
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound ("BFG/Fly", CHAN_BODY, 1.0, 1) // Start flying sound loop
		TNT1 A 0 Thing_ChangeTID (0, CallACS ("SetTIDForBFGBall")) // Set our TID
	Spawn2:
		TNT1 A 0 A_Explode (200, 36, 0)
		TNT1 AA 1 A_SpawnItemEx ("BFGAltEyecandySpawner") // Spawn effects
		TNT1 A 0 A_Explode (200, 36, 0)
		TNT1 AA 1 A_SpawnItemEx ("BFGAltEyecandySpawner") // Spawn effects
		TNT1 A 0 A_Explode (200, 36, 0)
		TNT1 AA 1 A_SpawnItemEx ("BFGAltEyecandySpawner") // Spawn effects
		TNT1 A 0 A_Explode (200, 36, 0)
		TNT1 AA 1 A_SpawnItemEx ("BFGAltEyecandySpawner") // Spawn effects
		Loop
	Death:
		TNT1 A 0 A_SetUserVar ("user_ExplosionDMG", random (192, 256))
		TNT1 A 0 Thing_ChangeTID (0, 0)
		TNT1 A 0 A_Quake (6, 18, 0, 384, "") // Shake the ground
		TNT1 A 0 A_Explode (user_ExplosionDMG, user_ExplosionDMG) // Explode with a radius and damage of random (192, 256)
		TNT1 A 0 A_PlaySound ("BFG/Explode", 5, 1.0, 0, 0.0) // Play "BFG/Explode" in channel 5 with full volume everywhere
		BFE1 ABCDEF 3 Bright
		Stop
	}
}

Actor FDBFGExtra {
	Health -1

	+NoGravity +ExtremeDeath +IsMonster +NoTarget
	+NeverTarget +NotAutoaimed +Friendly +NoDamage
	+SpecialMonster
	
	States {
	Spawn:
		TNT1 A 0 // This is here to make sure nothing fucks up
		TNT1 A 0 A_RearrangePointers (AAPTR_TRACER, AAPTR_TARGET, AAPTR_MASTER) // Set up our pointers

	Working:
		TNT1 A 0 A_CustomRailgun (random (1, 4) * 4, 0, none, 00FF00, RGF_SILENT|RGF_FULLBRIGHT, 1, 1.0, "FDBFGExtraPuff") // Fire a railgun attack at the target
		TNT1 A 0 A_CustomBulletAttack (0.0, 0.0, 1, 0, "FDBFGExtraPuff2", 0, CBAF_NORANDOMPUFFZ)
		Stop
	}
}

Actor FDBFGExtraPuff { +PuffGetsOwner +BloodlessImpact +NoGravity +ExtremeDeath DamageType "BFGExtra" }
Actor FDBFGExtraPuff2 : FDBFGExtraPuff {
	RenderStyle Add
	Alpha 0.75
	+Painless
	States {
	Spawn:
		BFE2 ABCD 4
		Stop
	}
}

// Eyecandy

Actor BFGTrail {
	Alpha 0.75
	Scale 0.5
	RenderStyle Add
	+ForceXYBillboard +NoGravity +NoInteraction
	States {
	Spawn:
		ENR1 C 8
	Fade:
		ENR1 C 1 Bright A_FadeOut (0.1)
		Wait
	}
}

Actor BFGShockwave {
	Translation "192:207=112:127", "240:247=120:127"
	RenderStyle Add
	Alpha 5.0
	Scale 2.0
	+ForceXYBillboard +NoGravity
	States {
	Spawn:
		SSHK ABCDEFGHIJKLMNOPQR 1 Bright
		Stop
	}
}

Actor BFGParticleSpawner : ParticleSpawnerBase {
	States {
	Spawn:
		TNT1 A 0 NoDelay A_CustomMissile ("BFGParticle", 0, random (-40,40), 180, 2)
		Stop
	}
}

Actor BFGParticle {
	Mass 0
	+Missile +NoBlockMap +DontSplash +DoomBounce
	+ForceXYBillboard
	BounceFactor 0.2
	Gravity 0.8
	RenderStyle Add
	Scale 0.035
	Speed 12
	States {
	Spawn:
		TNT1 A 0 NoDelay A_Jump (256, "SpawnA", "SpawnB", "SpawnC", "SpawnD", "SpawnE", "SpawnF", "SpawnG", "SpawnH")
		Loop
	Fade:
		"----" A 1 Bright A_FadeOut (0.03)
		Wait
	SpawnA:
		FLR2 A 10 Bright
		Goto Fade
	SpawnB:
		FLR2 B 10 Bright
		Goto Fade
	SpawnC:
		FLR2 C 10 Bright
		Goto Fade
	SpawnD:
		FLR2 D 10 Bright
		Goto Fade
	SpawnE:
		FLR2 E 10 Bright
		Goto Fade
	SpawnF:
		FLR2 F 10 Bright
		Goto Fade
	SpawnG:
		FLR2 G 10 Bright
		Goto Fade
	SpawnH:
		FLR2 H 10 Bright
		Goto Fade
	}
}

Actor BFGAltEyecandySpawner : ParticleSpawnerBase {
	States {
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx ("BFGAltFlare", 10.0, 0.0, 0.0, 0.0, 0.0, 0.0)
		Stop
	}
}

Actor BFGAltFlare {
	Scale .256
	Renderstyle Add
	+NoGravity +NoInteraction
	States {
	Spawn:
		BFAF A 0 NoDelay A_SetScale (ScaleX - 0.01, ScaleY - 0.01)
		BFAF A 1 Bright A_FadeOut (0.1)
		Loop
	}
}