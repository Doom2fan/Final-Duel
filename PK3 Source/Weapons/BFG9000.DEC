// Credits:
// DECORATE: Alex_Mercer
// Sprites: Bane, Midway, Neoworm
// Sounds: ID Software, Alex_Mercer
Actor DroppedBFG : DroppedWeaponBase {
	States {
	Spawn:
		BFGG Z 1
		Loop
	Death:
		BFGG Z 0 A_SpawnItemEx ("BioForceGun")
		Stop
	}
}

Actor AmmoBFG : AMMO {
	Inventory.MaxAmount 80
	Ammo.BackpackMaxAmount 80
	+IgnoreSkill
}

Actor BioForceGun : FDWeapon 30035 {
	//$Category Final Duel-Weapons
	//$Title Bio Force Gun MK XVI
	//$Sprite BFGGZ0
	Tag "$TAGBFG"
	Game Doom
	Height 20
	Weapon.AmmoUse 40
	Weapon.AmmoGive 0
	Weapon.AmmoType "AmmoBFG"
	Weapon.AmmoType2 "Cell2"
	Inventory.PickupMessage "$GOTBFG"
	+Alt_Ammo_Optional
	States {
	Ready:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 75)
		BFGS IHGFEDCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		BFGG A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedBFG", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("BioForceGun", 1)
		Stop
	Deselect:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 100)
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		BFGS ABCDEFGHI 1
		BFGG A 0 A_Lower
		Wait
	Select:
		BFGG A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIf (CallACS ("CheckBFGBall") == 1, "KillBFGBall")
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_PlaySound ("BFG/BeginFire", CHAN_WEAPON)
		BFGG ABCD 3 Bright
		BFGG EFG 4 Bright
		TNT1 A 0 Bright A_PlaySound ("BFG/Fire", CHAN_WEAPON)
		TNT1 A 0 Bright A_FireCustomMissile ("FDBFGBall")
		TNT1 A 0 A_Recoil (cos (pitch) * 15.0)
		BFGG HIJK 4 Bright
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIf (CallACS ("CheckBFGBall") == 1, "KillBFGBall")
		goto KillBFGBall
		TNT1 A 0 A_JumpIfInventory ("AmmoBFG", 80, 1)
		Goto DryFire
		TNT1 A 0 A_PlaySound ("BFG/BeginFire", CHAN_WEAPON)
		BFGG ABCD 4 Bright
		BFGG EFG 5 Bright
		TNT1 A 0 Bright A_PlaySound ("BFG/Fire", CHAN_WEAPON)
		TNT1 A 0 Bright A_FireCustomMissile ("FDBFGBallAlt")
		TNT1 A 0 A_Recoil (cos (pitch) * 20.0)
		BFGG HIJK 4 Bright
		Goto Ready2
	KillBFGBall:
		TNT1 A 0 A_PlaySound ("BFG/SpecialAction", 7)
		BFGG A 5 ACS_NamedExecute ("KillBFGBall", 0)
		Goto Ready2
	Reload:
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("Cell2", 1, 1)
        Goto Ready2
		TNT1 A 0 A_JumpIfInventory ("AmmoBFG", 80, "Ready2")
		TNT1 A 0 A_JumpIfInventory ("AmmoBFG", 1, 1)
		BFGG AAAAAAAAAA 2 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random (-15, 15), random (0, 3))
		BFGG AAAAAAAAAA 2 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random (-15, 15), random (0, 3))
		BFGS ABCDEFGHI 1
		TNT1 A 1
		TNT1 A 0 A_FireCustomMissile ("PlasmaCaseSpawn", 0, 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("AmmoBFG", 80)
		TNT1 A 52 A_PlaySound ("BFG/CellOut", CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory ("AmmoBFG", 80)
		TNT1 A 0 A_TakeInventory ("Cell2", 1)
		TNT1 A 30 A_PlaySound ("BFG/CellIn", CHAN_WEAPON)
		BFGS IHGFEDCBA 1
		Goto Ready2
	DryFire:
		BFGG A 12 A_PlaySound ("BFG/Dryfire", 7)
		TNT1 A 0 A_JumpIfInventory("EnableAutoReload",1,"Reload")
		Goto Ready2
	Flash:
		Goto LightDone
	Spawn:
		BFGG Z -1
		Stop
	}
}

Actor FDBFGBall : FastProjectile Replaces BFGBall {
	Radius 13
	Height 8
	Speed 13
	Damage (random (1, 4) * 200)
	Projectile
	RenderStyle Add
	Alpha 0.75
	Obituary "$OBBFG"
	SelfObituary "$OBBFGSELF"
	DamageType BFG
	+ExtremeDeath +DontSplash +SeekerMissile
	States {
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound ("BFG/Fly", CHAN_BODY, 1.0, 1) // Start flying sound loop
		TNT1 A 0 Thing_ChangeTID (0, CallACS ("SetTIDForBFGBall")) // Set our TID
	Spawn2:
		BFS1 AA 1 Bright A_SeekerMissile (25, 55, SMF_LOOK|SMF_PRECISE)
		TNT1 A 0 A_SpawnItemEx ("FDBFGExtra", 0.0, 0.0, 0.0,
			0.0, 0.0, 0.0, 0, SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS) // Spawn secondary effect
		TNT1 A 0 A_SpawnItemEx ("BFGTrail") // Spawn trail
		BFS1 AABB 1 Bright A_SeekerMissile (25, 55, SMF_LOOK|SMF_PRECISE)
		TNT1 A 0 A_SpawnItemEx ("BFGTrail") // Spawn trail
		BFS1 BB 1 Bright A_SeekerMissile (25, 55, SMF_LOOK|SMF_PRECISE)
		Loop
	KilledByPlayer:
		TNT1 A 0 A_Quake (8, 18, 0, 512, "") // Shake the ground
		TNT1 A 0 A_SpawnItem ("BFGShockwave") // Spawn a shockwave
		TNT1 A 0 A_Explode (256, 256) // Explode with a radius of 256 and damage of 256
		TNT1 A 0 A_SetScale (2.0) // Set scale to 2.0
		TNT1 A 0 A_PlaySound ("BFG/Explode", 5, 1.0, 0, 0.0) // Play "BFG/Explode" in channel 5 with full volume everywhere
		TNT1 A 0 A_RemoveChildren // Remove any secondary effect actor still alive
		BFE1 ABCDEF 3 Bright
		Stop
	Death:
		TNT1 A 0 Thing_ChangeTID (0, 0)
		TNT1 A 0 A_Quake (8, 18, 0, 512, "") // Shake the ground
		TNT1 A 0 A_SpawnItem ("BFGShockwave") // Spawn a shockwave
		TNT1 A 0 A_Explode (256, 256) // Explode with a radius of 256 and damage of 256
		TNT1 A 0 A_SetScale (2.0) // Set scale to 2.0
		TNT1 A 0 A_PlaySound ("BFG/Explode", 5, 1.0, 0, 0.0) // Play "BFG/Explode" in channel 5 with full volume everywhere
		TNT1 A 0 A_RemoveChildren // Remove any secondary effect actor still alive
		BFE1 ABCDEF 3 Bright
		Stop
	}
}

Actor FDBFGExtra {
	Health -1

	+NoGravity +ExtremeDeath +IsMonster +NoTarget
	+NeverTarget +NotAutoaimed +Friendly +NoDamage
	+SpecialMonster
	
	States {
	Spawn:
		TNT1 A 0 // This is here to make sure nothing fucks up
		TNT1 A 0 A_RearrangePointers (AAPTR_TRACER, AAPTR_TARGET, AAPTR_MASTER) // Set up our pointers

	Working:
		TNT1 A 0 A_CustomRailgun (random (1, 4) * 4, 0, none, 00FF00, RGF_SILENT|RGF_FULLBRIGHT, 1, 1.0, "FDBFGExtraPuff") // Fire a railgun attack at the target
		Stop
	}
}

Actor FDBFGExtraPuff {
	+PuffGetsOwner +BloodlessImpact +NoGravity
	RenderStyle Add
	Alpha 0.75
	DamageType "BFGExtra"
	+ExtremeDeath
	States {
	Spawn:
		BFE2 ABCD 4
		Stop
	Crash:
		Stop
	}
}

Actor BFGTrail {
	Alpha 0.75
	Scale 0.5
	RenderStyle Add
	+ForceXYBillboard +NoGravity
	States {
	Spawn:
		ENR1 C 8 Bright
	Fade:
		ENR1 C 1 Bright A_FadeOut (0.1)
		Wait
	}
}

Actor BFGShockwave {
	Translation "192:207=112:127", "240:247=120:127"
	RenderStyle Add
	Alpha 5.0
	Scale 2.0
	+ForceXYBillboard +NoGravity
	States {
	Spawn:
		SSHK ABCDEFGHIJKLMNOPQR 1
		Stop
	}
}