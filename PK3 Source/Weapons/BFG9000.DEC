// Credits:
// DECORATE: Alex_Mercer
// Sprites: Bane, Midway, Neoworm
// Sounds: ID Software
Actor DroppedBFG : DroppedWeaponBase
{
	States
	{
	Spawn:
		BFGG Z 1
		Loop
	Death:
		BFGG Z 0 A_SpawnItemEx("BioForceGun")
		Stop
	}
}
Actor BioForceGun : FDWeapon 30035
{
	//$Category Final Duel-Weapons
	//$Title Bio Force Gun MK XVI
	//$Sprite BFGGZ0
	Tag "$TAGBFG"
	Game Doom
	Height 20
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "Cell2"
	+Weapon.NOAUTOFIRE
	+AMMO_OPTIONAL
	Inventory.PickupMessage "$GOTBFG9000"
	States
	{
	Ready:
		TNT1 A 0 ACS_NamedExecute("PlayerSpeed", 0, 50)
		BFGS EDCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory("DropDatWeapon",1,"DropWeapon")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		TNT1 A 0 A_TakeInventory("Reloading",1)
		BFGG A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile("DroppedBFG", 0, 0, 0)
		TNT1 A 0 A_TakeInventory("BioForceGun", 1)
		Stop
	Deselect:
		TNT1 A 0 ACS_NamedExecute("PlayerSpeed", 0, 100)
		TNT1 A 0 A_JumpIfHealthLower(0,"DeselectDEAD")
		BFGS ABCDE 1
		BFGG A 0 A_Lower
		Wait
	Select:
		BFGG A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_PlaySound("BFG9000/BeginFire", CHAN_Weapon)
		BFGG ABCD 3 Bright
		BFGG EFG 4 Bright
		TNT1 A 0 Bright A_PlaySound("BFG9000/Fire", CHAN_Weapon)
		TNT1 A 0 Bright A_FireCustomMissile("FDBFGBall")
		TNT1 A 0 A_Recoil(cos(pitch) * 15.0)
		BFGG HIJK 4 Bright
		BFGG AAAAAAAAAAAAAAAAAAAA 2 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random(-15,15), random(0,3))
		Goto Ready2
	DryFire:
		BFGG A 0 A_PlaySound("BFG/Dryfire")
		Goto Ready2
	Flash:
		Goto LightDone
	Spawn:
		BFGG Z -1
		Stop
	}
}


Actor FDBFGBall : FastProjectile Replaces BFGBall
{
	Game Doom
	Radius 13
	Height 8
	Speed 20
	Damage (random(1,4) * 200)
	Projectile
	RenderStyle Add
	Alpha 0.75
	Obituary "$OBBFG9000"
	DamageType BFG
	+ExtremeDeath
	+SEEKERMISSILE
	+DontSplash
	+SCREENSEEKER
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("BFG9000/Fly", CHAN_BODY, 1.0, 1)
	Spawn2:
		BFS1 A 2 Bright
		TNT1 A 0 A_SpawnItemEx("BFGTrail")
		BFG1 AB 2 Bright
		TNT1 A 0 A_SpawnItemEx("BFGTrail")
		BFS1 B 2 Bright
		Loop
	Death:
		TNT1 A 0 A_PlaySound("BFG9000/ChargeExplosion", CHAN_BODY, 1.0, 0, 0.0)
		TNT1 A 0 A_SetTranslucent(1.0, 0)
		TNT1 A 0 A_SetScale(1.5)
		NN02 RRQQPPOONNMMLLKKIIHHGGFFEEDDCCBBAA 1 Bright
		TNT1 A 0 A_SetTranslucent(1.0, 1)
		NN02 AAAAA 1 A_FadeOut(0.1999998)
		TNT1 A 15
		TNT1 AAAAA 1 A_FadeIn(0.1999998)
		TNT1 A 0 A_Quake(8, 18, 0, 512, "")
		TNT1 A 0 A_SpawnItem("BFGShockwave")
		TNT1 A 0 A_Explode(128, 128)
		TNT1 A 0 A_SetScale(2.0)
		TNT1 A 0 A_PlaySound("BFG9000/Explode", 5, 1.0, 0, 0.0)
		TNT1 A 0 A_SpawnItemEx("FDBFGExtra", 0.0, 0.0, 0.0,
								0.0, 0.0, 0.0, 0.0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		BFE1 ABCDEF 3 Bright
		Stop
	}
}

Actor FDBFGExtra
{
	Monster
	Health 0
	+NODAMAGE
	+NoBlockmap
	+NoGravity
	+ExtremeDeath
	+FRIENDLY
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_RearrangePointers(AAPTR_NULL, AAPTR_TARGET, AAPTR_NULL)
		TNT1 A 0 A_Chase
		TNT1 A 0 A_JumpIfPointerNull(AAPTR_TARGET, "NoTarget")
		TNT1 A 0 A_FaceTarget
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomRailgun(random(1,4) * 8, 0, none, 00FF00, RGF_SILENT|RGF_NOPIERCING|RGF_FULLBright, 0, 2.0, "FDBFGExtraPuff", 0, 0)
		Stop
	NoTarget:
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomRailgun(random(1,2) * 6, 0, none, "00 FF 00", RGF_SILENT|RGF_NOPIERCING|RGF_FULLBright|RGF_EXPLICITANGLE, 0, 0, "FDBFGExtraPuff", frandom(-180.0, 180.0), frandom(-90.0, 90.0), 512)
		Stop
	}
}

Actor FDBFGExtraPuff
{
	DamageType "BFGExtra"
}

Actor BFGTrail
{
	RenderStyle ADD
	+ForceXYBillboard
	+NoGravity
	Alpha 0.75
	Scale 0.5
	States
	{
	Spawn:
		ENR1 C 8 Bright
	Fade:
		ENR1 C 1 Bright A_FadeOut(0.1)
		Wait
	}
}
Actor BFGShockwave
{
	Translation "192:207=112:127", "240:247=120:127"
	RenderStyle ADD
	Alpha 5.0
	Scale 2.0
	+ForceXYBillboard
	+NoGravity
	States
	{
	Spawn:
		SSHK ABCDEFGHIJKLMNOPQR 1
		Stop
	}
}