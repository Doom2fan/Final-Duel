// Credits:
// DECORATE: Alex_Mercer
// Sprites: Bane, Midway, Neoworm
// Sounds: ID Software
Actor DroppedBFG : DroppedWeaponBase {
	States {
	Spawn:
		BFGG Z 1
		Loop
	Death:
		BFGG Z 0 A_SpawnItemEx ("BioForceGun")
		Stop
	}
}

Actor AmmoBFG : AMMO {
	Inventory.MaxAmount 80
	Ammo.BackpackMaxAmount 80
	+IgnoreSkill
}

Actor BioForceGun : FDWeapon 30035 {
	//$Category Final Duel-Weapons
	//$Title Bio Force Gun MK XVI
	//$Sprite BFGGZ0
	Tag "$TAGBFG"
	Game Doom
	Height 20
	Weapon.AmmoUse 40
	Weapon.AmmoGive 0
	Weapon.AmmoType "AmmoBFG"
	Weapon.AmmoType2 "Cell2"
	Inventory.PickupMessage "$GOTBFG"
	States {
	Ready:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 75)
		BFGS IHGFEDCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		BFGG A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedBFG", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("BioForceGun", 1)
		Stop
	Deselect:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 100)
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		BFGS ABCDEFGHI 1
		BFGG A 0 A_Lower
		Wait
	Select:
		BFGG A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_PlaySound ("BFG/BeginFire", CHAN_WEAPON)
		BFGG ABCD 3 Bright
		BFGG EFG 4 Bright
		TNT1 A 0 Bright A_PlaySound ("BFG/Fire", CHAN_WEAPON)
		TNT1 A 0 Bright A_FireCustomMissile ("FDBFGBall")
		TNT1 A 0 A_Recoil (cos (pitch) * 15.0)
		BFGG HIJK 4 Bright
		BFGG AAAAAAAAAAAAAAAAAAAA 2 A_FireCustomMissile ("HeatSmokeSpawner", 0, 0, random (-15, 15), random (0, 3))
		Goto Ready2
	Reload:
		TNT1 A 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("Cell2",1,1)
        Goto Ready2
		TNT1 A 0 A_JumpIfInventory("AmmoBFG",80,"Ready2")
		BFGS ABCDEFGHI 1
		TNT1 A 1
		TNT1 A 0 A_FireCustomMissile("PlasmaCaseSpawn",0,0,0,0)
		TNT1 A 0 A_TakeInventory("AmmoBFG",80)
		TNT1 A 52 A_PlaySound("BFG/CellOut", CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("AmmoBFG",80)
		TNT1 A 0 A_TakeInventory("Cell2",1)
		TNT1 A 30 A_PlaySound("BFG/CellIn", CHAN_WEAPON)
		BFGS IHGFEDCBA 1
		Goto Ready2
	DryFire:
		BFGG A 12 A_PlaySound ("BFG/Dryfire", 7)
		TNT1 A 0 A_JumpIfInventory("EnableAutoReload",1,"Reload")
		Goto Ready2
	Flash:
		Goto LightDone
	Spawn:
		BFGG Z -1
		Stop
	}
}

Actor FDBFGBall : FastProjectile Replaces BFGBall {
	Game Doom
	Radius 13
	Height 8
	Speed 20
	Damage (random (1, 4) * 200)
	Projectile
	RenderStyle Add
	Alpha 0.75
	Obituary "$OBBFG"
	SelfObituary "$OBBFGSELF"
	DamageType BFG
	+ExtremeDeath +SeekerMissile +DontSplash +ScreenSeeker
	States {
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound ("BFG/Fly", CHAN_BODY, 1.0, 1)
	Spawn2:
		BFS1 A 2 Bright
		TNT1 A 0 A_SpawnItemEx ("BFGTrail")
		BFG1 AB 2 Bright
		TNT1 A 0 A_SpawnItemEx ("BFGTrail")
		BFS1 B 2 Bright
		Loop
	Death:
		TNT1 A 0 A_PlaySound ("BFG/ChargeExplosion", CHAN_BODY, 1.0, 0, 0.0)
		TNT1 A 0 A_SetTranslucent (1.0, 0)
		TNT1 A 0 A_SetScale (1.5)
		NN02 ABCDEFGHIKLMNOPQR 2 Bright
		TNT1 A 0 A_SetTranslucent (1.0, 1)
		TNT1 A 0 A_FadeOut (0.999999)
		TNT1 A 15
		TNT1 A 5 A_FadeIn (0.999999)
		TNT1 A 0 A_Quake (8, 18, 0, 512, "")
		TNT1 A 0 A_SpawnItem ("BFGShockwave")
		TNT1 A 0 A_Explode (128, 128)
		TNT1 A 0 A_SetScale (2.0)
		TNT1 A 0 A_PlaySound ("BFG/Explode", 5, 1.0, 0, 0.0)
		TNT1 A 0 A_SpawnItemEx ("FDBFGExtra", 0.0, 0.0, 0.0,
								0.0, 0.0, 0.0, 0.0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		BFE1 ABCDEF 3 Bright
		Stop
	}
}

Actor FDBFGExtra {
	Monster
	Health -1
	Speed 0
	Mass 0x7fffffff
	+NoDamage +NoBlockmap +NoGravity +ExtremeDeath
	+LookAllAround +IsMonster
	States {
	Spawn:
		TNT1 A 0 NoDelay A_RearrangePointers (AAPTR_NULL, AAPTR_TARGET, AAPTR_NULL)
		TNT1 AAAAAAAAAA 0 A_LookEx
		TNT1 AAAAAAAAAA 0 A_Chase
		TNT1 AAAAA 0 A_FaceTarget
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomRailgun (random (1, 4) * 8, 0, none, 00FF00, RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT, 1, 2.0, "FDBFGExtraPuff", 0, 0)
		Stop
	}
}

Actor FDBFGExtraPuff { DamageType "BFGExtra" }

Actor BFGTrail {
	Alpha 0.75
	Scale 0.5
	RenderStyle Add
	+ForceXYBillboard +NoGravity
	States {
	Spawn:
		ENR1 C 8 Bright
	Fade:
		ENR1 C 1 Bright A_FadeOut (0.1)
		Wait
	}
}

Actor BFGShockwave {
	Translation "192:207=112:127", "240:247=120:127"
	RenderStyle Add
	Alpha 5.0
	Scale 2.0
	+ForceXYBillboard +NoGravity
	States {
	Spawn:
		SSHK ABCDEFGHIJKLMNOPQR 1
		Stop
	}
}