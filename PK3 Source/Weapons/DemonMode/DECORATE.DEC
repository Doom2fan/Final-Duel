Actor DemonModeOn : Boolean { }
Actor DemonMode : FDWeapon {
	Tag "$TAGDEMONMODE"
	Obituary "$OBDEMONMODE"
	+Weapon.MeleeWeapon +Weapon.NoAlert +Weapon.NoAutoaim
	States {
	Ready:
		FSTS CBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DemonModeOn", 1, 1)
		Goto KillDemonMode
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_TakeInventory ("Taunting", 1)
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		FIST A 1 A_WeaponReady (WRF_DISABLESWITCH)
		Loop
	KillDemonMode:
		FIST A 1 A_WeaponReady (WRF_NOFIRE)
		TNT1 A 0 A_SelectWeapon ("Magic")
		Loop
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways ("FDDemonMode")
		FIST A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		FSTS ABC 1
		FIST A 0 A_Lower
		Wait
		
	// Attack
	Fire:
		FSTS ABC 1
		DMNF BC 1
		TNT1 A 0 A_CustomPunch (random (30, 40) * CallACS ("FD_StrengthStatMelee", 10), 1, 0, "FistPuff")
		DMNF DE 1
		DMNF FG 1 A_SetAngle(angle - 1.25)
		TNT1 A 5 A_Refire ("Combo1P2")
		TNT1 A 0 A_Refire ("Combo1P2")
		FSTS CBA 1
		FIST AAA 1
		Goto Ready2
	AltFire:
		FSTS ABC 1
		DMF2 ABC 1
		TNT1 A 0 A_PlaySound ("DemonMode/Altfire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("DemonModeAltfire", 0.0, 0, -5, 2)
		DMF2 DEF 1
		TNT1 A 5 A_Refire ("AltFire2")
		TNT1 A 0 A_Refire ("AltFire2")
		FSTS CBA 1
		FIST AAA 1
		Goto Ready2
	AltFire2:
		DMF2 HIJ 1
		TNT1 A 0 A_PlaySound ("DemonMode/Altfire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("DemonModeAltfire", 0.0, 0, 5, 2)
		DMF2 KLM 1
		TNT1 A 5 A_Refire ("AltFire")
		TNT1 A 0 A_Refire ("AltFire")
		FSTS CBA 1
		FIST AAA 1
		Goto Ready2
		
	// Combos
	Combo1P2:
		DMF1 AB 1
		TNT1 A 0 A_CustomPunch (random (30, 40) * CallACS ("FD_StrengthStatMelee", 15), 1, 0, "FistPuff")
		TNT1 A 0 
		DMF1 CD 1
		DMF1 EF 1 A_SetAngle (angle + 1.25)
		TNT1 A 5 A_Refire ("Combo1Finish")
		TNT1 A 0 A_Refire ("Combo1Finish")
		FSTS CBA 1
		FIST AAA 1
		Goto Ready2
	Combo1Finish:
		TNT1 A 0 A_ChangeVelocity (50.0, 0, 10.0, CVF_RELATIVE)
		DMF1 ABCD 1
		TNT1 A 0 A_CustomPunch (random (40, 50) * CallACS ("FD_StrengthStatMelee", 20), 1, 0, "FistPuff")
		DMF1 DCBA 1
		TNT1 A 5
		FSTS CBA 1
		FIST AAA 1
		Goto Ready2
	}
}