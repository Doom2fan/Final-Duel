Actor DroppedChainsaw : DroppedWeaponBase {
	States {
	Spawn:
		SAWY A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx ("FDChainsaw")
		Stop
	}
}

Actor FDChainsaw : FDWeapon 30163 {
	//$Category Final Duel-Weapons
	//$Title Chainsaw
	//$Sprite SAWYA0
	Weapon.SisterWeapon FDChainsawPowered
	Tag "$TAGCHAINSAW"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.Kickback 0
	Weapon.UpSound "Weapons/sawup"
	Inventory.PickupMessage "$GOTSAW"
	Obituary "$OBSAW"
	+Weapon.MeleeWeapon +Weapon.NoAlert
	States {
	Ready:
		CSAW GFE 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		TNT1 A 0 A_PlaySound ("Weapons/saw2idle", CHAN_WEAPON)
		CSAW AB 2 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedChainsaw", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("FDChainsaw", 1)
		Stop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		CSAW EFG 1
		CSAW A 0 A_Lower
		Wait
	Select:
		CSAW A 0 A_Raise
		Wait
	Fire:
		CSAW C 2 A_Saw ("Weapons/saw2full", "Weapons/saw2hit", 0, "SawPuff")
		CSAW D 2
		TNT1 A 0 A_AlertMonsters
		CSAW D 0 A_ReFire
		Goto Ready2
	Spawn:
		SAWY A -1
		Stop
	}
}

Actor FDChainsawPowered : FDChainsaw {
	Weapon.SisterWeapon FDChainsaw
	+Weapon.Powered_Up
	States {
	Fire:
		CSAW CD 2 A_Saw ("Weapons/saw2full", "Weapons/saw2hit", 0, "SawPuff", 0, 0, 0, 0, 0.5)
		TNT1 A 0 A_AlertMonsters
		CSAW D 0 A_ReFire
		Goto Ready2
	}
}