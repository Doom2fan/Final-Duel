Actor BerettaMode2    : Boolean { }
Actor BerettaBurstSeq : Counter { Inventory.MaxAmount 3 } // Burst fire counter
Actor BerettaTic      : Boolean { }
Actor BerettaUpgrade  : Boolean { }
Actor BerettaUpgrade2 : Boolean { }
Actor Beretta         : FDWeapon {
	Tag "$TAGBERETTA"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 0
	Weapon.AmmoType "AmmoBeretta"
	Weapon.AmmoType2 "Clip2"
	Inventory.RestrictedTo OmegaClass
	Obituary "$OBBERETTA"
	+Weapon.NoAlert
	States {
	Ready:
		BRTS CBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("AmmoBeretta", 1, 1)
		Goto Ready2.NoAmmo
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		BRTA A 1 A_WeaponReady
		Loop
	Ready2.NoAmmo:
		TNT1 A 0 A_JumpIfInventory ("AmmoBeretta", 1, "Ready2")
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		BRTA F 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		BRTA A 5 ACS_NamedExecute ("FD_CannotDropThisWeapon", 0)
		Goto Ready2
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		BRTS ABC 1
		BRTA A 0 A_Lower
		Wait
	Select:
		BRTA A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory ("BerettaMode2", 1, "Fire.Burst")
		TNT1 A 0 A_JumpIfInventory ("BerettaUpgrade2", 1, "Fire.Upgrade")
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		BRTA A 0 A_FireCustomMissile ("PistolTracer", frandom (-1.0, 1.0), 1, 0, -1, 0, frandom (-2.0, 2.0))
		TNT1 A 0 A_FireCustomMissile ("9mmCasingSpawn", 5, 0, 0, -3)
		TNT1 A 0 A_PlaySound ("Beretta/Fire", CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.1, 0, Sin (Pitch) * 0.1, 1)
		BRTA B 1 Bright A_SetPitch (pitch - 0.25, SPF_INTERPOLATE)
		TNT1 A 0 A_SetPitch (pitch - 0.25, SPF_INTERPOLATE)
		BRTA E 1
		TNT1 A 0 A_JumpIfNoAmmo ("Ready2.NoAmmo")
		BRTA DCA 1
		BRTA A 1 A_WeaponReady (WRF_NOBOB|WRF_NOSECONDARY)
		Goto Ready2
	Fire.Upgrade:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		BRTA A 0 A_FireCustomMissile ("PistolTracer", frandom (-1.0, 1.0), 1, 0, -1, 0, frandom (-2.0, 2.0))
		TNT1 A 0 A_FireCustomMissile ("9mmCasingSpawn", 5, 0, 0, -3)
		TNT1 A 0 A_PlaySound ("Beretta/Fire", CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.1, 0, Sin (Pitch) * 0.1, 1)
		BRTA B 1 Bright A_SetPitch (pitch - 0.25, SPF_INTERPOLATE)
		TNT1 A 0 A_SetPitch (pitch - 0.25, SPF_INTERPOLATE)
		BRTA E 1
		TNT1 A 0 A_JumpIfNoAmmo ("Ready2.NoAmmo")
		BRTA DC 1
		BRTA A 1 A_WeaponReady (WRF_NOBOB|WRF_NOSECONDARY)
		Goto Ready2
	Fire.Burst:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		BRTA A 0 A_FireCustomMissile ("PistolTracer", frandom (-3.0, 3.0), 1, 0, -1, 0, frandom (-3.5, 3.5))
		TNT1 A 0 A_FireCustomMissile ("9mmCasingSpawn", 5, 0, 0, -3)
		TNT1 A 0 A_PlaySound ("Beretta/Fire", CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory ("BerettaBurstSeq", 1)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.18, 0, Sin (Pitch) * 0.18, 1)
		BRTA B 1 Bright A_SetPitch (pitch - 0.525, SPF_INTERPOLATE)
		TNT1 A 0 A_SetPitch (pitch - 0.525, SPF_INTERPOLATE)
		BRTA A 1 A_Refire ("Fire.Burst.Hold")
		TNT1 A 0 A_TakeInventory ("BerettaBurstSeq", 3)
		BRTA E 1
		TNT1 A 0 A_JumpIfNoAmmo ("FinishBurst.NoAmmo")
		BRTA DCAA 1
		BRTA AAA 1 A_WeaponReady (WRF_NOBOB|WRF_NOSECONDARY)
		Goto Ready2
	Fire.Burst.Hold:
		TNT1 A 0 A_JumpIfInventory ("BerettaBurstSeq", 3, "FinishBurst")
		TNT1 A 0 A_JumpIfNoAmmo ("FinishBurst.NoAmmo")
		BRTA EDC 1
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		BRTA A 0 A_FireCustomMissile ("PistolTracer", frandom (-3.0, 3.0), 1, 0, -1, 0, frandom (-3.5, 3.5))
		TNT1 A 0 A_FireCustomMissile ("9mmCasingSpawn", 5, 0, 0, -3)
		TNT1 A 0 A_PlaySound ("Beretta/Fire", CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory ("BerettaBurstSeq", 1)
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.18, 0, Sin (Pitch) * 0.18, 1)
		BRTA B 1 Bright A_SetPitch (pitch - 0.525, SPF_INTERPOLATE)
		TNT1 A 0 A_SetPitch (pitch - 0.525, SPF_INTERPOLATE)
		BRTA A 1 A_Refire ("Fire.Burst.Hold")
	FinishBurst:
		BRTA EDCAA 1
		BRTA AAA 1 A_WeaponReady (WRF_NOBOB|WRF_NOSECONDARY)
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_TakeInventory ("BerettaBurstSeq", 3)
		Goto Ready2
	FinishBurst.NoAmmo:
		BRTA E 1
		BRTA FFF 1 A_WeaponReady (WRF_NOBOB|WRF_NOSECONDARY)
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_TakeInventory ("BerettaBurstSeq", 3)
		Goto Ready2
	Altfire:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory ("BerettaUpgrade", 1, 1)
		Goto Ready2
		TNT1 A 0 A_PlaySound ("Weapons/Mode", 5, 1.5)
		TNT1 A 0 A_JumpIfInventory ("BerettaMode2", 1, 3)
		TNT1 A 0 A_GiveInventory ("BerettaMode2", 1)
		TNT1 A 0 A_Print ("- Burstfire mode -")
		Goto Ready2
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_TakeInventory ("BerettaBurstSeq", 3)
		TNT1 A 0 A_TakeInventory ("BerettaMode2", 1)
		TNT1 A 0 A_Print ("- Semiautomatic mode -")
		Goto Ready2
	DryFire:
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_TakeInventory ("BerettaBurstSeq", 3)
		BRTA F 1 A_PlaySound ("Weapons/empty", 7)
		BRTA GH 1
		TNT1 A 0 A_JumpIfInventory ("EnableAutoReload", 1, "Reload")
		Goto Ready2
	Reload:
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("AmmoBeretta", 15, "Ready2")
		TNT1 A 0 A_JumpIfInventory ("Clip2", 1, 1)
		Goto Ready2
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_TakeInventory ("BerettaBurstSeq", 3)
		TNT1 A 0 A_JumpIfInventory ("AmmoBeretta", 1, 1)
		Goto Reload.NoAmmo
		BRR2 A 1
		Goto Reload.Work
	Reload.NoAmmo:
		BRTR A 1
	Reload.Work:
		"####" A 0 A_TakeInventory ("Clip2", 1)
		"####" A 0 A_PlaySound ("Beretta/ClipOut", CHAN_WEAPON)
		"####" A 0 A_FireCustomMissile ("EmptyClipSpawn", 0, 0, 15, 0)
		"####" BCDEFGHHH 1
		"####" A 0 A_PlaySound ("Beretta/ClipIn", CHAN_WEAPON)
		"####" IJKLMNO 2
		BRTR P 3
		BRTR A 0 A_GiveInventory ("AmmoBeretta", 15)
		Goto Ready2
	}
}

Actor AmmoBeretta : AMMO {
	Inventory.MaxAmount 16
	Ammo.BackpackMaxAmount 16
	+IgnoreSkill
}