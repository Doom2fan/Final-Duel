// DECORATE: Alex_Mercer
// Sprites: That_One_Doom_Mod, Minigunner
Actor MCSBFire    : Boolean { }
Actor MCSBAltFire : Boolean { }
Actor MeleeAttack : FDWeapon {
	Tag "$TAGMELEE"
	Obituary "$OBMELEE"
	+Weapon.MeleeWeapon +Weapon.NoAlert +Weapon.NoAutoaim
	States {
	Kicking:
		TNT1 A 0 A_ZoomFactor (1.0)
		TNT1 A 0 A_TakeInventory ("Kicking", 1)
		TNT1 A 0 A_JumpIf (momZ > 0, "AirKick")
		TNT1 A 0 A_JumpIf (momZ < 0, "AirKick")
		TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "BerserkKick")
		KICK BCD 1
		KICK H 4 A_CustomPunch (random (30, 40), 1, 0, "MeleePuff")
		KICK IGFEDCBA 1
		Goto Ready2
	AirKick:
		TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "AirKick.Berserk")
		KICK JKLMN 1
		RIFF A 0 A_CustomPunch (random (50, 60), 1, 0, "MeleePuff")
		KICK OPQRST 2
		Goto Ready2
	Ready:
		FSTS CBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		TNT1 A 0 A_JumpIfInventory ("MCSBFire", 1, "Attack")
		TNT1 A 0 A_JumpIfInventory ("MCSBAltFire", 1, "AltAttack")
		FIST A 1 A_WeaponReady (WRF_NOFIRE)
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		FIST A 5 ACS_NamedExecute ("FD_TryingToDropFists", 0)
		Goto Ready2
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways ("FDMeleeComboSystem", 0, 0, 0, 0)
		FIST A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		FSTS ABC 1
		FIST A 0 A_Lower
		Wait
		
	// Attack
	Fire:
		Goto Ready2
	Attack:
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "Attack.Berserk")
		FIST BC 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * 10, 1, 0, "FistPuff")
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST D 1 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST E 1 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST F 1 A_TakeInventory ("MCSBAltFire", 1)
		FIST GH 1
		FIST AAAA 1 A_JumpIfInventory ("MCSBAltFire", 1, "Combo1P2")
		TNT1 A 0 A_JumpIfInventory ("MCSBAltFire", 1, "Combo1P2")
		Goto Ready2
	AltAttack:
		TNT1 A 0 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "AltAttack.Berserk")
		FIST IJ 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * 10, 1, 0, "FistPuff")
		FIST KLMNOAAA 1
		Goto Ready2
		
	// Combos
	Combo1P2:
		TNT1 A 0 A_TakeInventory ("MCSBAltFire", 1)
		FIST I 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * 15, 1, 0, "FistPuff")
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST K 1 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST M 1 A_TakeInventory ("MCSBAltFire", 1)
		FIST NO 1
		FIST AAAA 1 A_JumpIfInventory ("MCSBFire", 1, "Combo1Finish")
		TNT1 A 0 A_JumpIfInventory ("MCSBFire", 1, "Combo1Finish")
		Goto Ready2
	Combo1Finish:
		TNT1 A 0 A_TakeInventory ("MCSBFire")
		FST1 ABCD 1
		TNT1 A 0 A_CustomPunch (random (4, 5) * 20, 1, 0, "FistPuff")
		FST1 DCBA 1
		FIST AAA 1
		Goto Ready2
		
	// Berserk
	Attack.Berserk:
		FIST BC 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * 30, 1, 0, "FistPuffStrong")
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST D 1 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST E 1 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST F 1 A_TakeInventory ("MCSBAltFire", 1)
		FIST GH 1
		FIST AAA 1 A_JumpIfInventory ("MCSBAltFire", 1, "Combo1P2.Berserk")
		TNT1 A 0 A_JumpIfInventory ("MCSBAltFire", 1, "Combo1P2.Berserk")
		Goto Ready2
	AltAttack.Berserk:
		FIST KLM 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * 30, 1, 0, "FistPuffStrong")
		FIST NOPQRSAAA 1
		Goto Ready2
	BerserkKick:
		KICK BCD 1
		KICK H 4 A_CustomPunch (3 * random (30, 40), 1, 0, "MeleePuffStrong")
		KICK IGFEDCBA 1
		Goto Ready2
	AirKick.Berserk:
		KICK JKLMN 1
		RIFF A 0 A_CustomPunch (3 * random (50, 60), 1, 0, "MeleePuffStrong")
		KICK OPQRST 1
		Goto Ready2
	Combo1P2.Berserk:
		TNT1 A 0 A_TakeInventory ("MCSBAltFire", 1)
		FIST I 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * 45, 1, 0, "FistPuffStrong")
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST K 1 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST M 1 A_TakeInventory ("MCSBAltFire", 1)
		FIST NO 1
		FIST AAA 1 A_JumpIfInventory ("MCSBFire", 1, "Combo1Finish.Berserk")
		TNT1 A 0 A_JumpIfInventory ("MCSBFire", 1, "Combo1Finish.Berserk")
		Goto Ready2
	Combo1Finish.Berserk:
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FST1 ABCD 1
		TNT1 A 0 A_CustomPunch (random (4, 5) * 60, 1, 0, "FistPuffStrong")
		FST1 DCBA 1
		FIST AAA 1
		Goto Ready2
	}
}

Actor FistPuff {
	+NoBlockmap +NoGravity +PuffOnActors +NoExtremeDeath
	AttackSound "Melee/Fists/HitWall"
	States {
	Spawn:
		TNT1 A 0
		Stop
	Melee:
		TNT1 A 0 A_PlaySound ("Melee/Fists/HitWall")
		Stop
	}
}

Actor FistPuffStrong : FistPuff {
	-NoExtremeDeath +ExtremeDeath
	Decal FistScorch
	AttackSound "Melee/Fists/HitWall"
	States {
	Spawn:
		TNT1 A 0
		Stop
	Melee:
		TNT1 A 0 A_PlaySound ("Melee/Fists/HitWall")
		Stop
	}
}