// DECORATE: Alex_Mercer
// Sprites: That_One_Doom_Mod, Minigunner
Actor MCSBFire    : Boolean { }
Actor MCSBAltFire : Boolean { }
Actor MeleeAttack : FDWeapon {
	Tag "$TAGMELEE"
	Obituary "$OBMELEE"
	+Weapon.MeleeWeapon +Weapon.NoAlert +Weapon.NoAutoaim
	States {
	Ready:
		FSTS CBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		TNT1 A 0 A_JumpIfInventory ("MCSBFire", 1, "Attack")
		TNT1 A 0 A_JumpIfInventory ("MCSBAltFire", 1, "AltAttack")
		FIST A 1 A_WeaponReady (WRF_NOFIRE)
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		FIST A 5 ACS_NamedExecute ("FD_TryingToDropFists", 0)
		Goto Ready2
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways ("FDMeleeComboSystem", 0, 0, 0, 0)
		FIST A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		FSTS ABC 1
		FIST A 0 A_Lower
		Wait
		
	// Attack
	Fire:
		Goto Ready2
	Attack:
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "Attack.Berserk")
		FIST BC 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * CallACS ("FD_StrengthStatMelee", 10), 1, 0, "FistPuff")
		FIST DE 1 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_SetAngle(angle - 1.25)
		FIST F 1 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_SetAngle(angle - 1.25)
		FIST G 1 A_TakeInventory ("MCSBAltFire", 1)
		FSTS CBA 1 A_JumpIfInventory ("MCSBAltFire", 1, "Combo1P2")
		FIST AAA 1 A_JumpIfInventory ("MCSBAltFire", 1, "Combo1P2")
		TNT1 A 0 A_JumpIfInventory ("MCSBAltFire", 1, "Combo1P2")
		Goto Ready2
	AltAttack:
		TNT1 A 0 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "AltAttack.Berserk")
		FIST HI 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * CallACS ("FD_StrengthStatMelee", 10), 1, 0, "FistPuff")
		FIST JK 1
		FIST LM 1 A_SetAngle (angle + 1.25)
		FSTS CBA 1
		FIST AAA 1
		Goto Ready2
		
	// Combos
	Combo1P2:
		TNT1 A 0 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "Combo1P2.Berserk")
		FST1 AB 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * CallACS ("FD_StrengthStatMelee", 15), 1, 0, "FistPuff")
		TNT1 A 0 
		FST1 CD 1
		TNT1 A 0 A_SetAngle (angle + 1.25)
		FST1 E 1 A_TakeInventory ("MCSBFire", 1)
		TNT1 A 0 A_SetAngle (angle + 1.25)
		FST1 F 1 A_TakeInventory ("MCSBFire", 1)
		FSTS CBA 1 A_JumpIfInventory ("MCSBFire", 1, "Combo1Finish")
		FIST AAA 1 A_JumpIfInventory ("MCSBFire", 1, "Combo1Finish")
		TNT1 A 0 A_JumpIfInventory ("MCSBFire", 1, "Combo1Finish")
		Goto Ready2
	Combo1Finish:
		TNT1 A 0 A_TakeInventory ("MCSBFire")
		TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "Combo1Finish.Berserk")
		TNT1 A 0 A_ChangeVelocity (50.0, 0, 10.0, CVF_RELATIVE)
		FST1 ABCD 1
		TNT1 A 0 A_CustomPunch (random (4, 5) * CallACS ("FD_StrengthStatMelee", 20), 1, 0, "FistPuff")
		FST1 DCBA 1
		FSTS CBA 1
		FIST AAA 1
		Goto Ready2
		
	// Berserk
	Attack.Berserk:
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FIST BC 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * CallACS ("FD_StrengthStatMelee", 30), 1, 0, "FistPuff")
		FIST DE 1 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_SetAngle(angle - 1.25)
		FIST F 1 A_TakeInventory ("MCSBAltFire", 1)
		TNT1 A 0 A_SetAngle(angle - 1.25)
		FIST G 1 A_TakeInventory ("MCSBAltFire", 1)
		FSTS CBA 1 A_JumpIfInventory ("MCSBAltFire", 1, "Combo1P2")
		FIST AAA 1 A_JumpIfInventory ("MCSBAltFire", 1, "Combo1P2")
		TNT1 A 0 A_JumpIfInventory ("MCSBAltFire", 1, "Combo1P2")
		Goto Ready2
	AltAttack.Berserk:
		TNT1 A 0 A_TakeInventory ("MCSBAltFire", 1)
		FIST HI 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * CallACS ("FD_StrengthStatMelee", 30), 1, 0, "FistPuff")
		FIST JK 1
		FIST LM 1 A_SetAngle (angle + 1.25)
		FSTS CBA 1
		FIST AAA 1
		Goto Ready2
	Combo1P2.Berserk:
		TNT1 A 0 A_TakeInventory ("MCSBAltFire", 1)
		FST1 AB 1
		TNT1 A 0 A_CustomPunch (random (3, 4) * CallACS ("FD_StrengthStatMelee", 45), 1, 0, "FistPuff")
		TNT1 A 0 
		FST1 CD 1
		TNT1 A 0 A_SetAngle (angle + 1.25)
		FST1 E 1 A_TakeInventory ("MCSBFire", 1)
		TNT1 A 0 A_SetAngle (angle + 1.25)
		FST1 F 1 A_TakeInventory ("MCSBFire", 1)
		FSTS CBA 1 A_JumpIfInventory ("MCSBFire", 1, "Combo1Finish")
		FIST AAA 1 A_JumpIfInventory ("MCSBFire", 1, "Combo1Finish")
		TNT1 A 0 A_JumpIfInventory ("MCSBFire", 1, "Combo1Finish")
		Goto Ready2
	Combo1Finish.Berserk:
		TNT1 A 0 A_TakeInventory ("MCSBFire", 1)
		FST1 ABCD 1
		TNT1 A 0 A_CustomPunch (random (4, 5) * CallACS ("FD_StrengthStatMelee", 60), 1, 0, "FistPuffStrong")
		FST1 DCBA 1
		FIST AAA 1
		Goto Ready
	}
}

Actor FistPuff {
	+NoBlockmap +NoGravity +PuffOnActors +NoExtremeDeath
	AttackSound "Melee/Fists/HitWall"
	States {
	Spawn:
		TNT1 A 0
		Stop
	Melee:
		TNT1 A 0 A_PlaySound ("Melee/Fists/HitWall")
		Stop
	}
}

Actor FistPuffStrong : FistPuff {
	-NoExtremeDeath +ExtremeDeath
	Decal FistScorch
	AttackSound "Melee/Fists/HitWall"
	States {
	Spawn:
		TNT1 A 0
		Stop
	Melee:
		TNT1 A 0 A_PlaySound ("Melee/Fists/HitWall")
		Stop
	}
}