/*
DECORATE: Alex_Mercer
Sprites: ???
Sounds: Langerium (a Freesound user), Sound Library
*/
Actor DroppedSword : DroppedWeaponBase {
	States {
	Spawn:
		SWRD Z 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx ("Sword")
		Stop
	}
}

Actor Sword : FDWeapon 30072 {
	//$Category Final Duel-Weapons
	//$Title Sword
	//$Sprite SWRDZ0
	Tag "$TAGSWORD"
	Obituary "$OBSWORD"
	Inventory.PickupMessage "$GOTSWORD"
	Inventory.PickupSound ""
	+Weapon.MeleeWeapon +Weapon.NoAlert +Weapon.NoAutoAim
	States {
	Ready:
		SRDS CBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		SWRD A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedSword", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("Sword", 1)
		Stop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		SRDS ABC 1
		SRDS C 0 A_Lower
		Wait
	Select:
		SWRD A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory ("PowerFDStrength", 1, "Fire.Berserk")
		TNT1 A 0 A_PlaySound ("Melee/Sword/Swing", CHAN_WEAPON)
		SWRD BCDE 1
		TNT1 A 0 A_CustomPunch (CallACS ("FD_StrengthStatSword", random (30, 50)), 1, CPF_NORANDOMPUFFZ, "SwordPuff")
		SWRD FGHIJK 1
		SRDS CBA 1
		SWRD A 1
		Goto Ready2
	Fire.Berserk:
		TNT1 A 0 A_PlaySound ("Melee/Sword/Swing", CHAN_WEAPON)
		SWRD BDE 1
		TNT1 A 0 A_CustomPunch (6 * CallACS ("FD_StrengthStatSword", random (30, 50)), 1, CPF_NORANDOMPUFFZ, "BerserkSwordPuff")
		SWRD GHJK 1
		SRDS CBA 1
		SWRD A 1
		Goto Ready2
	Spawn:
		SWRD Z -1
		Stop
	}
}

Actor SwordPuff {
	+NoGravity +ForceXYBillboard +NoExtremeDeath
	RenderStyle Add
	Scale 0.1
	Alpha 0.8
	AttackSound "Melee/Sword/HitWall"
	States {
	Spawn:
		TNT1 ABCD 1
		Stop
	Melee:
		TNT1 A 0 A_PlaySound ("Melee/Sword/HitThing")
	}
}

Actor BerserkSwordPuff : SwordPuff { DamageType Saw }