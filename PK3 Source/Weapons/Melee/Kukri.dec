/*
Sprites: Eriance, Mechadon, Uboa, Alex_Mercer, ID Software, Banjo Software, Blox
DECORATE: Alex_Mercer, torridGristle
Sounds: WildWeasel, Uboa, Mechadon
*/
// By Alex_Mercer
actor Kukri : FDWeapon
{
	Tag "$TAGKUKRI"
	Weapon.Kickback 100
	Obituary "$OBKUKRI"
	+WEAPON.MELEEWEAPON
    +WEAPON.NOALERT
    +WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+AMMO_OPTIONAL
	States
	{
	Ready:
		KNFS EDCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Taunt")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		KNF1 A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(0,"DeselectDEAD")
		KNFS ABCDE 1
		KNF1 A 0 A_Lower
		Wait
	Select:
		KNF1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerFDStrength",1,"BerserkFire")
		KNF1 B 1 A_CustomPunch (random(10,15), 1, CPF_NORANDOMPUFFZ, "KnifePuff")
		KNF1 CDEFGHIJKLM 1
		TNT1 A 0 A_Refire("Fire2")
		Goto Ready2
	Fire2:
		TNT1 A 0 A_JumpIfInventory("PowerFDStrength",1,"BerserkFire2")
		KNF2 A 2 A_CustomPunch (random(10,15), 1, CPF_NORANDOMPUFFZ, "KnifePuff")
		KNF2 BCDEFGH 2
		TNT1 A 0 A_Refire("Fire")
		Goto Ready2
	BerserkFire:
		TNT1 A 0 A_JumpIfInventory("PowerFDStrength",1,1)
		Goto Fire
		KNF1 B 1 A_CustomPunch (6 * random(10,15), 1, CPF_NORANDOMPUFFZ, "BerserkKnifePuff")
		KNF1 DFHJL 1
		TNT1 A 0 A_Refire("BerserkFire2")
		Goto Ready2
	BerserkFire2:
		TNT1 A 0 A_JumpIfInventory("PowerFDStrength",1,1)
		Goto Fire2
		KNF2 A 1 A_CustomPunch (6 * random(10,15), 1, CPF_NORANDOMPUFFZ, "BerserkKnifePuff")
		KNF2 BCDEFGH 1
		TNT1 A 0 A_Refire("BerserkFire")
		Goto Ready2
	}
}

Actor TauntAlert : FastProjectile
{
	Speed 1000
	Projectile
	Damage 0
	Radius 1
	Height 1
	DamageType Taunt
	+FORCEPAIN
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Death:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 1 A_Explode(0,0)
		Stop
	}
}
// By Mechadon,Edited by Alex_Mercer
ACTOR KnifePuff
{
  +NOGRAVITY
  +FORCEXYBILLBOARD
  -DONTSPLASH
  -ALLOWPARTICLES
  +NOEXTREMEDEATH
  renderstyle Add
  scale 0.1
  alpha 0.8
  AttackSound "Melee/Knife/HitWall"
  ActiveSound "Melee/Knife/Miss"
  States
  {
  Spawn:
	TNT1 ABCD 1 bright
	stop
  Melee:
	TNT1 A 0 A_PlaySound("Melee/Knife/HitThing")
  }
}  

Actor BerserkKnifePuff : KnifePuff { -NOEXTREMEDEATH }