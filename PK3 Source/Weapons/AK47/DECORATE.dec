// Spawned AK47
Actor SpawnedAK47 : BaseSpawnedWeapon 30004 {
	Inventory.PickupMessage "$GOTAK47"
	States {
	Spawn:
		AK47 Z -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory ("AK47", 0, "GrabAmmo")
		TNT1 A 0 A_TakeInventory ("AmmoAK47", 999999999)
		TNT1 A 0 A_GiveInventory ("AK47", 1)
		TNT1 A 0 A_GiveInventory ("AmmoAK47", 999999999)
		TNT1 A 0 A_GiveInventory ("RifleMag", 1)
		Stop
	GrabAmmo:
		TNT1 A 0 A_JumpIfInventory ("RifleMag", 0, "Failed")
		TNT1 A 0 A_GiveInventory ("RifleMag", 2)
		Stop
	}
}

Actor DroppedAK47 : DroppedWeaponBase {
	States {
	Spawn:
		AK47 Z 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx ("AK47")
		Stop
	}
}
Actor AK47Zoomed : Boolean { }
Actor AK47 : FDWeapon {
	//$Category Final Duel-Weapons
	//$Title ???
	//$Sprite AK47Z0
	Tag "$TAGAK47"
	Inventory.RestrictedTo MarineClass
	Inventory.PickupSound "misc/gunpickup"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType "AmmoAK47"
	Weapon.AmmoType2 "RifleMag"
	Inventory.PickupMessage "$GOTAK47"
	Obituary "$OBAK47"
	+Weapon.NoAlert +Inventory.RestrictAbsolutely
	States {
	Ready:
		AKGS CBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("AK47Zoomed", 1, "Ready.Zoomed")
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		AK47 AA 1 A_WeaponReady (WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory ("PressingZoom", 1)
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedAK47", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("AK47", 1)
		Stop
	Deselect:
		SNPD A 0 A_JumpIfInventory ("AK47Zoomed", 1, "Deselect.Unzoom")
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		AKGS ABC 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory ("AK47Zoomed", 1, "Fire.Zoomed")
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound ("AK-47/fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 3, -1)
		TNT1 A 0 A_FireCustomMissile ("AK47Tracer", frandom (-1.3, 1.3), 1, 0, 0, 0, frandom (-1.6, 1.6))
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.2, 0, Sin (Pitch) * 0.2, 1)
		AK47 BB 1 Bright A_SetPitch (pitch - 0.25, SPF_INTERPOLATE)
		AK47 A 2
		AK47 A 0 A_ReFire ("Fire.FullAuto")
		Goto Ready2
	Fire.FullAuto:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound ("AK-47/fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 3, -1)
		TNT1 A 0 A_FireCustomMissile ("AK47Tracer", frandom (-1.6, 1.6), 1, 0, 0, 0, frandom (-1.9, 1.9))
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.2, 0, Sin (Pitch) * 0.2, 1)
		AK47 BB 1 Bright A_SetPitch (pitch - 0.25, SPF_INTERPOLATE)
		AK47 A 3
		AK47 A 0 A_ReFire ("Fire.FullAuto2")
		TNT1 A 0 A_ClearRefire
		Goto Ready2
	Fire.FullAuto2:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound ("AK-47/fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 3, -1)
		TNT1 A 0 A_FireCustomMissile ("AK47Tracer", frandom (-1.6, 1.6), 1, 0, 0, 0, frandom (-1.9, 1.9))
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.2, 0, Sin (Pitch) * 0.2, 1)
		AK47 BB 1 Bright A_SetPitch (pitch - 0.25, SPF_INTERPOLATE)
		AK47 A 2
		AK47 A 0 A_ReFire ("Fire.FullAuto")
		TNT1 A 0 A_ClearRefire
		Goto Ready2
	DryFire:
		TNT1 A 0 A_ClearRefire
		AK47 A 10 A_PlaySound ("Weapons/empty", 7)
		TNT1 A 0 A_JumpIfInventory ("EnableAutoReload", 1, "Reload")
		Goto Ready2
	Reload:
		TNT1 A 0 A_JumpIfInventory ("AK47Zoomed", 1, "Reload.Unzoom")
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory ("AmmoAK47", 30, "Ready2")
		TNT1 A 0 A_JumpIfInventory ("RifleMag", 1, 1)
		Goto Ready2
		AKGS ABC 1
		TNT1 A 0 A_FireCustomMissile ("EmptyClipSpawn", 0, 0, 0, 0)
		TNT1 A 0 A_PlaySound ("AK-47/MagOut", CHAN_WEAPON)
		TNT1 A 30
		TNT1 A 6 A_PlaySound ("AK-47/MagIn", CHAN_WEAPON)
		AKGS CBA 1
		TNT1 A 0 A_TakeInventory ("AmmoAK47", 30)
		TNT1 A 0 A_GiveInventory ("AmmoAK47", 30)
		Goto Ready2
		
	// Zoomed
	
	Zoom:
		TNT1 A 0 A_JumpIfInventory ("PressingZoom", 1, "Ready2")
		TNT1 A 0 A_GiveInventory ("PressingZoom", 1)
		TNT1 A 0 A_JumpIfInventory ("AK47Zoomed", 1, "Unzoom")
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 65)
		TNT1 A 0 A_GiveInventory ("AK47Zoomed", 1)
		TNT1 A 0 A_SetCrosshair (99)
		AK47 ACDE 1
		TNT1 A 0 A_ZoomFactor (2.0)
		Goto Ready.Zoomed
	Unzoom:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 100)
		TNT1 A 0 A_TakeInventory ("AK47Zoomed", 1)
		TNT1 A 0 A_SetCrosshair (0)
		AK47 E 1 A_ZoomFactor (1.0)
		AK47 DCA 1
		Goto Ready2
	Deselect.Unzoom:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 100)
		TNT1 A 0 A_TakeInventory ("AK47Zoomed", 1)
		TNT1 A 0 A_SetCrosshair (0)
		AK47 E 1 A_ZoomFactor (1.0)
		AK47 DCA 1
		Goto Deselect
		
	Ready.Zoomed:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		AK47 EE 1 A_WeaponReady (WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory ("PressingZoom", 1)
		Loop
	Fire.Zoomed:
		TNT1 A 0 A_JumpIfNoAmmo ("DryFire.Zoomed")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound ("AK-47/fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile ("RifleCasingSpawn", 5, 0, 8, -9)
		TNT1 A 0 A_FireCustomMissile ("AK47Tracer", frandom (-1.0, 1.0), 1, 0, 0, 0, frandom (-1.3, 1.3))
		TNT1 A 0 A_ChangeVelocity (Cos (Pitch) * -0.1, 0, Sin (Pitch) * 0.1, 1)
		AK47 FG 1 Bright A_SetPitch (pitch - 0.175, SPF_INTERPOLATE)
		AK47 E 3
		Goto Ready.Zoomed
	DryFire.Zoomed:
		TNT1 A 0 A_ClearRefire
		AK47 E 10 A_PlaySound ("Weapons/empty", 7)
		TNT1 A 0 A_JumpIfInventory ("EnableAutoReload", 1, "Reload.Unzoom")
		Goto Ready.Zoomed
	Reload.Unzoom:
		TNT1 A 0 ACS_NamedExecute ("PlayerSpeed", 0, 100)
		TNT1 A 0 A_TakeInventory ("AK47Zoomed", 1)
		TNT1 A 0 A_SetCrosshair (0)
		AK47 E 1 A_ZoomFactor (1.0)
		AK47 DCA 1
		Goto Reload
	Spawn:
		AK47 Z -1
		Stop
	}
}

Actor AmmoAK47: AMMO {
	Inventory.MaxAmount 30
	Ammo.BackpackMaxAmount 30
	+IGNORESKILL
}