actor AK47 : FDWeapon 30004
{
	//$Category Final Duel-Weapons
	//$Sprite AK47Z0
	//$Title AK-47
	Tag "$TAGAK47"
	Inventory.RestrictedTo MarineClass
	Inventory.PickupSound "misc/gunpickup"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType "AmmoAK47"
	Weapon.AmmoType2 "Clip2"
	+WEAPON.NOALERT
	+WEAPON.NOAUTOFIRE
    +INVENTORY.RESTRICTABSOLUTELY
	Inventory.PickupMessage "$GOTAK47"
	Obituary "$OBAK47"
	+AMMO_OPTIONAL
	States
	{
	Ready:
		AKGS FEDCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		AK47 A 1 A_WeaponReady
		Goto Ready2
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(0,"DeselectDEAD")
		AKGS ABCDEF 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("AK-47/fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-4)
		TNT1 A 0 A_FireCustomMissile("AK47Tracer", frandom(-1.5,1.5), 1, 2, 3, 0, frandom(-1.75,1.75))
		TNT1 A 0 A_Recoil(cos(pitch) * 0.2)
		AK47 BB 1 A_SetPitch(pitch - 0.25)
		AK47 A 2 Bright
		AK47 A 0 A_ReFire("Fire.FullAuto")
		Goto Ready2
	Fire.FullAuto:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("AK-47/fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("RifleCasingSpawn",5,0,8,-4)
		TNT1 A 0 A_FireCustomMissile("AK47Tracer", frandom(-3.0,3.0), 1, 2, 3, 0, frandom(-3.5,3.5))
		TNT1 A 0 A_Recoil(cos(pitch) * 0.2)
		AK47 BB 1 A_SetPitch(pitch - 0.25)
		AK47 A 2 Bright
		AK47 A 0 A_ReFire("Fire.FullAuto")
		Goto Ready2
	DryFire:
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_PlaySound("weapons/empty")
		TNT1 A 0 A_JumpIfInventory("EnableAutoReload",1,"Reload")
		Goto Ready2
    Reload:
		TNT1 A 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("AmmoAK47",30,"Ready2")
        TNT1 A 0 A_JumpIfInventory("Clip2",1,1)
        Goto Ready2
		AKGR ABC 2
		TNT1 A 0 A_TakeInventory("AmmoAK47",30)
		TNT1 A 0 A_PlaySound("AK-47/MagOut",CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",0,0,0,0)
		AKGR DEFGGGGGGH 2
		TNT1 A 0 A_GiveInventory("AmmoAK47",30)
		TNT1 A 0 A_TakeInventory("Clip2",1)
		TNT1 A 0 A_PlaySound("AK-47/MagIn",CHAN_WEAPON)
		AKGR IJKBA 2
		Goto Ready2
	Spawn:
		AK47 Z -1
		Stop
	}
}
ACTOR AmmoAK47: AMMO
{
	Inventory.MaxAmount 30
	Ammo.BackpackMaxAmount 30
	+IGNORESKILL
}