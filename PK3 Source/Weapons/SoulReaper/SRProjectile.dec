actor SRLaser
{
    DeathSound "SoulReaper/Explode"
	Decal Scorch
	Radius 2
	Height 2
	Speed 60
	Projectile
	Scale 0.1
	Damage 50
	+RANDOMIZE
	+DONTSPLASH
	RenderStyle Add
	Obituary "$OBSOULREAPER"
	MissileHeight 0
	DamageType Extreme
    States
    {
    Spawn:
		L2NR A 1 bright A_SpawnItemEx("SRLaserTrail",-5,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		L2NR A 1 bright A_SpawnItemEx("SRLaserTrail",-10,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		L2NR A 1 bright A_SpawnItemEx("SRLaserTrail",-15,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		Loop
    Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Explode(64,64)
		TNT1 A 1 A_SpawnItem("SRLaserExplosion",0,0,0)
		Stop
    }
}

actor SRLaser2 : SRLaser
{
	Damage 25
	Speed 80
	States
	{
	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Explode(32,32)
		TNT1 A 1 A_SpawnItem("SRLaserExplosion2",0,0,0)
		Stop
	}
}

Actor SRLaserExplosion2 : DebrisGeneral
{
	States
	{
	Spawn:
		TNT1 AAA 0 A_CustomMissile("SmokingPiece",0,0,random(-360,360),2,random(-90,90))
		TNT1 A 0 bright A_SpawnItem("SRLaserShockwave")
		stop
	}
}

Actor SRLaserExplosion : DebrisGeneral
{
	States
	{
	Spawn:
		TNT1 AAAAAA 0 A_CustomMissile("SmokingPiece",0,0,random(-360,360),2,random(-90,90))
		TNT1 A 0 bright A_SpawnItem("SRLaserShockwave")
		stop
	}
}

Actor SRLaserShockwave
{
	Translation "192:207=176:191", "240:247=184:191"
	RenderStyle ADD
	Alpha 1.0
	+FORCEXYBILLBOARD
	+NOGRAVITY
	States
	{
	Spawn:
		SSHK ACEGIKMOQR 2 Light(SRLASER)
		Stop
	}
}

// Trail

/*
PILLOW'S SHAMELESSLY BASED ON KEKSDOSE'S PARTICLE EFFECT BUILDING METHOD!

Use as you wish, as long as you credit the fellas below.

Coded by PillowBlaster, graphics by KeksDose and DukeNukem.
*/

ACTOR SRLaserTrail : TwerpmeisterAvenue
{
	States
	{
	Spawn:
		TNT1 AA 0 A_SpawnItemEx("SRLaser_Smoke2",0,random(3,-3),random(3,-3),frandom(-0.25,-0.125),frandom(0.1,0.05),frandom(0.1,0.05),random(135,225),SXF_CLIENTSIDE,20)
		TNT1 A 0 A_SpawnItemEx("SRLaser_Flame2",0,random(3,-3),random(3,-3),frandom(-0.25,-0.125),frandom(0.1,0.05),frandom(0.1,0.05),random(135,225),SXF_CLIENTSIDE,20)	
		TNT1 A 1 A_SpawnItemEx("SRLaser_FlightFlare",0,random(3,-3),random(3,-3),frandom(-0.25,-0.125),frandom(0.1,0.05),frandom(0.1,0.05),random(135,225),SXF_CLIENTSIDE,20)
		Stop
	}
}

Actor SRLaser_Flare : RagingBattleHamster
{
	Scale 0.075
	Alpha 0.9
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(256,"SprA","SprB")
	SprA:
		SRP1 A 0 A_Jump(256,"Main")
	SprB:
		SRP1 B 0 A_Jump(256,"Main")
	Main:
		"####" "#" 0 A_FadeOut(frandom(0.05,0.025))
		"####" "#" 1 Bright A_SetScale(ScaleX + frandom(0.02,0.03))
		Loop
	}
}

Actor SRLaser_Smoke : RagingBattleHamster
{
	Scale 0.0
	Alpha 0.0
	Renderstyle Translucent
	States
	{
	Spawn:
		TNT1 AA 0 A_Jump(256,"SprA","SprB","SprC","SprD")
	SprA:
		SRP2 A 0 A_Jump(256,"Scaling")
	SprB:
		SRP2 B 0 A_Jump(256,"Scaling")
	SprC:
		SRP2 C 0 A_Jump(256,"Scaling")
	SprD:
		SRP2 D 0 A_Jump(256,"Scaling")
	Scaling: //Why so derplicated? Because smoke is one hell of an ass to make look good, when mixed with rest of stuff.
		"####" "#" 0 A_SetScale(frandom(0.05,0.075))
	FadingIn:
		"####" "#" 0 A_ChangeVelocity(random(-0.02,0.02),random(-0.02,0.02),random(0.0,0.05),CVF_RELATIVE)
		"####" "#" 0 A_FadeIn(frandom(0.02,0.04))
		"####" "#" 0 A_SetScale(ScaleX + frandom(0.01,0.015))
		"####" "#" 1 A_JumpIf(Alpha>=0.5,"Main")
		Loop
	Main:
		"####" "#" 0 A_ChangeVelocity(random(-0.02,0.02),random(-0.02,0.02),random(0.0,0.05),CVF_RELATIVE)
		"####" "#" 0 A_FadeOut(frandom(0.05,0.08))
		"####" "#" 1 A_SetScale(ScaleX + frandom(0.01,0.015))
		Loop
	}
}

Actor SRLaser_Flame : RagingBattleHamster
{
	Scale 0.025 //0.15
	States
	{
	Spawn:
		TNT1 AA 0
		TNT1 A 0 A_FadeOut(frandom(0.1,0.0))
		TNT1 A 0 A_Jump(256,"SprA","SprB","SprC","SprD","SprE","SprF","SprG","SprH","SprI","SprJ","SprK","SprL")
	SprA:
		SRP3 A 0 A_Jump(256,"Main")
	SprB:
		SRP3 B 0 A_Jump(256,"Main")
	SprC:
		SRP3 C 0 A_Jump(256,"Main")
	SprD:
		SRP3 D 0 A_Jump(256,"Main")
	SprE:
		SRP3 E 0 A_Jump(256,"Main")
	SprF:
		SRP3 F 0 A_Jump(256,"Main")
	SprG:
		SRP3 G 0 A_Jump(256,"Main")
	SprH:
		SRP3 H 0 A_Jump(256,"Main")
	SprI:
		SRP3 I 0 A_Jump(256,"Main")
	SprJ:
		SRP3 J 0 A_Jump(256,"Main")
	SprK:
		SRP3 K 0 A_Jump(256,"Main")
	SprL:
		SRP3 L 0 A_Jump(256,"Main")
	Main:
		"####" "#" 0 A_SetScale(ScaleX + frandom(0.01,0.02))
		"####" "#" random(1,2) Bright A_FadeOut(frandom(0.05,0.075))
		Loop
	}
}

Actor SRLaser_FlightFlare : SRLaser_Flare
{
	Scale 0.0375
	Alpha 0.99
	States
	{
	Main:
		"####" "#" 1 Bright A_SetScale(ScaleX-random(0.001,0.003))
		"####" "#" 0 A_FadeOut(frandom(0.2,0.4))
		"####" "#" 0 A_JumpIf(ScaleX <= 0, "NULL")
		Loop
	}
}

Actor SRLaser_Flame2 : SRLaser_Flame
{
	Scale 0.04
	States
	{
	Main:
		"####" "#" 0 A_SetScale(ScaleX + frandom(0.005,0.003))
		"####" "#" 1 Bright A_FadeOut(frandom(0.09,0.08))
		Loop
	}
}

Actor SRLaser_Smoke2 : SRLaser_Smoke
{
	States
	{
	Scaling:
		"####" "#" 0 A_SetScale(frandom(0.0075,0.008))
	FadingIn:
		"####" "#" 0 A_ChangeVelocity(random(-0.02,0.02),random(-0.02,0.02),random(0.0,0.05),CVF_RELATIVE)
		"####" "#" 0 A_FadeIn(frandom(0.06,0.04))
		"####" "#" 0 A_SetScale(ScaleX + frandom(0.001,0.0015))
		"####" "#" 1 A_JumpIf(Alpha>=0.4,"Main")
		Loop
	Main:
		"####" "#" 0 A_ChangeVelocity(random(-0.02,0.02),random(-0.02,0.02),random(0.0,0.05),CVF_RELATIVE)
		"####" "#" 0 A_FadeOut(frandom(0.03,0.04))
		"####" "#" 1 A_SetScale(ScaleX + frandom(0.005,0.003))
		Loop
	}
}