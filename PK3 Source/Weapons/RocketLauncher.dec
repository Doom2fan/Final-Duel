// Credits:
// DECORATE: Alex_Mercer, parts from zandronum.pk3
// Sprites: zrrion the insect, ID Software, Midway, Blox, Freedoom, Z86, Alex_Mercer, Xim, torridGristle
// Sounds: Beautiful Doom
Actor DroppedRocketLauncher : DroppedWeaponBase {
	States {
	Spawn:
		LAUN A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx ("FDRocketLauncher")
		Stop
	}
}

Actor AmmoRocketLauncher : AMMO {
	Inventory.MaxAmount 6
	Ammo.BackpackMaxAmount 6
	+IgnoreSkill
}

Actor RLUpgrade : Boolean { }
Actor FDRocketLauncher : FDWeapon 30057 {
	//$Category Final Duel-Weapons
	//$Title Rocket Launcher
	//$Sprite LAUNA0
	Tag "$TAGRLAUNCHER"
	Game Doom
	Weapon.AmmoUse 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoType "AmmoRocketLauncher"
	Weapon.AmmoType2 "RocketAmmo2"
	+Weapon.NoAutoFire +Ammo_Optional +Alt_Ammo_Optional +NoAlert
	Weapon.SisterWeapon FDRocketLauncherPowered
	Weapon.UpSound "RocketLauncher/Draw"
	Inventory.PickupMessage "$GOTLAUNCHER"
	States {
	Ready:
		MSGS EDCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory ("DropDatWeapon", 1, "DropWeapon")
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, "Reload")
		TNT1 A 0 A_JumpIfInventory ("Kicking", 1, "Kicking")
		TNT1 A 0 A_JumpIfInventory ("Taunting", 1, "Taunt")
		MISG A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory ("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile ("DroppedRocketLauncher", 0, 0, 0)
		TNT1 A 0 A_TakeInventory ("FDRocketLauncher", 1)
		Stop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower (0, "DeselectDEAD")
		MSGS ABCDE 1
		MISG A 0 A_Lower
		Wait
	Select:
		MISG A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory ("AmmoRocketLauncher", 1, 1)
        Goto DryFire
		MISG B 0 A_PlaySound ("RocketLauncher/Fire", CHAN_WEAPON)
		MISG B 0 A_FireCustomMissile ("FDRocket")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory ("AmmoRocketLauncher", 1, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_Recoil (cos (pitch) * 5.0)
		MISG BBB 1 Bright A_SetPitch (pitch - 3.3333333333333333333333333333333)
		MISG CDE 3 Bright
		MISG F 2
		MISG AGHIAA 2
		MISG B 0 A_ReFire
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory ("RLUpgrade", 1, 1)
		Goto Ready2
		TNT1 A 0 A_JumpIfInventory ("AmmoRocketLauncher", 1, 1)
        Goto DryFire
		TNT1 A 0 A_JumpIfTargetInLOS ("TrueAltFire", 1.0, JLOSF_PROJECTILE)
		MISG A 1
		TNT1 A 0 A_Refire
	    Goto Ready2
	TrueAltFire:
		MISG B 0 A_PlaySound ("RocketLauncher/Fire", CHAN_WEAPON)
		MISG B 0 A_FireCustomMissile ("FDRocketSeeker")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory ("AmmoRocketLauncher", 1, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_Recoil (cos (pitch) * 5.0)
		MISG BBB 1 Bright A_SetPitch (pitch - 3.3333333333333333333333333333333)
		MISG CDE 3 Bright
		MISG F 2
		MISG AGHIAA 2
		MISG B 0 A_ReFire
		Goto Ready2
	DryFire:
		MISG A 0 A_PlaySound ("Weapons/empty", 7)
		MISG A 0 A_JumpIfInventory ("EnableAutoReload", 1, "Reload.Dry")
		Goto Ready2
	Reload.Dry:
		TNT1 A 0 A_GiveInventory ("Reloading", 1)
	Reload:
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, 1)
		Goto Reload.Fail
		TNT1 A 0 A_JumpIfInventory ("RocketAmmo2", 1, 1)
        Goto Reload.Fail
		TNT1 A 0 A_JumpIfInventory ("AmmoRocketLauncher", 6, "Reload.Fail")
		MISG J 3
		MISG K 3 A_PlaySound ("RocketLauncher/MagOut", CHAN_WEAPON)
		MISG LMNOP 2
	Reloading:
		TNT1 A 0 A_JumpIfInventory ("Reloading", 1, 1)
		Goto Reload.Done
		TNT1 A 0 A_JumpIfInventory ("AmmoRocketLauncher", 6, "Reload.Done")
		TNT1 A 0 A_JumpIfInventory ("RocketAmmo2", 1, 1)
		Goto Reload.Done
		MISG P 7
		TNT1 A 0 A_GiveInventory ("AmmoRocketLauncher", 1)
		TNT1 A 0 A_TakeInventory ("RocketAmmo2", 1)
		TNT1 A 0 A_PlaySound ("RocketLauncher/Load", 5)
		MISG P 7
		Loop
	Reload.Fail:
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		Goto Ready2
	Reload.Done:
		TNT1 A 0 A_TakeInventory ("Reloading", 1)
		MISG PONML 2
		TNT1 A 0 A_PlaySound ("RocketLauncher/MagIn", CHAN_WEAPON)
		MISG KJ 3
		MISG A 3
		Goto Ready2
	Spawn:
		LAUN A -1
		Stop
	}
}

Actor FDRocket Replaces Rocket {
	Radius 11
	Height 8
	Speed 30
	Damage 45
	Projectile
	+Randomize +DontSplash
	DeathSound "RocketLauncher/Explosion"
	Obituary "$OB_MPROCKET"
	SelfObituary "$OB_ROCKETSELF"
	DamageType Rocket
	States {
	Spawn:
		MISL A 2 Bright Light (ROCKET) A_CustomMissile ("Rocket1Trail", random (-2, 2), random (-2, 2))
		Loop
	Death:
		//TNT1 A 0 A_CheckFloor ("SpawnFire")
	XDeath:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_ChangeFlag ("ForceXYBillboard",1)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("FDShrapnel", random (0, 16), random (-8, 8), random (-360, 360), 2, random (10, 70))
		TNT1 A 0 A_Explode (224, 224)
		XPL1 ABCEDFGHIJKLMNOPQRSTUVWXYZ 1 Bright Light (ROCKET_X1)
		XPL2 ABCEDFGHIJK 1 Bright Light (ROCKET_X1)
		XPL2 LMNOPQRSTU 1 Bright Light (ROCKET_X1)
		stop
	SpawnFire:
		//TNT1 A 0 A_SpawnItemEx ("FDRocketFire")
		Goto XDeath
	}
}

Actor FDRocketSeeker : FDRocket {
	+SeekerMissile +ScreenSeeker
	States {
	Spawn:
		TNT1 A 0 A_SeekerMissile (5, 9, SMF_LOOK|SMF_PRECISE|SMF_CURSPEED, 192, 5)
		MISL A 2 Bright Light (ROCKET) A_CustomMissile ("Rocket1Trail", random (-2, 2), random (-2, 2))
		Loop
	}
}

Actor FDRocketLauncherPowered : FDRocketLauncher {
	Weapon.SisterWeapon FDRocketLauncher
	+Powered_Up
	States {
	Fire:
		TNT1 A 0 A_JumpIfInventory ("AmmoRocketLauncher", 1, 1)
        Goto DryFire
		MISG B 0 A_PlaySound ("RocketLauncher/Fire", CHAN_WEAPON)
		MISG B 0 A_FireCustomMissile ("FDRocketPowered", -5, 0)
		MISG B 0 A_FireCustomMissile ("FDRocketPowered", -2.5, 0)
		MISG B 0 A_FireCustomMissile ("FDRocketPowered")
		MISG B 0 A_FireCustomMissile ("FDRocketPowered", 2.5, 0)
		MISG B 0 A_FireCustomMissile ("FDRocketPowered", 5, 0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory ("AmmoRocketLauncher", 1, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_Recoil (cos (pitch) * 5.8)
		MISG BBB 1 Bright A_SetPitch (pitch - 2.0)
		MISG CDE 3 Bright
		MISG F 2
		MISG AGHIAA 2
		MISG B 0 A_ReFire
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory ("RLUpgrade", 1, 1)
		Goto Ready2
		TNT1 A 0 A_JumpIfInventory ("AmmoRocketLauncher", 1, 1)
        Goto DryFire
		TNT1 A 0 A_JumpIfTargetInLOS ("TrueAltFire", 1.0, JLOSF_PROJECTILE)
		MISG A 1
		TNT1 A 0 A_Refire
	    Goto Ready2
	TrueAltFire:
		MISG B 0 A_PlaySound ("RocketLauncher/Fire", CHAN_WEAPON)
		MISG B 0 A_FireCustomMissile ("FDRocketPoweredSeeker",-5,0)
		MISG B 0 A_FireCustomMissile ("FDRocketPoweredSeeker",-2.5,0)
		MISG B 0 A_FireCustomMissile ("FDRocketPoweredSeeker")
		MISG B 0 A_FireCustomMissile ("FDRocketPoweredSeeker",2.5,0)
		MISG B 0 A_FireCustomMissile ("FDRocketPoweredSeeker",5,0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory ("AmmoRocketLauncher", 1, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_Recoil (cos (pitch) * 5.8)
		MISG BBB 1 Bright A_SetPitch (pitch - 2.0)
		MISG CDE 3 Bright
		MISG F 2
		MISG AGHIAA 2
		MISG B 0 A_ReFire
		Goto Ready2
	}
}

Actor FDRocketPowered : FDRocket {
	Speed 60
	Damage 9
	Scale 0.5
	States {
	Spawn:
		MISL A 2 Bright Light (ROCKET) A_CustomMissile ("Rocket1TrailSmall", random (-2, 2), random (-2, 2))
		Loop
	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_ChangeFlag ("ForceXYBillboard", 1)
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile ("FDShrapnel", random (0, 16), random (-8, 8), random (-360, 360), 2, random (10, 70))
		TNT1 A 0 A_Explode (45, 45)
		XPL1 ABCEDFGHIJKLMNOPQRSTUVWXYZ 1 Bright Light (ROCKET_X1)
		XPL2 ABCEDFGHIJK 1 Bright Light (ROCKET_X1)
		XPL2 LMNOPQRSTU 1 Bright Light (ROCKET_X1)
		stop
	}
}

Actor Rocket1TrailSmall : Rocket1Trail { Scale 0.05 }

Actor FDRocketPoweredSeeker : FDRocketPowered {
	+SeekerMissile +ScreenSeeker
	States {
	Spawn:
		TNT1 A 0 A_SeekerMissile (4, 8, SMF_LOOK|SMF_PRECISE|SMF_CURSPEED, 192, 5)
		MISL A 2 Bright Light (ROCKET) A_CustomMissile ("Rocket1TrailSmall", random (-2, 2), random (-2, 2))
		Loop
	}
}