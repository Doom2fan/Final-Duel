// Credits:
// DECORATE: Alex_Mercer, parts from zandronum.pk3
// Sprites: zrrion the insect, ID Software, Midway, Blox, Freedoom
// Sounds: Beautiful Doom
// --------------------------------------------------------------------------
//
// Rocket launcher
//
// --------------------------------------------------------------------------
Actor RLUpgrade : Boolean { }
ACTOR FDRocketLauncher : FDWeapon 30057
{
	//$Category Final Duel-Weapons
	//$Sprite LAUNA0
	Tag "$TAGRLAUNCHER"
	Game Doom
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "RocketAmmo2"
	+WEAPON.NOAUTOFIRE
	+AMMO_OPTIONAL
	+NOALERT
	Weapon.SisterWeapon FDRocketLauncherPowered
	weapon.upsound "weapons/rlan/draw"
	Inventory.PickupMessage "$GOTLAUNCHER"
	States
	{
	Ready:
	Ready2:
		TNT1 A 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		MISG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(0,"DeselectDEAD")
		MISG A 1 A_Lower
		Loop
	Select:
		MISG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		MISG B 0 A_PlaySound("weapons/rlan/fire", CHAN_WEAPON)
		MISG B 0 A_FireCustomMissile("FDRocket")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 5.0)
		MISG BBBB 1 A_SetPitch(pitch - 2.5)
		MISG CDEF 4
		MISG B 0 A_ReFire
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("RLUpgrade", 1, 1)
		Goto Ready2
		TNT1 A 0 A_JumpIfTargetInLOS("TrueAltFire", 1.0, JLOSF_PROJECTILE)
		MISG A 1
		TNT1 A 0 A_Refire
	    Goto Ready2
	TrueAltFire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		MISG B 0 A_PlaySound("weapons/rlan/fire", CHAN_WEAPON)
		MISG B 0 A_FireCustomMissile("FDRocketSeeker")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 5.0)
		MISG BBBB 1 A_SetPitch(pitch - 2.5)
		MISG CDEF 4
		MISG B 0 A_ReFire
		Goto Ready2
	DryFire:
		MISG A 0 A_PlaySound("weapons/empty")
		Goto Ready2
	Spawn:
		LAUN A -1
		Stop
	}
}

ACTOR FDRocket Replaces Rocket
{
	Game Doom
	Radius 11
	Height 8
	Speed 20
	Damage 40
	Projectile
	+RANDOMIZE
	+DEHEXPLOSION
	-ROCKETTRAIL
	+DONTSPLASH
	deathsound "weapons/rlan/explosion"
	Obituary "$OB_MPROCKET"
	DamageType Rocket
	States
	{
	Spawn:
		MISL A 2 Bright light(ROCKET) A_CustomMissile("Rocket1Trail",random(-2,2),random(-2,2))
		Loop
	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		TNT1 AAAAAAA 0 A_custommissile("SmokingPiece",random(0,16),random(-8,8),random(-360,360),2,random(10,70))
		REXP G 2 bright light(ROCKET_X2) A_Explode
		REXP HIJ 2 bright light(ROCKET_X2)
		REXP KLMNO 2 bright light(ROCKET_X1)
		REXP PQRSTUVW 2 bright
		stop
	}
}
Actor FDRocketSeeker : FDRocket
{
	+SEEKERMISSILE
	+SCREENSEEKER
	States
	{
	Spawn:
		TNT1 A 0 A_SeekerMissile(4,8,SMF_LOOK|SMF_PRECISE|SMF_CURSPEED,192,5)
		MISL A 2 Bright light(ROCKET) A_CustomMissile("Rocket1Trail",random(-2,2),random(-2,2))
		Loop
	}
}
ACTOR FDRocketLauncherPowered : FDRocketLauncher
{
	Weapon.SisterWeapon FDRocketLauncher
	+Powered_Up
	States
	{
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		MISG B 0 A_PlaySound("weapons/rlan/fire", CHAN_WEAPON)
		MISG B 0 A_FireCustomMissile("FDRocketPowered",-5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPowered",-2.5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPowered")
		MISG B 0 A_FireCustomMissile("FDRocketPowered",2.5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPowered",5,0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 5.8)
		MISG BBBB 1 A_SetPitch(pitch - 1.5)
		MISG CDEF 4
		MISG B 0 A_ReFire
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("RLUpgrade", 1, 1)
		Goto Ready2
		TNT1 A 0 A_JumpIfTargetInLOS("TrueAltFire", 1.0, JLOSF_PROJECTILE)
		MISG A 1
		TNT1 A 0 A_Refire
	    Goto Ready2
	TrueAltFire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		MISG B 0 A_PlaySound("weapons/rlan/fire", CHAN_WEAPON)
		MISG B 0 A_FireCustomMissile("FDRocketPoweredSeeker",-5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPoweredSeeker",-2.5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPoweredSeeker")
		MISG B 0 A_FireCustomMissile("FDRocketPoweredSeeker",2.5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPoweredSeeker",5,0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 5.8)
		MISG BBBB 1 A_SetPitch(pitch - 1.5)
		MISG CDEF 4
		MISG B 0 A_ReFire
		Goto Ready2
	}
}
ACTOR FDRocketPowered : FDRocket
{
	Speed 60
	Damage 8
	Scale 0.5
	States
	{
	Spawn:
		MISL A 2 Bright light(ROCKET) A_CustomMissile("Rocket1TrailSmall",random(-2,2),random(-2,2))
		Loop
	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		TNT1 AAA 0 A_custommissile("SmokingPiece",random(0,16),random(-8,8),random(-360,360),2,random(10,70))
		TNT1 A 0 A_Explode(25,25)
		REXP GHIJ 2 bright light(ROCKET_X2)
		REXP KLMNO 2 bright light(ROCKET_X1)
		REXP PQRSTUVW 2 bright
		stop
	}
}
actor Rocket1TrailSmall : Rocket1Trail { Scale 0.05 }
Actor FDRocketPoweredSeeker : FDRocketPowered
{
	+SEEKERMISSILE
	+SCREENSEEKER
	States
	{
	Spawn:
		TNT1 A 0 A_SeekerMissile(4,8,SMF_LOOK|SMF_PRECISE|SMF_CURSPEED,192,5)
		MISL A 2 Bright light(ROCKET) A_CustomMissile("Rocket1TrailSmall",random(-2,2),random(-2,2))
		Loop
	}
}