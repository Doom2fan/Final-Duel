// Credits:
// DECORATE: Alex_Mercer, parts from zandronum.pk3
// Sprites: zrrion the insect, ID Software, Midway, Blox, Freedoom
// Sounds: Beautiful Doom
Actor DroppedRocketLauncher : DroppedWeaponBase
{
	States
	{
	Spawn:
		LAUN A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("FDRocketLauncher")
		Stop
	}
}
Actor RLUpgrade : Boolean { }
Actor FDRocketLauncher : FDWeapon 30057
{
	//$Category Final Duel-Weapons
	//$Title Rocket Launcher
	//$Sprite LAUNA0
	Tag "$TAGRLAUNCHER"
	Game Doom
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "RocketAmmo2"
	Weapon.AmmoType2 "RocketAmmo2"
	+Weapon.NOAUTOFIRE
	+AMMO_OPTIONAL
	+ALT_AMMO_OPTIONAL
	+NOALERT
	Weapon.SisterWeapon FDRocketLauncherPowered
	Weapon.upsound "Weapons/rlan/draw"
	Inventory.PickupMessage "$GOTLAUNCHER"
	States
	{
	Ready:
		MSGS EDCBA 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory("DropDatWeapon",1,"DropWeapon")
		TNT1 A 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kicking")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		MISG A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0 A_TakeInventory("DropDatWeapon", 1)
		TNT1 A 0 A_FireCustomMissile("DroppedRocketLauncher", 0, 0, 0)
		TNT1 A 0 A_TakeInventory("FDRocketLauncher", 1)
		Stop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(0,"DeselectDEAD")
		MSGS ABCDE 1
		MISG A 0 A_Lower
		Wait
	Select:
		MISG A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		MISG B 0 A_PlaySound("Weapons/rlan/fire", CHAN_Weapon)
		MISG B 0 A_FireCustomMissile("FDRocket")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 5.0)
		MISG BBBB 1 Bright A_SetPitch(pitch - 2.5)
		MISG CDE 4 Bright
		MISG F 4
		MISG B 0 A_ReFire
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("RLUpgrade", 1, 1)
		Goto Ready2
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_JumpIfTargetInLOS("TrueAltFire", 1.0, JLOSF_PROJECTILE)
		MISG A 1
		TNT1 A 0 A_Refire
	    Goto Ready2
	TrueAltFire:
		MISG B 0 A_PlaySound("Weapons/rlan/fire", CHAN_Weapon)
		MISG B 0 A_FireCustomMissile("FDRocketSeeker")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 5.0)
		MISG BBBB 1 Bright A_SetPitch(pitch - 2.5)
		MISG CDE 4 Bright
		MISG F 4
		MISG B 0 A_ReFire
		Goto Ready2
	DryFire:
		MISG A 0 A_PlaySound("Weapons/empty")
		Goto Ready2
	Spawn:
		LAUN A -1
		Stop
	}
}

Actor FDRocket Replaces Rocket
{
	Radius 11
	Height 8
	Speed 30
	Damage 40
	Projectile
	+RANDOMIZE
	+DEHEXPLOSION
	-ROCKETTRAIL
	+DontSplash
	DeathSound "Weapons/rlan/explosion"
	Obituary "$OB_MPROCKET"
	SelfObituary "$OB_ROCKETSELF"
	DamageType Rocket
	States
	{
	Spawn:
		MISL A 2 Bright Light(ROCKET) A_CustomMissile("Rocket1Trail",random(-2,2),random(-2,2))
		Loop
	Death:
		//TNT1 A 0 A_CheckFloor("SpawnFire")
	XDeath:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_ChangeFlag("ForceXYBillboard",1)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("FDShrapnel",random(0,16),random(-8,8),random(-360,360),2,random(10,70))
		TNT1 A 0 A_Explode
		XPL1 ABCEDFGHIJKLMNOPQRSTUVWXYZ 1 Bright Light(ROCKET_X1)
		XPL2 ABCEDFGHIJK 1 Bright Light(ROCKET_X1)
		XPL2 LMNOPQRSTU 1 Bright Light(ROCKET_X1)
		stop
	SpawnFire:
		//TNT1 A 0 A_SpawnItemEx("FDRocketFire")
		Goto XDeath
	}
}
Actor FDRocketSeeker : FDRocket
{
	+SEEKERMISSILE
	+SCREENSEEKER
	States
	{
	Spawn:
		TNT1 A 0 A_SeekerMissile(5, 9, SMF_LOOK|SMF_PRECISE|SMF_CURSPEED, 192, 5)
		MISL A 2 Bright Light(ROCKET) A_CustomMissile("Rocket1Trail",random(-2,2),random(-2,2))
		Loop
	}
}
Actor FDRocketLauncherPowered : FDRocketLauncher
{
	Weapon.SisterWeapon FDRocketLauncher
	+Powered_Up
	States
	{
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		MISG B 0 A_PlaySound("Weapons/rlan/fire", CHAN_Weapon)
		MISG B 0 A_FireCustomMissile("FDRocketPowered",-5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPowered",-2.5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPowered")
		MISG B 0 A_FireCustomMissile("FDRocketPowered",2.5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPowered",5,0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 5.8)
		MISG BBBB 1 Bright A_SetPitch(pitch - 1.5)
		MISG CDE 4 Bright
		MISG F 4
		MISG B 0 A_ReFire
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("RLUpgrade", 1, 1)
		Goto Ready2
		TNT1 A 0 A_JumpIfTargetInLOS("TrueAltFire", 1.0, JLOSF_PROJECTILE)
		MISG A 1
		TNT1 A 0 A_Refire
	    Goto Ready2
	TrueAltFire:
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		MISG B 0 A_PlaySound("Weapons/rlan/fire", CHAN_Weapon)
		MISG B 0 A_FireCustomMissile("FDRocketPoweredSeeker",-5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPoweredSeeker",-2.5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPoweredSeeker")
		MISG B 0 A_FireCustomMissile("FDRocketPoweredSeeker",2.5,0)
		MISG B 0 A_FireCustomMissile("FDRocketPoweredSeeker",5,0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(cos(pitch) * 5.8)
		MISG BBBB 1 Bright A_SetPitch(pitch - 1.5)
		MISG CDE 4 Bright
		MISG F 4
		MISG B 0 A_ReFire
		Goto Ready2
	}
}
Actor FDRocketPowered : FDRocket
{
	Speed 60
	Damage 8
	Scale 0.5
	States
	{
	Spawn:
		MISL A 2 Bright Light(ROCKET) A_CustomMissile("Rocket1TrailSmall",random(-2,2),random(-2,2))
		Loop
	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_ChangeFlag("ForceXYBillboard",1)
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("FDShrapnel",random(0,16),random(-8,8),random(-360,360),2,random(10,70))
		TNT1 A 0 A_Explode(25,25)
		XPL1 ABCEDFGHIJKLMNOPQRSTUVWXYZ 1 Bright Light(ROCKET_X1)
		XPL2 ABCEDFGHIJK 1 Bright Light(ROCKET_X1)
		XPL2 LMNOPQRSTU 1 Bright Light(ROCKET_X1)
		stop
	}
}
Actor Rocket1TrailSmall : Rocket1Trail { Scale 0.05 }
Actor FDRocketPoweredSeeker : FDRocketPowered
{
	+SEEKERMISSILE
	+SCREENSEEKER
	States
	{
	Spawn:
		TNT1 A 0 A_SeekerMissile(4,8,SMF_LOOK|SMF_PRECISE|SMF_CURSPEED,192,5)
		MISL A 2 Bright Light(ROCKET) A_CustomMissile("Rocket1TrailSmall",random(-2,2),random(-2,2))
		Loop
	}
}